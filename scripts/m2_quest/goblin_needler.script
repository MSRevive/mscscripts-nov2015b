
#scope server

{
	const NEW_MODEL 1

	const NPC_BASE_EXP 50
	const SOUND_BOW weapons/bow/bow.wav

	const GOB_JUMPER 0
	const GOB_CHARGER 0

	const DMG_BOW $rand(15,25)
	const DMG_KICK $rand(5,10)

	const KICK_RANGE 64
	const KICK_HITCHANCE 90

	const FREQ_KICK 10.0

	setvard CAN_FIREBALL 0

	setvard NPC_RANGED 1

	setvard DROPS_CONTAINER 1
	setvard CONTAINER_DROP_CHANCE 10% 
	setvard CONTAINER_SCRIPT chests/quiver_of_gpoison

	const ARROW_SCRIPT proj_arrow_npc

	setvard ANIM_ATTACK shootorcbow
}

#include monsters/bgoblin

{ [override] [shared] goblin_spawn

	name a|Goblin Needler
	race goblin
	blood red
	hp 100
	if ( !NEW_MODEL )
	{
		setmodel monsters/goblin2.mdl
		width 32
		height 60

		setmodelbody 0 3 
		setmodelbody 1 4 
		setmodelbody 2 2 
		setmodelbody 3 0 

		setprop ent_me skin 0
	}
	else
	{
		setmodel monsters/goblin_new.mdl
		width 24
		height 50

		setmodelbody 0 1 
		setmodelbody 1 2 
		setmodelbody 2 2 
		setmodelbody 3 0 
		

		setprop ent_me skin 0 
	}

	roam 1

	hearingsensitivity 2


	setidleanim ANIM_IDLE
	setmoveanim ANIM_WALK

	
	

	callevent 0.01 gob_extras
}

{ gob_extras

	
	setvard MOVE_RANGE 2000
	setvard ATTACK_RANGE 2000
	setvard ATTACK_HITRANGE 2000

	setvard DROP_GOLD_AMT $rand(10,30)
}

{ grab_arrow
	setmodelbody 3 1
}

{ shoot_arrow

	local TARGET_DIST $get(ent_lastseen,range)
	local FINAL_TARGET $get(ent_lastseen,origin)
	vectoradd FINAL_TARGET $relpos($vec(0,0,0),$vec(0,0,TARGET_DIST))
	divide TARGET_DIST 100
	setangle add_view.pitch TARGET_DIST

	tossprojectile view 900 DMG_BOW 2 ARROW_SCRIPT (0,0,32)
	callexternal $get(ent_lastprojectile,id) ext_lighten 0.4

	setmodelbody 3 0

	playsound 2 SOUND_BOW
}

{ game_death
	setmodelbody 3 0
}

{ gob_hunt
	if $get(NPCATK_TARGET,isalive)
	if $get(NPCATK_TARGET,range) < KICK_RANGE
	if game.time > NEXT_KICK
	setvard NEXT_KICK game.time
	playanim break
	setmodelbody 3 0
	add NEXT_KICK FREQ_KICK
	setvard ANIM_ATTACK ANIM_KICK
}

{ kick_land
	dodamage NPCATK_TARGET KICK_RANGE DMG_KICK KICK_HITCHANCE blunt
	setvard KICK_ATTACK 1
	setvard ANIM_ATTACK ANIM_BOW
	callevent npcatk_flee NPCATK_TARGET 1024 3.0
}

{ npcatk_flee
	movespeed 2.0
}

{ npcatk_stopflee
	movespeed 1.0
}

{ game_dodamage

	if ( KICK_ATTACK )
	{
		if PARAM1
		if $get(PARAM2,range) < KICK_RANGE
		applyeffect PARAM2 effects/heavy_stun 5
	}
	setvard KICK_ATTACK 0
}

