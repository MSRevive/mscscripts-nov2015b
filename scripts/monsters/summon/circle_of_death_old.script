{
	const SEAL_MODEL weapons/magic/seals.mdl
	const SEAL_OFS 4
	const SOUND_MANIFEST magic/temple.wav
	const SOUND_PULSE magic/pulsemachine_noloop.wav
	const SOUND_FADE magic/frost_reverse.wav
	const FX_SPRITE skull.spr
	const PULSE_PLAYTIME 1.7
	const CIRCLE_RADIUS 180
	const LIGHT_DIE 1.6

	precache FX_SPRITE
}	

#include monsters/base_npc

{ eventname game_dynamically_created 

	add FX_SPAMMERS 1

	setvard MY_OWNER PARAM1
	setvard MY_DURATION PARAM2
	setvard MY_BASE_DAMAGE PARAM3

	setvard OWNER_ISPLAYER $get(MY_OWNER,isplayer)

	if ( !OWNER_ISPLAYER )
	{
		setvard MY_SKILL PARAM4
	}
	else
	{
		setvard MY_SKILL none
	}
	setvard GAME_PVP game.pvp

	setvard MY_OWNER_RACE $get(PARAM1,race)

	race MY_OWNER_RACE


	callevent MY_DURATION circle_end

	

     
	
	
	
	
	
}

{ npc_spawn

	name a|Circle of Death
	hp 10000
	fov 359
	invincible 1
	height 2
	width 2
	fly 1
	float 1
	takedmg all 0.0
	gravity 0.0
	blood none
	setmodel none
	setsolid none
	setbbox none

	nopush 1

	setvard PLAYING_DEAD 1

	setvard CIRCLE_ON 1

	callevent 0.1 make_seal

	callevent 1.0 circle_go

	callevent 0.1 circle_hum
}

{ make_seal

	race MY_OWNER_RACE
	local SEAL_POS $get(ent_me,origin)
	local GROUND_Z $get_ground_height(SEAL_POS)
	local SEAL_Z $vec.z(SEAL_POS)
	local GROUND_DIST GROUND_Z
	subtract GROUND_DIST SEAL_Z
	
	subract GROUND_DIST 2
	vectoradd SEAL_POS $relpos($vec(0,0,0),$vec(0,0,GROUND_DIST)) 
	
	createnpc monsters/summon/seal_maker SEAL_POS SEAL_MODEL MY_DURATION SEAL_OFS
}

{ circle_hum

	

   	if ( !CIRCLE_UP )
	{
		
		local CLIENT_DURATION MY_DURATION
		subtract CLIENT_DURATION 0.2 
		clientevent new all_in_sight $currentscript CLIENT_DURATION $get(ent_me,index) CIRCLE_RADIUS
		local MY_ORIGIN $get(ent_me,origin)
		cleffect light new MY_ORIGIN CIRCLE_RADIUS (128,128,255) MY_DURATION
		setvard THIS_SCRIPT_CLIENT_ID game.script.last_sent_id
		setvard CIRCLE_UP 1
	}

	if ( DID_WINDUP ) playsound 0 10 SOUND_PULSE
	if ( !DID_WINDUP )
	{
		playsound 0 10 SOUND_MANIFEST
		setvard DID_WINDUP 1
	}


	if CIRCLE_ON

	callevent PULSE_PLAYTIME circle_hum
}

{ circle_go

	if CIRCLE_ON

	callevent 0.5 circle_go
	setvard CIRCLE_TARGETS $get_tsphere(enemy,CIRCLE_RADIUS)
	if CIRCLE_TARGETS isnot none
	calleventloop $get_token_amt(CIRCLE_TARGETS) damage_targets
}

{ damage_targets
	local CUR_TARG $get_token(CIRCLE_TARGETS,game.script.iteration)
	

	if ( OWNER_ISPLAYER )
	{
		if !GAME_PVP
		if $get(CUR_TARG,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	xdodamage CUR_TARG direct MY_BASE_DAMAGE 100% MY_OWNER MY_OWNER MY_SKILL magic
}

{ circle_end

	subtract FX_SPAMMERS 1

	clientevent remove all THIS_SCRIPT_CLIENT_ID
	setvard CIRCLE_ON 0

	clientevent remove all $currentscript	
	playsound 0 10 SOUND_FADE
	callevent 0.5 circle_remove
}

{ circle_remove

	deleteent ent_me
}


{ [client]

	const OFS_POS			128
	const OFS_NEG			-128
	const OFSZ_POS			256
	const OFSZ_NEG			-10
	
	const LIGHT_PLAYER_SCALE	0.3
	const LIGHT_DROPPED_SCALE	0.5
}

{ [client] client_activate

	setvard sfx.duration PARAM1
	setvard sfx.npcid PARAM2
	setvard sfx.radius PARAM3

	setcallback render enable

	setvard RAIN_SPRITES 1

	setvard SEAL_DROP_COUNTER 0

	setvard ROT_COUNT 0
	
	callevent 0.1 rain_go

	callevent PARAM1 effect_die
}


{ [client] rain_go
	

	if RAIN_SPRITES
	callevent createsprite
	callevent 1.0 rain_go
}

{ [client] createsprite

		local g.pos $getcl(sfx.npcid,origin)
		local SEAL_POS g.pos
		vectoradd SEAL_POS $relpos($vec(0,0,0),$vec(0,0,-64))
		
		
		cleffect remove LAST_LIGHT
		cleffect light new g.pos 196 (255,0,0) 0.9
		setvard LAST_LIGHT game.script.last_light_id
		cleffect tempent model SEAL_MODEL SEAL_POS setup_seal
		cleffect tempent model FX_SPRITE g.pos setup_flame
}

{ [client] setup_seal

	cleffect tempent set_current_prop death_delay 	 sfx.duration
    cleffect tempent set_current_prop bouncefactor 	0
	cleffect tempent set_current_prop scale 	1.0
	cleffect tempent set_current_prop framerate	3
	cleffect tempent set_current_prop frames	15
	cleffect tempent set_current_prop rendercolor 	(255,0,0)
	cleffect tempent set_current_prop gravity 	-1.0
	cleffect tempent set_current_prop collide 	none
    cleffect tempent set_current_prop velocity 	(0,0,-2)
	cleffect tempent set_current_prop body SEAL_OFS
}

{ [client] setup_flame

	cleffect tempent set_current_prop death_delay 	 sfx.duration
    cleffect tempent set_current_prop bouncefactor 	0
	cleffect tempent set_current_prop scale 	2.0
	cleffect tempent set_current_prop framerate	15
	cleffect tempent set_current_prop frames	15
	cleffect tempent set_current_prop rendercolor 	(255,0,0)
	cleffect tempent set_current_prop gravity 	0.0
	cleffect tempent set_current_prop collide 	none
    cleffect tempent set_current_prop velocity 	(0,0,-1)
	cleffect tempent set_current_prop rendermode add
	cleffect tempent set_current_prop renderamt 255
	
}

{ [client] effect_die

	cleffect remove LAST_LIGHT
	setvard RAIN_SPRITES 0
	removescript
}

{ game_death

	setalive 1
}
