

#scope server

{ [shared]
	const ANIM_IDLE idle
	const ANIM_CLOSE close
	const ANIM_OPEN open

	
	const REJECT_ITEMS "mana_bravery;fist_bare;mana_mpotion;health_apple;health_mpotion;health_lpotion;health_spotion;proj_;pack_;sheath_;magic_hand_;item_tk_"

	const BANK_STRINGS "b1;b2;b3;b4;b5;b6;b7;b8;b9"

	const MAX_BANK_STRING_SIZE 158 

	setvard GALAT_CHEST 1

	setvard NPC_ECHO_ITEMS 1
	setvard NPC_NO_REPORT_ITEMS 1
}

{ [shared] game_spawn
	name Galat's Wondrous Chest of Storage
	hp 1
	invincible  2
	width 20
	height 30
	setmodel misc/treasure.mdl
	setmodelbody 0 2 
	nopush 1

	setvard PLAYING_DEAD 1

	setidleanim	ANIM_IDLE

	gravity 0.1 

	menu.autoopen 1

	setvard IMMUNE_PUSH 1
}

{ game_menu_getoptions

	if ( AM_BUSY ) dplayermessage PARAM1 Galat chest is busy or in use by another player.
	if !AM_BUSY

	setvard AM_BUSY 1
	menu.autoopen 0

	setvard CUR_USER PARAM1
	setvard CHEST_USER PARAM1

	setvard N_DEPOSIT_OPTIONS 0

	setvard CUR_USER_LHAND_ID $get(CUR_USER,scriptvar,'PLR_LEFT_HAND')
	setvard CUR_USER_RHAND_ID $get(CUR_USER,scriptvar,'PLR_RIGHT_HAND')

	setvard BANK_CURRENT full
	setvard BANK_TOTAL_LEN 0
	calleventloop $get_token_amt(BANK_STRINGS) find_free_bank
	if ( BANK_CURRENT isnot full ) setvard BANK_STR $get_quest_data(CHEST_USER,BANK_CURRENT)


	if ( BANK_CURRENT equals full ) 
	{
		local reg.mitem.title "Chest is Full"
		local reg.mitem.type disabled
		menuitem.register
	}
	else
	{
		if ( $get(CUR_USER_LHAND_ID,exists) ) callevent add_deposit_option CUR_USER_LHAND_ID
		if ( $get(CUR_USER_RHAND_ID,exists) ) callevent add_deposit_option CUR_USER_RHAND_ID
		if ( N_DEPOSIT_OPTIONS == 0 ) infomsg CHEST_USER "Galat's wondrous Chest of Storage" "Be sure to place any items you wish to store in your hands."
	}

	if ( BANK_TOTAL_LEN >= $get_token_amt(BANK_STRINGS) )
	{
		if ( $get(PARAM1,numitems) >= G_MAX_ITEMS )
		{
			infomsg PARAM1 "Can't Withdaw Items" "Your inventory is full."
			local reg.mitem.title "(Inventory Full)"
			local reg.mitem.type disabled
			menuitem.register
			local EXIT_SUB 1
		}
		if !EXIT_SUB
	
		local reg.mitem.title "Withdraw Items"
		local reg.mitem.type callback
		local reg.mitem.callback  withdraw_items
		menuitem.register
	}
}

{ find_free_bank
	if BANK_CURRENT equals full
	local CUR_BANK $get_token(BANK_STRINGS,game.script.iteration)
	setvard BANK_STR $get_quest_data(CHEST_USER,CUR_BANK)
	add BANK_TOTAL_LEN $len(BANK_STR)
	if ( $len(BANK_STR) < MAX_BANK_STRING_SIZE ) setvard BANK_CURRENT CUR_BANK
}

{ game_menu_cancel
	callevent 2.0 set_not_busy
	menu.autoopen 1
	callevent erase_store
}

{ add_deposit_option 
	setvard DITEM_SCRIPTNAME $get(PARAM1,itemname)
	

	local DITEM_FULLNAME  $get(PARAM1,name) 

	setvard REJECT_ITEM 0
	setvard REJECT_CHECK DITEM_SCRIPTNAME
	calleventloop $get_token_amt(REJECT_ITEMS) check_reject
	if ( $get(PARAM1,scriptvar,'NO_BANK') ) setvard REJECT_ITEM 1 
	if ( REJECT_ITEM )
	{
		if DITEM_SCRIPTNAME isnot fist_bare
		local RETURN_STR "Galat Chest: Cannot deposit expendable, perishable, ammunition, ticket, or container item: ("
		stradd RETURN_STR DITEM_FULLNAME
		stradd RETURN_STR ")"
		dplayermessage CHEST_USER RETURN_STR
	}

	if ( $get(PARAM1,scriptvar,'ITEM_IN_EXCHANGE' )
	{
		dplayermessage CHEST_USER DITEM_FULLNAME is currently in ownership exchange and cannot be deposited. Please try again later.
		setvard REJECT_ITEM 1 
	}
	if !REJECT_ITEM 

	local DITEM_TITLE "Deposit "
	stradd DITEM_TITLE DITEM_FULLNAME

	add N_DEPOSIT_OPTIONS 1

	local reg.mitem.title DITEM_TITLE
	local reg.mitem.type callback
	local reg.mitem.data $get(PARAM1,id)
	local reg.mitem.callback  deposit_item
	menuitem.register
}

{ check_reject
	if !REJECT_ITEM
	local CUR_STR $get_token(REJECT_ITEMS,game.script.iteration)
	if ( REJECT_CHECK contains CUR_STR ) setvard REJECT_ITEM 1
	if ( REJECT_CHECK equals CUR_STR ) setvard REJECT_ITEM 1

}

{ deposit_item 

	if game.serverside

	setvard LOCK_TARGET PARAM1
	callexternal LOCK_TARGET ext_removed_effects lock
	applyeffect LOCK_TARGET effects/effect_templock LOCK_TARGET
	callevent 1.0 release_inv_lock

	setvard T_OWNER_ID PARAM1
	setvard T_ITEM_ID PARAM2

	setvard ITEM_TO_REMOVE PARAM2
	setvard DITEM_SCRIPTNAME $get(PARAM2,itemname)
	setvard DITEM_FULLNAME $get(PARAM2,name)

	callexternal ITEM_TO_REMOVE item_banked 
	callclitemevent ITEM_TO_REMOVE game_putinpack 
	callevent 0.2 remove_item 

	callevent 1.0 deposit_item2
}

{ deposit_item2

	if ( $get(ITEM_TO_REMOVE,exists) )
	{
		infomsg CHEST_USER "Galat's wondrous Chest of Storage" "Error: Can't access item - not stored"
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	callevent deposit_item3 T_OWNER_ID T_ITEM_ID
}

{ deposit_item3
	if ( BANK_STR equals '0' ) setvard BANK_STR ''
	token.add BANK_STR DITEM_SCRIPTNAME
	quest set CHEST_USER BANK_CURRENT BANK_STR
	local MSG_STR "Your "
	stradd MSG_STR DITEM_FULLNAME
	stradd MSG_STR " has been added to the Galat Chest."
	infomsg CHEST_USER "Galat's wondrous Chest of Storage" MSG_STR
	playsound 2 10 Items/creak.wav
	playanim once ANIM_OPEN
	callevent 0.5 close_chest
}

{ release_inv_lock
	callexternal LOCK_TARGET ext_remove_lock
}

{ remove_item
	deleteent ITEM_TO_REMOVE
}

{ close_chest
	playanim once ANIM_CLOSE
	callevent 2.0 set_not_busy
	menu.autoopen 1
}

{ withdraw_items
	callevent erase_store
	setvard STORENAME galat_
	stradd STORENAME $get(CHEST_USER,steamid)
	createstore STORENAME

	calleventloop $get_token_amt(BANK_STRINGS) build_stores
	offerstore STORENAME CHEST_USER inv trade	
}


{ ext_player_got_item 
	setvard RITEM_SCRIPTNAME $get(PARAM1,itemname)
	setvard RITEM_OWNER PARAM2
	setvard ITEM_REMOVED 0
	calleventloop $get_token_amt(BANK_STRINGS) find_and_remove_item

	if ( !ITEM_REMOVED )
	{
	
		callexternal PARAM1 game_putinpack 
		callclitemevent ITEM_TO_REMOVE game_putinpack 
		deleteent PARAM1
	}
}

{ find_and_remove_item
	if !ITEM_REMOVED
	local CUR_BANK_STRING $get_token(BANK_STRINGS,game.script.iteration)
	local CUR_BANK_ITEMS $get_quest_data(RITEM_OWNER,CUR_BANK_STRING)
	local ITEM_IDX $get_find_token(CUR_BANK_ITEMS,RITEM_SCRIPTNAME)

	if ITEM_IDX > -1

	token.del CUR_BANK_ITEMS ITEM_IDX
	quest set RITEM_OWNER CUR_BANK_STRING CUR_BANK_ITEMS
	setvard ITEM_REMOVED 1
}

{ build_stores
	local CUR_BANK_STRING $get_token(BANK_STRINGS,game.script.iteration)
	setvard CUR_BANK_ITEMS $get_quest_data(CHEST_USER,CUR_BANK_STRING)
	if ( $len(CUR_BANK_ITEMS) > 1 ) calleventloop $get_token_amt(CUR_BANK_ITEMS) add_to_chest
}

{ add_to_chest
	local CUR_ITEM $get_token(CUR_BANK_ITEMS,game.script.iteration)
	addstoreitem STORENAME CUR_ITEM 1 0
}

{ trade_success
	playsound 2 10 Items/creak.wav
	playanim hold ANIM_OPEN
	callexternal players ext_bank_lock
}

{ trade_done
	playanim once ANIM_CLOSE
	setidleanim ANIM_IDLE
	callevent erase_store
	callevent 2.0 set_not_busy
	menu.autoopen 1
}

{ erase_store
	npcstore.remove STORENAME
	
	
}

{ game_dynamically_created 


	local SUMMONER_YAW $get(PARAM1,angles.yaw)

	if ( !$get(PARAM1,isplayer) ) setangle face $vec(0,SUMMONER_YAW,0)

	if $get(PARAM1,isplayer)

	playsound 0 10 magic/spawn.wav

	setangle face $vec(0,$neg(SUMMONER_YAW),0)

	callexternal GAME_MASTER gm_fade_in $get(ent_me,id) 5

	

	local OTHER_CHEST $get_by_name(galat_bank1)
	if ( $get(OTHER_CHEST,exists) )
	{
		setorigin OTHER_CHEST $get(ent_me,origin)
		deleteent ent_me
	}
	else
	{
		name_unique galat_bank1
		setsolid none
		setvard SCAN_AREA $get_tsphere(any,64)
		if SCAN_AREA isnot none
		calleventloop $get_token_amt(SCAN_AREA) move_monsters 
	}
}

{ move_monsters
	local CUR_TARG $get_token(SCAN_AREA,game.script.iteration)
	if !$get(CUR_TARG,isplayer)
	if !$get(CUR_TARG,scriptvar,'GALAT_CHEST')
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(game.monster.origin,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(10,1000,110)) override
}

{ fade_in_done 
	setprop ent_me rendermode 0
	setprop ent_me renderamt 255
}

#include [server] monsters/debug

{ set_not_busy
	setvard AM_BUSY 0
}
