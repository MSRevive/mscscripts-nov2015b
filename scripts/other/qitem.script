//Special dynamic quest items, pick up when touched
//- Quest items without cluttering player inventory.
//- These track, but don't save (save function via quest data pending)

//current qitem codes:
//ap - "a|Golden Apple"
//bs - "a|Bag of Salt"
//bp - "a|Bag of Pepper"
//bm - "a|Barrel of Mead"
//la - "Sylphiel's Ladel"
//tnt - "a stick of|Dwarven Explosives"
//gen1 - "a|Package of Something" //(these use the generic item package model)
//gen2 - "a|Package of Something" //(name intended to be changed via second createdyn param or title property)
//gen3 - "a|Package of Something"
//gen3 - "a|Package of Something"

#scope server

{
	setvard GLOW_COLOR (255,200,0)
	setcallback touch enable
	setvar ANIM_QITEM_IDLE idle_syph
}

{ game_precache
	//be sure to precache any new item models here
	precache monsters/dwarf_bomber_tnt.mdl
}

{ game_spawn
	setmodel misc/p_misc.mdl
	width 16
	height 16
	setsolid trigger
	setidleanim ANIM_QITEM_IDLE
	invincible 1
	nopush 1
	setvard PLAYING_DEAD 1 //not a valid target
	callevent 1.0 glow_loop
}

{ glow_loop
	if IS_ACTIVE
	callevent 10.0 glow_loop
	effect glow ent_me GLOW_COLOR 16.0 9.0 -1
}

{ game_touch
	if IS_ACTIVE
	if game.time > NEXT_TOUCH
	setvard NEXT_TOUCH game.time
	add NEXT_TOUCH 1.0

	if $get(PARAM1,isplayer)
	setvard QUEST_PLAYER PARAM1			
	callevent found_item
}

{ found_item
	if IS_ACTIVE
	setvard IS_ACTIVE 0
	playsound 0 10 items/ammopickup1.wav
	local OUT_MSG "You find "
	stradd OUT_MSG $get(ent_me,name.full)
	gplayermessage QUEST_PLAYER You acquire $get(ent_me,name.full)
	infomsg QUEST_PLAYER "QUEST ITEM FOUND" OUT_MSG
	helptip QUEST_PLAYER questitem "QUEST ITEM" "This is a special quest item that will not appear in your inventory."
	callexternal QUEST_PLAYER ext_got_quest_item ITEM_TYPE
	callevent 0.5 remove_me
}

{ remove_me
	deleteent ent_me
}

{ game_postspawn //PARAM1 = (name|default), PARAM2 = DmgMulti, PARAM3 = HPMulti, PARAM4 = pass_params

	if ( !AM_SUMMONED ) callevent set_item_type $lcase(PARAM4)

	if PARAM1 isnot default
	name PARAM1
	setvard CUSTOM_NAME 1
}

{ set_item_type

	setvard IS_ACTIVE 1
	setvard ITEM_TYPE PARAM1
	if ( ITEM_TYPE equals ap )
	{
		if ( !CUSTOM_NAME ) name "a|Golden Apple"
		setmodelbody 0 72
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals bs )
	{
		if ( !CUSTOM_NAME ) name "a|Bag of Salt"
		setmodelbody 0 73
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals bp )
	{
		if ( !CUSTOM_NAME ) name "a|Bag of Pepper"
		setmodelbody 0 74
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals km )
	{
		if ( !CUSTOM_NAME ) name "a|Barrel of Mead"
		setmodelbody 0 75
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals la )
	{
		if ( !CUSTOM_NAME ) name "Sylphiel's Ladel"
		setmodelbody 0 76
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals gen1 )
	{
		if ( !CUSTOM_NAME ) name "a|Package of Something #1"
		setmodelbody 0 17
		setidleanim package_floor_idle
		setvard ANIM_QITEM_IDLE package_floor_idle
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals gen2 )
	{
		if ( !CUSTOM_NAME ) name "a|Package of Something #2"
		setmodelbody 0 17
		setidleanim package_floor_idle
		setvard ANIM_QITEM_IDLE package_floor_idle
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals gen3 )
	{
		if ( !CUSTOM_NAME ) name "a|Package of Something #3"
		setmodelbody 0 17
		setidleanim package_floor_idle
		setvard ANIM_QITEM_IDLE package_floor_idle
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals gen4 )
	{
		if ( !CUSTOM_NAME ) name "a|Package of Something #4"
		setmodelbody 0 17
		setidleanim package_floor_idle
		setvard ANIM_QITEM_IDLE package_floor_idle
		local L_DID_INIT 1
	}
	else if ( ITEM_TYPE equals tnt )
	{
		if ( !CUSTOM_NAME ) name "a stick of|Dwarven Explosives"
		setvard GLOW_COLOR $vec(0,255,0)
		setprop ent_me movetype const.movetype.bounce
		//setvard TNT_MODEL monsters/dwarf_bomber_tnt.mdl //be sure you precach this, if creating tnt
		setvard SLOW_COUNT 0
		callevent 0.1 slow_down_loop
		//setmodel TNT_MODEL
		setmodelbody 0 77
		setidleanim tnt_idle
		setvard ANIM_QITEM_IDLE tnt_idle
		local L_DID_INIT 1
		setmonsterclip 0
	}

	if !L_DID_INIT

	if ( !CUSTOM_NAME ) name "an|Unknown Quest Item"
	setmodelbody 0 17
	setidleanim package_floor_idle
	setvard ANIM_QITEM_IDLE package_floor_idle

	//local OUT_MSG "m2_quest/sylphiels_stuff cannot find type: "
	//stradd OUT_MSG ITEM_TYPE
	//infomsg all "MAPPING ERROR" OUT_MSG
}

{ slow_down_loop
	local CUR_VEL $get(ent_me,velocity)
	vectormultiply CUR_VEL $vec(0.5,0.5,0.5)
	add SLOW_COUNT 1
	if ( SLOW_COUNT >= 20 ) local CUR_VEL $vec(0,0,0)
	setvelocity ent_me CUR_VEL
	if CUR_VEL isnot $vec(0,0,0)
	callevent 0.5 slow_down_loop
}

{ game_dynamically_created //<type> [name]
	setvard AM_SUMMONED 1
	if ( PARAM2 !startswith PARAM )
	{
		name PARAM2
		setvard CUSTOM_NAME 1
	}
	callevent set_item_type $lcase(PARAM1)
}

