

#scope server
{
	setvard NPC_NO_PLAYER_DMG 1
	setvard NPC_BATTLE_ALLY 1
	const NPC_FIGHTS_NPCS 1

	saytextrange 2048

	setvard NO_STUCK_CHECKS 1

	const AI_NO_TARGET_STRING unset 

	setvard NPC_ALLY_RESPONSE_RANGE 4096 

	const ALLY_MIN_DISTANCE 42 
	const ALLY_FOLLOW_CLOSE_DIST 60 
	const ALLY_FOLLOW_NORM_DIST 128 
	const ALLY_MOVE_AWAY_DIST 64 
	setvard ALLY_FOLLOW_PLR_DIST 128 

	const ALLY_REGEN_RATIO 0.1 

	const ALLY_MAX_JUMP_RANGE 600 
	const FREQ_ALLYJUMP $randf(4.0,5.0) 


	
	setvard ALLY_STUCK_COUNT 0
	setvard ALLY_AS_UNSTUCK_ANG 0

	const ANIM_ALLY_JUMP jump
	const SOUND_ALLY_JUMP monsters/orc/attack1.wav
	const ALLY_JUMP_THRESHOLD 150 
}

{ npcatk_hunt
	if !SUSPEND_AI
	if ALLY_FOLLOW_ON
	
	local GAME_TIME game.time
	setvard CYCLE_TIME 0.1

	
	if NPCATK_TARGET equals AI_NO_TARGET_STRING

	if ( GAME_TIME > ALLY_NEXT_FAS_CHECK )
	{
		if $get(ALLY_FOLLOW_PLR_ID,isalive)
		setvard ALLY_NEXT_FAS_CHECK GAME_TIME
		add ALLY_NEXT_FAS_CHECK 0.5
		callevent ally_stuck_check
	}

	
	if ( GAME_TIME > ALLY_NEXT_REGEN )
	{
		if $get(ent_me,hp) < $get(ent_me,maxhp)
		local ALLY_REGEN_AMT $get(ent_me,maxhp)
		multiply ALLY_REGEN_AMT ALLY_REGEN_RATIO
		givehp ent_me ALLY_REGEN_AMT
		setvard ALLY_NEXT_REGEN GAME_TIME
		add ALLY_NEXT_REGEN 1.0
	}
	if !SUSPEND_AI 

	
	if game.players > 0

	if ( GAME_TIME > ALLY_NEXT_FOLLOW_CHECK ) local FIND_NEW_FOLLOW 1
	if ( !$get(ALLY_FOLLOW_PLR_ID,isalive) ) local FIND_NEW_FOLLOW 1

	if ( FIND_NEW_FOLLOW )
	{
		if game.playersnb > 0
		setvard ALLY_NEXT_FOLLOW_CHECK GAME_TIME
		add ALLY_NEXT_FOLLOW_CHECK 10.0
		setvard ALLY_FPLAYER_LIST 0
		getplayersnb ALLY_FPLAYER_LIST
		setvard ALLY_FPLAYER_LIST $sort_entlist(ALLY_FPLAYER_LIST,range)
		setvard ALLY_FOLLOW_PLR_ID $get_token(ALLY_FPLAYER_LIST,0)
	}

	if $get(ALLY_FOLLOW_PLR_ID,isalive)

	local L_ALLY_RANGE $get(ALLY_FOLLOW_PLR_ID,range)
	local L_ALLY_ORG $get(ALLY_FOLLOW_PLR_ID,origin)
	local L_ALLY_RUN_RANGE 200
	if ( ALLY_IS_LEADER )
	{
		local L_ORG $get(ent_me,origin)
		local L_ALLY_RANGE $dist(L_ORG,ALLY_LEADER_DEST)
		local L_ALLY_ORG ALLY_LEADER_DEST
		local L_ALLY_RUN_RANGE 64
	}

	
	if ( GAME_TIME > NEXT_ALLYJUMP )
	{
		if L_ALLY_RANGE < ALLY_MAX_JUMP_RANGE

		local MY_Z $get(ent_me,origin.z)
		local TARG_Z $vec.z(L_ALLY_ORG)
		if ( !ALLY_IS_LEADER ) subtract TARG_Z 38 

		local Z_DIFF TARG_Z
		subtract Z_DIFF MY_Z

		

		if ( Z_DIFF > ALLY_JUMP_THRESHOLD )
		{
			callevent ally_hop Z_DIFF
			setvard NEXT_ALLYJUMP GAME_TIME
			add NEXT_ALLYJUMP FREQ_ALLYJUMP
		}
	}

	if ( L_ALLY_RANGE > L_ALLY_RUN_RANGE )
	{
		setmoveanim ANIM_RUN
	}
	else
	{
		setmoveanim ANIM_WALK
	}

	if ( $get(ALLY_FOLLOW_PLR_ID,range) > ALLY_MIN_DISTANCE )
	{
		

		if ( !ALLY_IS_LEADER )
		{
			setmovedest ALLY_FOLLOW_PLR_ID ALLY_FOLLOW_PLR_DIST
		}
		else
		{
			
			if ( game.time > ALLY_NEXT_LEADER_UPDATE )
			{
				setvard ALLY_NEXT_LEADER_UPDATE game.time
				add ALLY_NEXT_LEADER_UPDATE 0.5
				local L_MOVE_DEST $get(ALLY_FOLLOW_PLR_ID,origin)
				local L_TRACE_START L_MOVE_DEST
				local L_PLR_VIEW $get(ALLY_FOLLOW_PLR_ID,viewangles)
				vectoradd L_MOVE_DEST $relpos(L_PLR_VIEW,$vec(0,640,0))
				local L_MOVE_DEST $get_traceline(L_TRACE_START,L_MOVE_DEST,worldonly)
				vectorset L_MOVE_DEST z $get_ground_height(L_MOVE_DEST)
				local L_MY_WIDTH $get(ent_me,width)
				setmovedest L_MOVE_DEST L_MY_WIDTH

				setvard ALLY_LEADER_DEST L_MOVE_DEST

				
				local L_ORG $get(ent_me,origin)
				if ( $dist(L_MOVE_DEST,L_ORG) <= L_MY_WIDTH ) setangle face.y $vec.y(L_PLR_VIEW)

				if ( G_DEVELOPER_MODE )
				{
					local L_BEAM_Z L_MOVE_DEST
					vectoradd L_BEAM_Z z 32
					effect beam point lgtning.spr 20 L_MOVE_DEST L_BEAM_Z (255,0,255) 200 0 0.2
				}
			}
		}
	}
	else
	{
		
		setmovedest ALLY_FOLLOW_PLR_ID ALLY_MOVE_AWAY_DIST flee
	}
}

{ game_damaged
	if $get(PARAM1,relationship,ent_me) equals enemy
	setvard ALLY_NEXT_REGEN game.time
	add ALLY_NEXT_REGEN 20.0
}

{ ally_follow_close
	setvard ALLY_FOLLOW_PLR_DIST ALLY_FOLLOW_CLOSE_DIST
}

{ ally_follow_normal
	setvard ALLY_FOLLOW_PLR_DIST ALLY_FOLLOW_NORM_DIST
}

{ ally_hop 

	playsound 0 10 SOUND_ALLY_JUMP
	setvard ALLY_JUMP_STR PARAM1
	if ( ALLY_JUMP_STR > 100 ) callevent 0.5 ally_push_forward
	multiply ALLY_JUMP_STR 5
	callevent npcatk_suspend_ai 1.0
	if ( !ALLY_IS_LEADER )
	{
		setvard ALLY_FWD_JUMP_STR $get(ALLY_FOLLOW_PLR_ID,range)
	}
	else
	{
		local L_ORG $get(ent_me,origin)
		setvard ALLY_FWD_JUMP_STR $dist(L_ORG,ALLY_LEADER_DEST)
	
	}
	playanim critical ANIM_ALLY_JUMP
	callevent 0.1 ally_jump_boost
}

{ ally_jump_boost
	addvelocity ent_me $relvel(0,ALLY_FWD_JUMP_STR,ALLY_JUMP_STR)
}

{ ally_push_forward
	if ( !ALLY_IS_LEADER ) setmovedest ALLY_FOLLOW_PLR_ID 9999
	else setmovedest ALLY_LEADER_DEST 9999
	addvelocity ent_me $relvel(0,110,0)
	local MY_Z $get(ent_me,origin.z)
	local GROUND_POS $get_ground_height(game.monster.origin)
	subtract MY_Z GROUND_POS
	if MY_Z > 20
	callevent 0.1 ally_push_forward
}

{ ally_stuck_check
	
	local MY_ORG $get(ent_me,origin)
	local MY_DEST game.monster.movedest.origin
	local CUR_DIST $dist(MY_ORG,MY_DEST)
	if CUR_DIST >= game.monster.movedest.prox
	if ( CUR_DIST >= ALLY_OLD_DIST )
	{
		if ALLY_OLD_DIST != 0
		add ALLY_STUCK_COUNT 1
	}
	else
	{
		setvard ALLY_STUCK_COUNT 0
		setvard ALLY_AS_UNSTUCK_ANG $randf(-15.0,15.0)
		setvard ALLY_OLD_DIST 0
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	setvard ALLY_OLD_DIST CUR_DIST

	if ( ALLY_STUCK_COUNT > 1 )
	{
	
		local MOVE_DEST MY_ORG
		callevent npcatk_suspend_ai $randf(0.5,1.0)
		add ALLY_AS_UNSTUCK_ANG 36
		if ( ALLY_AS_UNSTUCK_ANG > 359 ) setvard ALLY_AS_UNSTUCK_ANG 0
		local MY_YAW $get(ent_me,angles.yaw)
		add MY_YAW ALLY_AS_UNSTUCK_ANG
		if ( MY_YAW > 359 ) setvard MY_YAW 0
		vectoradd MOVE_DEST $relpos($vec(0,MY_YAW,0),$vec(0,200,0))
		setmovedest MOVE_DEST 1
		setvard ALLY_STUCK_COUNT 0
		
	}
}

{ set_leader 
	setvard ALLY_IS_LEADER 1
	setvard ALLY_FOLLOW_ON 1
	if ( PARAM1 equals 0 ) setvard ALLY_IS_LEADER 0
}

{ set_follower
	setvard ALLY_FOLLOW_ON 1
}
