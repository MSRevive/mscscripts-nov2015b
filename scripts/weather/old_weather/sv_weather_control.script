//Weather control
//

{
   const WEATHER_CLEAR  clear
   const WEATHER_SNOW 	weather/sfx_weather_snow
   const WEATHER_RAIN   weather/sfx_weather_rain
   const WEATHER_STORM  weather/sfx_weather_rain_storm


   const WEATHER_FOG_GREEN weather/sfx_weather_snow
   const WEATHER_FOG_BROWN weather/sfx_weather_snow
   const WEATHER_FOG_RED weather/sfx_weather_snow
   const WEATHER_FOG_WHITE weather/sfx_weather_snow
   const WEATHER_FOG_BLUE weather/sfx_weather_snow
   const WEATHER_FOG_THICK weather/sfx_weather_snow
   const WEATHER_FOG_BLACK weather/sfx_weather_snow

	//new sfx scripts make older clients crash, so these have all been put into the snow script
   //const WEATHER_FOG_GREEN weather/sfx_weather_fog_green
   //const WEATHER_FOG_BROWN weather/sfx_weather_fog_brown
   //const WEATHER_FOG_RED weather/sfx_weather_fog_red
   //const WEATHER_FOG_WHITE weather/sfx_weather_fog_white
   //const WEATHER_FOG_BLUE weather/sfx_weather_fog_blue
   //const WEATHER_FOG_THICK weather/sfx_weather_fog_thick

   const WEATHER_STOP_TIME 	0	//Time given to a weather to die off before a change
   const WEATHER_START_TIME 	15	//Time given to a weather to go from zero to full effect
   const WEATHER_DUR_MIN	1	//Weather duration min (hours)
   const WEATHER_DUR_MAX	6	//Weather duration max (hours)

   setvarg GLOBAL_WEATHER WEATHER_CLEAR
   callexternal all worldevent_weather clear
	clientevent update all const.localplayer.scriptID clear_weather
}


//World Weather
//==========

{ worldevent_time		// Causes weather to expire and change over time

	local l.expiretime global.weather.start		//Find expire time
	add l.expiretime global.weather.duration

	local l.changeweather 0
	if( global.mstime.secs >= l.expiretime )	//Weather has expired
		local l.changeweather 1
	if( global.mstime.secs < global.weather.start )	//Time magically went backward
		local l.changeweather 1

	if l.changeweather

	if ( game.time > 10 )
	{
		callevent 45.0 random_map_weather				//Start new weather, JAN2010_15 - but give 45 secs for day/night to finish
	}
	else
	{
		callevent random_map_weather	//JAN2010_15 unless during map init, then clear instant
	}

	setvarg global.weather.start 	global.mstime.secs			//Set start time (now)
	setvarg global.weather.duration $rand(WEATHER_DUR_MIN,WEATHER_DUR_MAX)	//set duration
	multiply global.weather.duration 3600
}

{ random_map_weather		// Chooses random weather from list of valid weather in map

	local l.weathertotal $get_token_amt(global.map.weather)
	subtract l.weathertotal 1
	local l.weathernum $rand(0,l.weathertotal)
	local l.weathername $get_token(global.map.weather,l.weathernum)

	if ( G_DEVELOPER_MODE )
	{
		local MSG_LIST l.weathername
		stradd MSG_LIST " of "
		stradd MSG_LIST global.map.weather
		infomsg all "WEATHER SET TO" MSG_LIST
	}

	callevent game_playercmd_setweather l.weathername $randf(5,15) $randf(5,15)
}


{   game_playercmd_setweather		//Player initiated weather change console command
					//'setweather' uses this

	callevent weather_to_scriptname PARAM1
	setvard WEATHER_TYPE PARAM1

	if weather.name isnot '0'

	setvard NEW_WEATHER weather.name
 
	setvard NEW_WEATHER_STOPOLD_DELAY	WEATHER_STOP_TIME
	setvard NEW_WEATHER_START_DELAY		WEATHER_START_TIME
	if( PARAM2 isnot 'default' ) setvard NEW_WEATHER_STOPOLD_DELAY PARAM2
	if( PARAM3 isnot 'default' ) setvard NEW_WEATHER_START_DELAY PARAM3

	callexternal all worldevent_weather PARAM1
	callevent global_change_weather
}

{   game_playercmd_setweather_me		//Player initiated weather change console command
					//'setweather' uses this

	//vain attempt to get the setweather command to only affect the client

	callevent weather_to_scriptname PARAM1
	setvard WEATHER_TYPE PARAM1

	if weather.name isnot '0'

	setvard NEW_WEATHER weather.name
 
	setvard NEW_WEATHER_STOPOLD_DELAY	1
	setvard NEW_WEATHER_START_DELAY		1

	callexternal PARAM2 worldevent_weather PARAM1
	callevent global_change_weather
}

{   game_change_weather			//Change the weather and update clients
					//mstrig_weather uses this

    setvard NEW_WEATHER 		PARAM1
    setvard NEW_WEATHER_STOPOLD_DELAY	PARAM2
    setvard NEW_WEATHER_START_DELAY	PARAM3
    callexternal all worldevent_weather PARAM1
    callevent global_change_weather
}

{   game_precache_weather		//mstrig_weather uses this

    precachefile game.precacheweather
}

{   global_change_weather

    callevent global_stop_weather				//Stop the old
    
    local STARTWEATHER_DELAY 0
    if( GLOBAL_WEATHER isnot WEATHER_CLEAR )
	{
		setvard STARTWEATHER_DELAY NEW_WEATHER_STOPOLD_DELAY
	}
	else
	{
		//FEB2010_13 - clear weather effects on clear weather call
		clientevent update all const.localplayer.scriptID clear_weather
	}


    if( NEW_WEATHER isnot WEATHER_CLEAR ) callevent STARTWEATHER_DELAY global_start_weather		//Start the new when the old is done
    
}
{   global_stop_weather				//Stop the old weather

	if GLOBAL_WEATHER isnot 'GLOBAL_WEATHER'
	if GLOBAL_WEATHER isnot WEATHER_CLEAR
	if game.players > 0

	callexternal ent_me worldevent_weather_die NEW_WEATHER_STOPOLD_DELAY
}
{   global_start_weather			//Start a new weather

	if NEW_WEATHER isnot 'NEW_WEATHER'

	setvarg GLOBAL_WEATHER NEW_WEATHER

	if NEW_WEATHER isnot WEATHER_CLEAR
	if game.players > 0

	setvarg G_CUR_WEATHER GLOBAL_WEATHER

	dbg global_start_weather $get(ent_me,name)

	applyeffect ent_me GLOBAL_WEATHER NEW_WEATHER_START_DELAY WEATHER_TYPE
}


//{   game_playerjoin			//Update the joining player with the current weather status
    //Nothing, for now - it's handled by clientevent 'persist'
//}


//Precache weather
{  game_precache

	callevent map_setweather
	calleventloop $get_token_amt(global.map.weather) weather_precache_mapweather
}

{  weather_precache_mapweather

	local l.weathername $get_token(global.map.weather,game.script.iteration)
	callevent weather_to_scriptname l.weathername

    	if weather.name isnot WEATHER_CLEAR
    	if weather.name isnot '0'
	precachefile weather.name

}

{  weather_to_scriptname  //PARAM1 (in): Weather common name  |  weather.name (out):  Weather script name

    	if( PARAM1 equals 'clear' )		setvard weather.name WEATHER_CLEAR
   		else if( PARAM1 equals 'snow' )		setvard weather.name WEATHER_SNOW
    	else if( PARAM1 equals 'rain' )		setvard weather.name WEATHER_RAIN
    	else if( PARAM1 equals 'storm' )	setvard weather.name WEATHER_STORM
		else if( PARAM1 equals 'fog_green' ) setvard weather.name WEATHER_FOG_GREEN
		else if( PARAM1 equals 'fog_brown' ) setvard weather.name WEATHER_FOG_BROWN
		else if( PARAM1 equals 'fog_red' ) setvard weather.name WEATHER_FOG_RED
		else if( PARAM1 equals 'fog_white' ) setvard weather.name WEATHER_FOG_WHITE
		else if( PARAM1 equals 'fog_blue' ) setvard weather.name WEATHER_FOG_BLUE
		else if( PARAM1 equals 'fog_thick' ) setvard weather.name WEATHER_FOG_THICK
		else if( PARAM1 equals 'fog_black' ) setvard weather.name WEATHER_FOG_BLACK
    	else 					setvard weather.name 0

		setvarg G_WEATHER_SCRIPT weather.name

		//FEB2010_13 - clear weather effects cl event - probably redundant
		//event is in player_cl_effects_world
		if ( PARAM1 equals 'clear' ) clientevent update all const.localplayer.scriptID clear_weather
}