
{
	setvar ATTACK_RANGE 64
	const ANIM_ATTACK beatdoor
	const STORE_NAME dorfgans_blacksmith
}

#include monsters/base_npc
#include monsters/base_chat
#include monsters/base_npc_vendor
#include monsters/base_npc_vendor_confirm
#include old_helena/base_old_helena_npc

{ npc_spawn

   hp 350/350
   gold 25
   name        Dorfgan
   
   width 32
   height 72
   race human
   roam 0      
   setmodel    npc/blacksmith.mdl
   setactionanim beatdoor

   setvar SOUND_PAIN player/chesthit1.wav
   setvar SOUND_PAIN2 player/armhit1.wav
   setvar ATTACK1_DAMAGE 3
   setvard ATTACK_RANGE 90

   setvar SAY_SO 0
   setvar EXPLAINING_QUEST 0
   setvar QUEST_1 0
   setvar QUEST_2 0
   setvar OFFER_ITEMS 1
   setvar CANCHAT 1
   setvar CAN_SCREAM 1
   setvar CAN_RUN 1
   setvar SEE_ENEMY 0

   callevent createmystore   

   catchspeech say_hi hi
   catchspeech say_hi hello
   catchspeech say_hi hail
   catchspeech say_hi greet

   catchspeech say_quest attack
   catchspeech say_quest orc
	catchspeech say_dagger dagger

	setvard NPCATK_TARGET unset

	menu.autoopen 1

	callevent 260.0 add_moar_stuff
}

{
  eventname say_hi

  saytext Adventurer!  We're under [attack]!
  calleventtimed 3 say_hi2test
}

{
  eventname say_hi2test

  var SAY_SO equals 0

  saytext I'm Dorfgan, the blacksmith

  callevent setsayso
}

{
  eventname satsayso
  setvar SAY_SO 1
}

{
  eventname say_quest

  var EXPLAINING_QUEST equals 0

  saytext Our village is being ransacked by those blasted Orcs!
  setvar EXPLAINING_QUEST 1
  calleventtimed 2 say_quest_2
}



{
   
   eventname struck
   
   volume 10
   playrandomsound 2 SOUND_PAIN SOUND_PAIN2 none none
   playanim once raflinch

   callevent flee
}
{
   eventname resetoffer

   setvar OFFER_ITEMS 1   
}

{
   eventname playerused

	if ( HELENA_SAVED ) saytext I've also an item or two for exclusive sale to our brave saviors here.

   var OFFER_ITEMS equals 1

   offerstore STORE_NAME buysell trade
}
{
   eventname trade_success

   var CANCHAT equals 1

   saytext  We're low on supplies, so I don't have much to offer...
   setvar CANCHAT 0
   calleventtimed 10 resetchat
}
{
   eventname resetchat

   setvar CANCHAT 1
}


{ vendor_addstoreitems

   createstore STORE_NAME
   
   
   addstoreitem  STORE_NAME     swords_shortsword   1         100
   addstoreitem  STORE_NAME     swords_bastardsword   1         100
   addstoreitem  STORE_NAME     axes_doubleaxe   2         100
   addstoreitem  STORE_NAME     shields_ironshield   2         100
   addstoreitem  STORE_NAME     shields_lironshield   2         100
	addstoreitem STORE_NAME proj_bolt_wooden 100 100 0 25

   callevent addrandomitems
}

{ add_moar_stuff
	
	add MOAR_STUFF 1
	if ( MOAR_STUFF == 1 ) addstoreitem STORE_NAME proj_bolt_fire 100 100 0 25
	if ( MOAR_STUFF == 2 ) addstoreitem STORE_NAME proj_bolt_iron 100 100 0 25
	if ( MOAR_STUFF == 3 ) addstoreitem STORE_NAME proj_bolt_steel 75 800 0 25
	if ( MOAR_STUFF == 4 ) addstoreitem STORE_NAME proj_bolt_silver 25 800 0 25
	if MOAR_STUFF < 4
	callevent 260.0 add_moar_stuff
}

{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     swords_longsword   	 1          200
}
{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     swords_scimitar   	 1          100
}

{
   eventname addrandomitems
   if $rand(1,2) == 1
   addstoreitem  STORE_NAME     axes_battleaxe   	 1          200
}


{ old_helena_warboss_died

	setvard HELENA_SAVED 1
	
	addstoreitem STORE_NAME blunt_northmaul972 1 200 0
	addstoreitem STORE_NAME bows_crossbow_heavy33 1 100 0
}

{ basevendor_offerstore

	if HELENA_SAVED
	saytext I've got a few unusual items just for those who rescued Helena.
	callevent bchat_mouth_move
}

{ catapults_fire

	if !DID_CATA_COMMENT
	add CATAPULT_COMMENT 1
	if CATAPULT_COMMENT > 3

	saytext Catapults!? May the gods save us! They've brought catapults!
	setvard DID_CATA_COMMENT 1
}

{
	repeatdelay 1.5

	local TARGETS_NEAR $get_tsphere(enemy,128,game.monster.origin)

	if ( TARGETS_NEAR >= 1 )
	{
		playanim once ANIM_ATTACK
		dodamage $get_token(TARGETS_NEAR,0) ATTACK_RANGE 20.0 90% blunt
	}
}

{ say_dagger

   if $item_exists(PARAM1,smallarms_rd)
   
   if ( !HELENA_SAVED ) saytext Is now really the time to talk about this? We're under attack!

   if HELENA_SAVED
   if !FIXING_DAGGER
   setvard CUR_PLAYER PARAM1

   saytext I always hate to see adventurers who can't properly take care of their equipment
   callevent 1.0 say_dagger2
}

{ say_dagger2

   saytext I can fix that up for you, for a nominal fee, of course.

   menu.open CUR_PLAYER

}

{ not_enough_repair_dagger
   saytext You travellers have to learn somehow.
}

{ start_repair_dagger
   
	setvard REPAIR_QUEST_WINNER PARAM1

   setvard FIXING_DAGGER 1
   setvard REPAIR_STEP 0
   callevent repair_dagger

}

{ repair_dagger
   add REPAIR_STEP 1

   if ( REPAIR_STEP == 1 )
   {
      playanim once portal
      saytext This'll be no trouble at all
   }
   if ( REPAIR_STEP == 2 )
   {
      playanim once panic
      saytext *Crunch* Ow!
   }
   if ( REPAIR_STEP == 3 )
   {
      playanim once push_button
      saytext Sorry, I broke your dagger. Strange thing is, I think it cut me AFTER it shattered...
      offer REPAIR_QUEST_WINNER smallarms_eth
      setvard FIXING_DAGGER 0
   }

   if REPAIR_STEP < 3
   callevent 2.0 repair_dagger

}

{ game_playerused
	if HELENA_SAVED
	if $item_exists(PARAM1,smallarms_rd)
	setvard CHAT_STEP1 "Hey, that's a mighty interesting looking hunk of rust you have there."
	setvard CHAT_STEP2 "I might be able to clean it up into something even more interesting, for a price."
	setvard CHAT_STEPS 2
	callevent chat_loop
	setvard MENTIONED_DAGGER 1
}

{ game_menu_getoptions
	if HELENA_SAVED
	if ( $item_exists(PARAM1,smallarms_rd) )
	{
		local reg.mitem.title    "Repair rusted dagger (10,000 gold)"
		local reg.mitem.type    payment
		local reg.mitem.data    "smallarms_rd;gold:10000"
		local reg.mitem.callback  start_repair_dagger
		local reg.mitem.cb_failed not_enough_repair_dagger
		menuitem.register
	}
}
