#scope server

{
	const PLR_COMBAT_ICON alpha_poison_immune

	setvard PLR_HAND_SET 0 
	setvard PLR_UNIQUE_SUMMONS ''

	const PLR_LCOD_AOE 90
	const PLR_GCOD_AOE 180

	const PLR_2HPEN_LIGHT 0.7
	const PLR_2HPEN_LIGHT_REPORT 30%
	const PLR_2HPEN_HEAVY 0.6
	const PLR_2HPEN_HEAVY_REPORT 40%

	setvard ICE_SHIELDED 0
	setvard SCROLL_WARNING_LEVEL 0
	setvard ICE_SHIELD_TIME 0

	setvard PLR_BLOOM_CYCLE 0

	setvard IMMUNE_COLD 0.99
	setvard IMMUNE_FIRE 0.99
	setvard IMMUNE_LIGHTNING 0.99
	setvard IMMUNE_POISON 0.99
	setvard IMMUNE_STUN 0

	setvard PIMMUNE_COLD IMMUNE_COLD
	setvard PIMMUNE_FIRE IMMUNE_FIRE
	setvard PIMMUNE_LIGHTNING IMMUNE_LIGHTNING
	setvard PIMMUNE_POISON IMMUNE_POISON

	setvard PLR_DMG_MULTI '' 
	setvard PLR_DMG_RESIST '' 

	setvard PLR_PET_TYPES ''
	setvard PLR_PET_IDS ''

	setvard EXT_EFFECT_LIST 0

	const DEMON_BLOOD_LOSS -5
	const VAMPIRE_MULTI 0.35 

	const PLR_FAURA_CL_RATE 10.0 
	const PLR_PAURA_CL_RATE 30.0 

	const PLR_SFAURA_MP_COST 2 
	const PLR_SFAURA_MAXSIZE 175 
	const PLR_SFAURA_GROWTH_RATE 2 

	setvard IAM_PLAYER 1 

	setvard SCARABS_ATTACHED 0
	setvard WEBS_ATTACHED 0

	array.create ARRAY_JUMP_BEAM_IDS
	array.create ARRAY_JUMP_BEAM_TARGS

	setvard PLR_IDLE_MUSIC none
	setvard PLR_IDLE_MUSIC_LENGTH 0
	setvard PLR_COMBAT_MUSIC none
	setvard PLR_COMBAT_MUSIC_LENGTH 0

	setvard PLR_CURRENT_MUSIC none 
}

#include [casual] test_scripts/player_externals
#include [casual] $currentmap_player_externals

{ game_scriptflag_update 



	
	
	if ( $get_scriptflag(ent_me,nopush,type_exists) )
	{
		nopush 1
	}
	else
	{
		nopush 0
	}

	
	if ( $get_scriptflag(ent_me,cmusak,type_exists) )
	{
		local L_FLAG_MUSIC_VALUE $get_scriptflag(ent_me,cmusak,type_first)
		if L_FLAG_MUSIC_VALUE isnot none
		
		local L_COMBAT_MUSIC $get_token(L_FLAG_MUSIC_VALUE,1)
		local L_COMBAT_MUSIC_LENGTH $get_token(L_FLAG_MUSIC_VALUE,0)
		if ( PLR_CURRENT_MUSIC isnot L_COMBAT_MUSIC )
		{
			
			setvard PLR_COMBAT_MUSIC L_COMBAT_MUSIC
			setvard PLR_COMBAT_MUSIC_LENGTH L_COMBAT_MUSIC_LENGTH
			playmp3 ent_me L_COMBAT_MUSIC_LENGTH L_COMBAT_MUSIC
		}
		else
		{
			
		}
	}
	else
	{
		if PLR_CURRENT_MUSIC equals PLR_COMBAT_MUSIC
		
		callevent ext_fade_music
	}

	
	if ( PARAM1 equals add ) local L_CHECK_EXPIRE 1
	if ( PARAM1 equals edit )
	{
		
		local L_CHECK_EXPIRE 1
	}
	
	if L_CHECK_EXPIRE

	local L_CHECK_EXPTIME PARAM5

	if ( L_CHECK_EXPTIME > -1 )
	{
		add L_CHECK_EXPTIME 0.1
		callevent L_CHECK_EXPTIME check_flags_expired
	}
}

{ ext_sflag_test
	scriptflags ent_me add test1 cold 1 5.00 "Test1-Cold+1 Expired"
	scriptflags ent_me add test2 cold 2 10.00 "Test2-Cold+2 Expired"
	scriptflags ent_me add test3 fire 4 5.0 "Test3-Fire+3 Expired"
	scriptflags ent_me add test4 fire -1 11.0 "Test4-Fire-1 Expired"
	callevent ext_sflag_test_loop
}

{ ext_sflag_test_loop
	if $get_scriptflag(ent_me,fire,exists)
	gplayermessage ent_me cold $get_scriptflag(ent_me,cold,totalvalue) fire  $get_scriptflag(ent_me,fire,totalvalue)
	callevent 1.0 ext_sflag_test_loop
}

{ ext_set_status_flag 



	

	scriptflags ent_me add PARAM1 PARAM2 PARAM3 PARAM4 PARAM5
	callevent check_flags
}

{ check_flags
	if ( $get_scriptflag(ent_me,mana_regen,type_exists) )
	{
		callevent ext_mana_regen_loop
	}

}

{ check_flags_expired

	scriptflags ent_me remove_expired
	callexternal ent_me ext_scriptflag_expired
}

{ ext_remove_status_flag 
	scriptflags ent_me remove PARAM1
	
}

{ ext_remove_status_flags_type 

	scriptflags ent_me cleartype PARAM1
	
}

{
	const SOUNDSET_HM_SWORDREADY player/swordready.wav
	const SOUNDSET_HM_SHOUT1 player/shout1.wav
	const SOUNDSET_HM_JAB1 player/jab1.wav
	const SOUNDSET_HM_JAB2 player/jab2.wav
	const SOUNDSET_HM_BREATHFAST1 player/breathe_fast1.wav
	const SOUNDSET_HM_BREATHFAST2 player/breathe_fast2.wav
	const SOUNDSET_HM_BREATHFAST3 player/breathe_fast3.wav
	const SOUNDSET_HM_DEATH player/death.wav
	const SOUNDSET_HM_CHESTHIT1 player/chesthit1.wav
	const SOUNDSET_HM_STOMACHHIT1 player/stomachhit1.wav
	const SOUNDSET_HM_ARMHIT1 player/armhit1.wav
	const SOUNDSET_HM_LEGHIT1 player/leghit1.wav
	const SOUNDSET_HM_FALLPAIN1 player/fallpain1.wav
	const SOUNDSET_HM_FALLPAIN2 player/fallpain2.wav
	const SOUNDSET_HM_FALLPAIN3 player/fallpain3.wav
	const SOUNDSET_HM_FALLPAIN4 player/fallpain4.wav


	const SOUNDSET_HF_SWORDREADY player/Femaleswordready.wav
	const SOUNDSET_HF_SHOUT1 player/Femaleshout1.wav
	const SOUNDSET_HF_JAB1 player/Femalejab1.wav
	const SOUNDSET_HF_JAB2 player/Femalejab2.wav
	const SOUNDSET_HF_BREATHFAST1 player/Femalebreathe_fast1.wav
	const SOUNDSET_HF_BREATHFAST2 player/Femalebreathe_fast2.wav
	const SOUNDSET_HF_BREATHFAST3 player/Femalebreathe_fast3.wav
	const SOUNDSET_HF_DEATH player/FemaleDeath.wav
	const SOUNDSET_HF_CHESTHIT1 player/Femalechesthit1.wav
	const SOUNDSET_HF_STOMACHHIT1 player/Femalestomachhit1.wav
	const SOUNDSET_HF_ARMHIT1 player/Femalearmhit1.wav
	const SOUNDSET_HF_LEGHIT1 player/Femaleleghit1.wav
	const SOUNDSET_HF_FALLPAIN1 player/Femalefallpain1.wav
	const SOUNDSET_HF_FALLPAIN2 player/Femalefallpain2.wav
	const SOUNDSET_HF_FALLPAIN3 player/Femalefallpain3.wav
	const SOUNDSET_HF_FALLPAIN4 player/Femalefallpain4.wav

	precache SOUNDSET_HM_SWORDREADY
	precache SOUNDSET_HM_SHOUT1
	precache SOUNDSET_HM_JAB1
	precache SOUNDSET_HM_JAB2
	precache SOUNDSET_HM_BREATHFAST1
	precache SOUNDSET_HM_BREATHFAST2
	precache SOUNDSET_HM_BREATHFAST3
	precache SOUNDSET_HM_DEATH
	precache SOUNDSET_HM_CHESTHIT1
	precache SOUNDSET_HM_STOMACHHIT1
	precache SOUNDSET_HM_ARMHIT1
	precache SOUNDSET_HM_LEGHIT1
	precache SOUNDSET_HM_FALLPAIN1
	precache SOUNDSET_HM_FALLPAIN2
	precache SOUNDSET_HM_FALLPAIN3
	precache SOUNDSET_HM_FALLPAIN4

	precache SOUNDSET_HF_SWORDREADY
	precache SOUNDSET_HF_SHOUT1
	precache SOUNDSET_HF_JAB1
	precache SOUNDSET_HF_JAB2
	precache SOUNDSET_HF_BREATHFAST1
	precache SOUNDSET_HF_BREATHFAST2
	precache SOUNDSET_HF_BREATHFAST3
	precache SOUNDSET_HF_DEATH
	precache SOUNDSET_HF_CHESTHIT1
	precache SOUNDSET_HF_STOMACHHIT1
	precache SOUNDSET_HF_ARMHIT1
	precache SOUNDSET_HF_LEGHIT1
	precache SOUNDSET_HF_FALLPAIN1
	precache SOUNDSET_HF_FALLPAIN2
	precache SOUNDSET_HF_FALLPAIN3
	precache SOUNDSET_HF_FALLPAIN4
}

{ fake_sound_precache
	

	svplaysound 1 0 magic/volcano_loop.wav
	svplaysound 1 0 weapons/polearm_spin.wav
	svplaysound 1 0 ambience/pulsemachine.wav
	svplaysound 1 0 ambience/dronemachine1.wav
	svplaysound 1 0 monsters/bear/c_beardire_bat1.wav
	svplaysound 1 0 monsters/goblin/sps_fogfire.wav

	svplaysound 1 0 SOUNDSET_HM_SWORDREADY
	svplaysound 1 0 SOUNDSET_HM_SHOUT1
	svplaysound 1 0 SOUNDSET_HM_JAB1
	svplaysound 1 0 SOUNDSET_HM_JAB2
	svplaysound 1 0 SOUNDSET_HM_BREATHFAST1
	svplaysound 1 0 SOUNDSET_HM_BREATHFAST2
	svplaysound 1 0 SOUNDSET_HM_BREATHFAST3
	svplaysound 1 0 SOUNDSET_HM_DEATH
	svplaysound 1 0 SOUNDSET_HM_CHESTHIT1
	svplaysound 1 0 SOUNDSET_HM_STOMACHHIT1
	svplaysound 1 0 SOUNDSET_HM_ARMHIT1
	svplaysound 1 0 SOUNDSET_HM_LEGHIT1
	svplaysound 1 0 SOUNDSET_HM_FALLPAIN1
	svplaysound 1 0 SOUNDSET_HM_FALLPAIN2
	svplaysound 1 0 SOUNDSET_HM_FALLPAIN3
	svplaysound 1 0 SOUNDSET_HM_FALLPAIN4

	svplaysound 1 0 SOUNDSET_HF_SWORDREADY
	svplaysound 1 0 SOUNDSET_HF_SHOUT1
	svplaysound 1 0 SOUNDSET_HF_JAB1
	svplaysound 1 0 SOUNDSET_HF_JAB2
	svplaysound 1 0 SOUNDSET_HF_BREATHFAST1
	svplaysound 1 0 SOUNDSET_HF_BREATHFAST2
	svplaysound 1 0 SOUNDSET_HF_BREATHFAST3
	svplaysound 1 0 SOUNDSET_HF_DEATH
	svplaysound 1 0 SOUNDSET_HF_CHESTHIT1
	svplaysound 1 0 SOUNDSET_HF_STOMACHHIT1
	svplaysound 1 0 SOUNDSET_HF_ARMHIT1
	svplaysound 1 0 SOUNDSET_HF_LEGHIT1
	svplaysound 1 0 SOUNDSET_HF_FALLPAIN1
	svplaysound 1 0 SOUNDSET_HF_FALLPAIN2
	svplaysound 1 0 SOUNDSET_HF_FALLPAIN3
	svplaysound 1 0 SOUNDSET_HF_FALLPAIN4
}


{ game_hitbodypart 

	if $rand(1,5) == 1
	if ( PARAM1 equals chest ) svplaysound 2 10 PLR_SOUND_CHESTHIT1
	if ( PARAM1 equals stomach ) svplaysound 2 10 PLR_SOUND_STOMACHHIT1
	if ( PARAM1 equals larm ) svplaysound 2 10 PLR_SOUND_ARMHIT1
	if ( PARAM1 equals rarm ) svplaysound 2 10 PLR_SOUND_ARMHIT1
	if ( PARAM1 equals rleg ) svplaysound 2 10 PLR_SOUND_LEGHIT1
	if ( PARAM1 equals lleg ) svplaysound 2 10 PLR_SOUND_LEGHIT1
}

{ game_fallpain_sound
	
	svplayrandomsound 2 10 PLR_SOUND_FALLPAIN1 PLR_SOUND_FALLPAIN2 PLR_SOUND_FALLPAIN3 PLR_SOUND_FALLPAIN4
}

{ [shared] set_glow_id

	
	
	setvard MY_GLOW_ID PARAM1
	
}

{ ice_shield_check

	if ( ICE_SHIELDED ) setvarg ICE_SHIELD_CHECK 1
	if ( !ICE_SHIELDED ) setvarg ICE_SHIELD_CHECK 0
	
}

{ ice_shield_on

	
	setvard ICE_SHIELDED 1
	if ( ICE_SHIELD_TIME equals 0 ) setvard ICE_SHIELD_TIME game.time
	if ( ICE_SHIELD_TIME isnot 0 ) local REFRESHED_SHIELD 1
	setvard ICE_SHIELD_TIME PARAM1
	bplayermessage ent_me Ice shield $int(ICE_SHIELD_TIME) seconds remain.
}

{ ice_shield_off

	
	setvard ICE_SHIELDED 0
	setvard ICE_SHIELD_TIME 0
}

{ effect_damage

	

	if !EFFECT_DAMAGE_DELAY
	setvard EFFECT_DAMAGE_DELAY 1
	callevent 0.9 reset_effect_damage
	
	dodamage ent_me direct PARAM1 100% PARAM2 PARAM4 PARAM5
}

{ reset_effect_damage

	setvard EFFECT_DAMAGE_DELAY 0
}

{ lightning_immune

	
	gplayermessage ent_me You are now immune to lightning.
	takedmg lightning 0.0
	setvard IMMUNE_LIGHTNING 1
	callevent set_player_immunes
	callevent PARAM1 lightning_normal
}
{ fire_immune

	gplayermessage ent_me You are now immune to fire.
	takedmg fire 0.0
	setvard IMMUNE_FIRE 1
	callevent set_player_immunes
	callevent PARAM1 fire_normal
}
{ poison_immune

	gplayermessage ent_me You are now immune to poison.
	takedmg poison 0.0
	setvard IMMUNE_POISON 1
	callevent set_player_immunes
	callevent PARAM1 poison_normal
}
{ cold_immune

	gplayermessage ent_me You are now immune to cold effects.
	takedmg cold 0.0
	takedmg ice 0.0
	
	
	callevent set_player_immunes
	callevent PARAM1 cold_normal
}

{ fire_resist

	gplayermessage ent_me You are now resistant to fire damage. ( 75% )
	takedmg fire 0.25
	setvard IMMUNE_FIRE 0.25
	callevent set_player_immunes
	callevent PARAM1 fire_normal 
}

{ [server] fire_resist_adj 

	local RETURN_PARAM 100
	local INC_PARAM PARAM1
	multiply INC_PARAM 100
	subtract RETURN_PARAM INC_PARAM

	local RETURN_PARAM $int(RETURN_PARAM)
	stradd RETURN_PARAM '%'

	if ( PARAM1 == 0 )
	{
		callevent fire_normal
		local EXIT_SUB 1
	}
	if !EXIT_SUB


	if ( PARAM1 < 1.0 ) 
	{
		if PARAM1 > 0
		gplayermessage ent_me You are now more resistant to fire. ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 > 1.0 )
	{
		playermessage ent_me You are now more vulnerable to fire damage! ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 == 1.0 )
	{
		gplayermessage ent_me You are now immuned to fire damage!
		local DAMAGE_REDUCT 0.0
	}
	
	
	takedmg fire DAMAGE_REDUCT
	
	callevent set_player_immunes
	callevent PARAM2 fire_normal 
}

{ [server] lightning_resist_adj 

	local RETURN_PARAM 100
	local INC_PARAM PARAM1
	multiply INC_PARAM 100
	subtract RETURN_PARAM INC_PARAM

	local RETURN_PARAM $int(RETURN_PARAM)
	stradd RETURN_PARAM '%'

	if ( PARAM1 == 0 )
	{
		callevent lightning_normal
		local EXIT_SUB 1
	}
	if !EXIT_SUB


	if ( PARAM1 < 1.0 ) 
	{
		if PARAM1 > 0
		gplayermessage ent_me You are now more resistant to electricity. ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 > 1.0 )
	{
		playermessage ent_me You are now more vulnerable electricity! ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 == 1.0 )
	{
		gplayermessage ent_me You are now immuned to electricity!
		local DAMAGE_REDUCT 0.0
	}
	
	
	takedmg lightning DAMAGE_REDUCT
	callevent set_player_immunes
	callevent PARAM2 lightning_normal 
}

{ [server] cold_resist_adj 

	local RETURN_PARAM 100
	local INC_PARAM PARAM1
	multiply INC_PARAM 100
	subtract RETURN_PARAM INC_PARAM

	local RETURN_PARAM $int(RETURN_PARAM)
	stradd RETURN_PARAM '%'

	if ( PARAM1 == 0 )
	{
		callevent cold_normal
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM1 < 1.0 ) 
	{
		if PARAM1 > 0
		gplayermessage ent_me You are now more resistant to cold. ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 > 1.0 )
	{
		playermessage ent_me You are now more vulnerable to cold damage! ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 == 1.0 )
	{
		gplayermessage ent_me You are now immuned to ice magic!
		local DAMAGE_REDUCT 0.0
	}
	
	
	takedmg cold DAMAGE_REDUCT
	takedmg ice DAMAGE_REDUCT
	setvard IMMUNE_COLD PARAM1
	callevent set_player_immunes
	callevent PARAM2 cold_normal 
}

{ cold_resist

	gplayermessage ent_me You feel more resistant to the effects of extreme cold. ( 75% )
	takedmg cold 0.25
	takedmg ice 0.25
	setvard IMMUNE_COLD 0.25
	callevent set_player_immunes
	callevent PARAM1 cold_normal
}

{ [server] poison_resist_adj 

	local RETURN_PARAM 100
	local INC_PARAM PARAM1
	multiply INC_PARAM 100
	subtract RETURN_PARAM INC_PARAM

	local RETURN_PARAM $int(RETURN_PARAM)
	stradd RETURN_PARAM '%'

	if ( PARAM1 == 0 )
	{
		callevent cold_normal
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM1 < 1.0 ) 
	{
		if PARAM1 > 0
		gplayermessage ent_me You are now more resistant to poisons. ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 > 1.0 )
	{
		playermessage ent_me You are now more vulnerable to poison! ( RETURN_PARAM )
		local DAMAGE_REDUCT PARAM1
	}
	if ( PARAM1 == 1.0 )
	{
		gplayermessage ent_me You are now immuned to poison!
		local DAMAGE_REDUCT 0.0
	}
	
	
	takedmg poison DAMAGE_REDUCT
	setvard IMMUNE_POISON PARAM1
	callevent set_player_immunes
	callevent PARAM2 poison_normal 
}

{ normal_immune

	if ( IMMUNE_COLD > 0 ) playermessage ent_me Your have lost your resistance to cold!
	if ( IMMUNE_FIRE > 0 ) playermessage ent_me Your have lost your resistance to fire!
	if ( IMMUNE_POISON > 0 ) playermessage ent_me Your have lost your resistance to poison!
	if ( IMMUNE_LIGHTNING > 0 ) playermessage ent_me Your have lost your resistance to lightning!

	setvard IMMUNE_COLD 0
	setvard IMMUNE_FIRE 0
	setvard IMMUNE_LIGHTNING 0
	setvard IMMUNE_POISON 0
	takedmg cold 1.0
	takedmg fire 1.0
	takedmg lightning 1.0
	takedmg poison 1.0
	takedmg ice 1.0
	callevent set_player_immunes
}

{ cold_normal
	takedmg cold 1.0
	callevent set_player_immunes
}

{ fire_normal
	takedmg fire 1.0
	callevent set_player_immunes
}

{ lightning_normal
	takedmg lightning 1.0
	callevent set_player_immunes
}

{ poison_normal
	takedmg poison 1.0
	callevent set_player_immunes
}

{ set_player_immunes

	
	

	

	
	if ( IMMUNE_COLD > PIMMUNE_COLD )
	{
		if IMMUNE_COLD < 1
		dplayermessage ent_me You lose your resistance to cold.
	}
	if ( IMMUNE_FIRE > PIMMUNE_FIRE )
	{
		if IMMUNE_FIRE < 1
		dplayermessage ent_me You lose your resistance to fire.
	}
	if ( IMMUNE_LIGHTNING > PIMMUNE_LIGHTNING )
	{
		if IMMUNE_LIGHTNING < 1
		dplayermessage ent_me You lose your resistance to lightning.
	}
	if ( IMMUNE_POISON > PIMMUNE_POISON )
	{
		if IMMUNE_POISON < 1
		dplayermessage ent_me You lose your resistance to poison.
	}

	
	if ( PIMMUNE_COLD == 1 )
	{
		if IMMUNE_COLD < 1
		dplayermessage ent_me You lose your immunity to cold.
	}
	if ( PIMMUNE_FIRE == 1 )
	{
		if IMMUNE_FIRE < 1
		dplayermessage ent_me You lose your immunity to fire.
	}
	if ( PIMMUNE_LIGHTNING == 1 )
	{
		if IMMUNE_LIGHTNING < 1
		dplayermessage ent_me You lose your immunity to lightning.
	}
	if ( PIMMUNE_POISON == 1 )
	{
		if IMMUNE_POISON < 1
		dplayermessage ent_me You lose your immunity to poison.
	}

	setvard PIMMUNE_COLD IMMUNE_COLD
	setvard PIMMUNE_FIRE IMMUNE_FIRE
	setvard PIMMUNE_LIGHTNING IMMUNE_LIGHTNING
	setvard PIMMUNE_POISON IMMUNE_POISON
}

{ tome_warn

	add SCROLL_WARNING_LEVEL 1
	if ( SCROLL_WARNING_LEVEL > 3 ) setvard SCROLL_WARNING_LEVEL 3
	if ( SCROLL_WARNING_LEVEL == 1 ) infomsg ent_me "TOMES vs SCROLLS" "TOMES can be used to memorize spells permanently, however you can only memorize 8 such spells."
	if ( SCROLL_WARNING_LEVEL == 2 ) infomsg ent_me "TOMES vs SCROLLS" "SCROLLS can be used to bypass this limit, but they are more expensive."
}

{ set_stun_prot

	if !PL_STABILITY_POTION
	setvard IMMUNE_STUN PARAM1
	setvard PL_IMMUNE_STUN PARAM1
}


{ toggle_light

	

	if ( IR_GLOWING == 1 ) 
	{
		setvard IR_GLOWING 0
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( IR_GLOWING == 0 ) setvard IR_GLOWING 1

	if ( IR_GLOWING equals 'IR_GLOWING' ) setvard IR_GLOWING 1
}

{ set_spawn_point

	
	setvard NEW_SPAWN_POS PARAM1
}

{ demon_blood

	setvard DEMON_BLOOD_END_TIME game.time
	add DEMON_BLOOD_END_TIME PARAM1

	effect screenfade ent_me 0.5 3 (255,0,0) 255 fadeout
	gplayermessage ent_me The demonic soul fills your heart with rage!
	playsound 0 10 monsters/troll/trollidle2.wav

	setvard MAKE_NOISE 2
	setvard DEMON_BLOOD 1

	

	callevent 1.0 demon_blood_loop
}

{ end_demon_blood
	if DEMON_BLOOD
	setvard DEMON_BLOOD 0
	gplayermessage ent_me "The demon blood fades."
}

{ demon_blood_loop

	if DEMON_BLOOD

	if ( game.time >= DEMON_BLOOD_END_TIME )
	{
		callevent end_demon_blood
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	
	

	if ( FX_SPAMMERS < MAX_FX_SPAMMERS ) effect glow ent_me (255,0,0) 256 1.9 1.9

	callevent 2.0 demon_blood_loop


	add MAKE_NOISE 1	
	if ( MAKE_NOISE > 5 )
	{
		playrandomsound 0 10 monsters/troll/trollidle2.wav monsters/troll/trollidle.wav
		setvard MAKE_NOISE 0
	}

	givehp DEMON_BLOOD_LOSS
	effect screenfade ent_me 0.5 1 (255,0,0) 150 fadeout
	effect screenshake $relpos(0,0,0) 32 10 1 32
	if ( MAKE_NOISE > 0 ) playsound 0 10 player/heartbeat_noloop.wav

	if ( $get(ent_me,hp) <= 0 ) kill ent_me
}

{ register_body 

	
	setvard MY_BODY PARAM1
	setvard MY_CL_BODY PARAM2
}

{ hide_body_parts 

	

	setvard HIDE_LEGS 0
	setvard HIDE_HEAD 0
	setvard HIDE_CHEST 0
	setvard HIDE_ARMS 0

	

	if ( $get_token(PARAM1,0) equals legs ) setvard HIDE_LEGS 1
	if ( $get_token(PARAM1,0) equals head ) setvard HIDE_HEAD 1
	if ( $get_token(PARAM1,0) equals chest ) setvard HIDE_CHEST 1
	if ( $get_token(PARAM1,0) equals arms ) setvard HIDE_ARMS 1
	if ( $get_token(PARAM1,1) equals legs ) setvard HIDE_LEGS 1
	if ( $get_token(PARAM1,1) equals head ) setvard HIDE_HEAD 1
	if ( $get_token(PARAM1,1) equals chest ) setvard HIDE_CHEST 1
	if ( $get_token(PARAM1,1) equals arms ) setvard HIDE_ARMS 1
	if ( $get_token(PARAM1,2) equals legs ) setvard HIDE_LEGS 1
	if ( $get_token(PARAM1,2) equals head ) setvard HIDE_HEAD 1
	if ( $get_token(PARAM1,2) equals chest ) setvard HIDE_CHEST 1
	if ( $get_token(PARAM1,2) equals arms ) setvard HIDE_ARMS 1
	if ( $get_token(PARAM1,3) equals legs ) setvard HIDE_LEGS 1
	if ( $get_token(PARAM1,3) equals head ) setvard HIDE_HEAD 1
	if ( $get_token(PARAM1,3) equals chest ) setvard HIDE_CHEST 1
	if ( $get_token(PARAM1,3) equals arms ) setvard HIDE_ARMS 1
	if ( $get_token(PARAM1,4) equals legs ) setvard HIDE_LEGS 1
	if ( $get_token(PARAM1,4) equals head ) setvard HIDE_HEAD 1
	if ( $get_token(PARAM1,4) equals chest ) setvard HIDE_CHEST 1
	if ( $get_token(PARAM1,4) equals arms ) setvard HIDE_ARMS 1

	callexternal MY_BODY hide_parts HIDE_LEGS HIDE_HEAD HIDE_CHEST HIDE_ARMS
}

{ [shared] wearing_armor

	

	setvard ARMOR_EQUIPED PARAM1
	if ( PARAM1 == 0 ) 
	{
		setvard ARMOR_TYPE none
		setvard ARMOR_OFS 0
	}
	else
	{
		setvard ARMOR_TYPE PARAM2
		local INC_OFS PARAM3
		add INC_OFS 1
		setvard ARMOR_OFS PARAM3
	}
}

{ potion_regen 

	if !REGEN_ON 
	if ( VAMPIRE_ON )
	{
		dplayermessage ent_me Vampire blood and regeneration potions do not mix.
		kill ent_me
	}

	setvard REGEN_ON 1
	setvard REGEN_RATE 5
	callevent PARAM1 regen_end
	callevent regen_start
}

{ regen_start

	if REGEN_ON

	local MY_MAX_HEALTH $get(ent_me,maxhp)
	local MY_CUR_HEALTH $get(ent_me,hp)

	playsound 0 10 player/heartbeat_noloop.wav

	if ( MY_CUR_HEALTH < MY_MAX_HEALTH ) givehp REGEN_RATE
	callevent 1.0 regen_start
}

{ regen_end

	if REGEN_ON
	playermessage ent_me The regenerative magic fades.
	setvard REGEN_ON 0
}

{ potion_vampire 

	if !VAMPIRE_ON
	setvard VAMPIRE_ON 1
	setvard IMMUNE_VAMPIRE 1
	callevent vampire_suncheck
	
	callevent PARAM1 vampire_off
}

{ vampire_suncheck

	if VAMPIRE_ON

	


	

	if ( CURRENT_TIME_HOUR < 19 )
	{
		if CURRENT_TIME_HOUR > 5
		local MY_POS $get(ent_me,origin)
		if $get_under_sky(MY_POS)
		if game.map.name isnot sfor
		

		{
		local SUN_DAMAGE 100
		if ( CURRENT_TIME_HOUR > 16 ) local SUN_DAMAGE 50

		callexternal GAME_MASTER gm_setname "The light of the sun"
		xdodamage ent_me direct SUN_DAMAGE 100% GAME_MASTER GAME_MASTER none magic_effect
		
	}
	callevent 5.1 vampire_suncheck
}

{ vampire_off

	setvard IMMUNE_VAMPIRE 0
	setvard VAMPIRE_ON 0
	playermessage ent_me The effects of the vampire blood fade.
	
}

{ game_damaged_other
	
	if ( VAMPIRE_ON )
	{
		if $get(PARAM1,race) isnot undead
		if !$get(PARAM1,scriptvar,'IMMUNE_VAMPIRE')

		local MY_MAX_HEALTH $get(ent_me,maxhp)
		local MY_CUR_HEALTH $get(ent_me,hp)

		if MY_CUR_HEALTH < MY_MAX_HEALTH

		playsound 0 10 player/heartbeat_noloop.wav

		local DMG_INFLICTED PARAM2
		multiply DMG_INFLICTED VAMPIRE_MULTI
		givehp DMG_INFLICTED

		effect screenfade ent_me 0.5 3 (255,0,0) 50 fadeout		
	}

	if ( DEMON_BLOOD )
	{
		if $get(PARAM1,isalive)
		if $get(PARAM1,relationship,ent_me) equals enemy

		local DEMON_BLOOD_DAMAGE $get(ent_me,skill.spellcasting)
		multiply DEMON_BLOOD_DAMAGE 10

		if $rand(1,3) == 1

		if game.time > NEXT_DEMON_BLOOD_DMG
		setvard NEXT_DEMON_BLOOD_DMG game.time
		add NEXT_DEMON_BLOOD_DMG 1.0

		applyeffect PARAM1 effects/generic_damage DEMON_BLOOD_DAMAGE $get(ent_me,id) 1 magic PL_ACTIVE_SKILL
		playrandomsound 0 10 monsters/troll/trollpain.wav monsters/troll/trollattack.wav
	}
}


{ ext_playsound_kiss
	playsound PARAM1 PARAM2 PARAM3
}

{ ext_playsound 

	if ( PARAM3 isnot 'PARAM3' )
	{
		local SOURCE_ORG PARAM2
		
		if $dist(game.monster.origin,SOURCE_ORG) > PARAM3
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	setvard EXTPLAY_SOUND PARAM1
	callevent 0.1 ext_playsound2 
}

{ ext_playsound2
	
	playsound 0 10 EXTPLAY_SOUND
}

{ using_lightning 
	setvard USING_LIGHTNING PARAM1
	setvard LIGHTNING_DMG_RATIO PARAM2
	setvard LIGHTNING_SKILL PARAM3
}

{ ext_push_targ 

	
	if $get(PARAM1,scriptvar,'IMMUNE_PUSH') != 1
	addvelocity PARAM1 PARAM2
}

{ ext_rename_me
	name PARAM1
}

{ ext_play_music 

	
	local RANGE_RESULT PARAM4
	if ( PARAM4 equals 'PARAM4' ) local RANGE_RESULT 0
	if ( PARAM2 isnot stop.mp3 )
	{
		playmp3 PARAM1 PARAM2 PARAM3 RANGE_RESULT
	}
	else
	{
		playmp3 0 stop.mp3
		
	}
}

{ ext_play_music_me 
	
	
	if ( PARAM2 isnot stop.mp3 )
	{
		playmp3 ent_me PARAM1 PARAM2
	}
	else
	{
		playmp3 ent_me 0 stop.mp3
		
	}
}

{ ext_play_music_range 
	if $get(PARAM3,range) < PARAM4
	if ( PARAM2 isnot stop.mp3 )
	{
		playmp3 ent_me PARAM1 PARAM2
	}
	else
	{
		playmp3 ent_me 0 stop.mp3
		
	}
}

{ ext_scan_radius 

	consolemsg ent_me ext_scan_radius getents PARAM1 PARAM2
	getents PARAM1 PARAM2
	setvard LOOP_COUNT 0
	setvard ENT_LIST "Names:"
	calleventloop GET_COUNT scan_loop
	consolemsg ent_me ent_count GET_COUNT list( ENT_LIST ) @ $get(GET_ENT1,range)
}

{ scan_loop

	add LOOP_COUNT 1
	stradd ENT_LIST LOOP_COUNT
	stradd ENT_LIST "-"
	if ( LOOP_COUNT == 1 ) stradd ENT_LIST $get(GET_ENT1,name)
	if ( LOOP_COUNT == 2 ) stradd ENT_LIST $get(GET_ENT2,name)
	if ( LOOP_COUNT == 3 ) stradd ENT_LIST $get(GET_ENT3,name)
	if ( LOOP_COUNT == 4 ) stradd ENT_LIST $get(GET_ENT4,name)
	if ( LOOP_COUNT == 5 ) stradd ENT_LIST $get(GET_ENT5,name)
	if ( LOOP_COUNT == 6 ) stradd ENT_LIST $get(GET_ENT6,name)
	if ( LOOP_COUNT == 7 ) stradd ENT_LIST $get(GET_ENT7,name)
	if ( LOOP_COUNT == 8 ) stradd ENT_LIST $get(GET_ENT8,name)
	if ( LOOP_COUNT == 9 ) stradd ENT_LIST $get(GET_ENT9,name)

	stradd ENT_LIST ","
}

{ ext_setangle 
	
	setangle PARAM1 PARAM2
	
}

{ set_mana_dart 



	setvard GAME_PVP game.pvp
	setvard BALL_SIZE PARAM1
	setvard BALL_DMG PARAM2
	if ( BALL_SIZE > 5 ) multiply BALL_DMG 2.0
}

{ ext_hit_manaball

	if !HIT_BY_MANABALL
	setvard HIT_BY_MANABALL 1
	callevent 1.0 reset_hit_by_manaball
}

{ reset_hit_by_manaball
	setvard HIT_BY_MANABALL 0
}

{ ext_get_sphere_test

	setvard SPHERE_GOT $get_insphere(PARAM1,PARAM2,PARAM3)
	consolemsg ent_me sphere [ PARAM1 PARAM2 PARAM3 ] got $get(SPHERE_GOT,name) id: $get(SPHERE_GOT,id)
}


{ ext_quest 
	

	quest set ent_me PARAM1 PARAM2
	savenow ent_me
}

{ ext_addgold
	addgold PARAM1
	
}

{ ext_setgold
	gold PARAM1
	
}

{ [server] send_damage 
	
	
	
	
	dodamage PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}

{ [server] ext_chest_lastused 
	
	consolemsg ent_me treasure chest used.
	setvard PL_LAST_CHEST PARAM1
	callevent 2.0 reset_pl_last_chest
}

{ [server] reset_pl_last_chest
	setvard PL_LAST_CHEST unset
}

{ ext_fake_time
	setvard IS_AFK 0
	setvard AFK_TIME 0
	setvard IN_WORLD 1
	setvard PL_TIME 999
	setvard PL_BEEN_ATTACKED 1
}

{ monster_health_on
	setvard DISPLAY_TARG_HP 1
}

{ monster_health_off
	setvard DISPLAY_TARG_HP 0
}

{ give_hp 
	givehp PARAM1
}

{ give_mp 
	givemp PARAM1
}


{ ext_usetrig 
	usetrigger PARAM1
}

{ gen_cmd

	PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}


{ [server] ext_invalidate 
	setvard PLAYING_DEAD PARAM1	
}

{ [server] ext_dodamage 

	dodamage PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}




{ [server] freeze_solid_start 
	
	setvard I_R_FROZEN 1
	setvard PL_I_R_FROZEN 1
	
	callevent PARAM1 freeze_solid_end
}

{ [server] freeze_solid_end
	
	setvard I_R_FROZEN 0
	setvard PL_I_R_FROZEN 0
}

{ [server] ext_remove_lock
	setvard EXT_REMOVE_EFFECT lock
}

{ [server] ext_remove_nojump
	setvard EXT_REMOVE_EFFECT nojump
}

{ [server] ext_removed_effects
	setvard EXT_REMOVE_EFFECT unset
}

{ [server] ext_setstat
	setstat PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8
}

{ [server] ext_vistoggle


	if ( PLR_VIS_ON )
	{
		
		
		
		
		setvard PLR_VIS_ON 0
	}
	else
	{
 		
		
		
		setvard PLR_VIS_ON 1
	}
}

{ ext_spider_protect 
	
	setvard PLR_SPIDER_PROT 1
	setvard PLR_SPIDER_AMT PARAM2
	callevent PARAM1 spider_protect_end
	callevent float_to_percent PLR_SPIDER_AMT
	setvard FLOAT_RETURN $int(FLOAT_RETURN)
	stradd FLOAT_RETURN '%'
	gplayermessage ent_me You are now protected from spiders ( FLOAT_RETURN )
}

{ spider_protect_end
	setvard PLR_SPIDER_PROT 0
	playermessage ent_me The protection from spider magic fades.
}

{ float_to_percent 

	if ( PARAM1 == 0 ) setvard FLOAT_RETURN '100%'
	if ( PARAM1 == 1 ) setvard FLOAT_RETURN '100%'
	if PARAM1 != 0

	setvard FLOAT_RETURN 100
	local INC_FLOAT PARAM1
	multiply INC_FLOAT 100
	subtract FLOAT_RETURN INC_FLOAT

	local FLOAT_RETURN $int(FLOAT_RETURN)
	stradd FLOAT_RETURN '%'
}

{ ext_set_alco_type
	
	
	setvard ALCO_TYPE PARAM1
	setvard ALCO_SPAWN_ITEM PARAM2
}

{ [server] element_resist 

	if ( PARAM2 equals 'fire' ) local CUR_PROTECT IMMUNE_FIRE
	if ( PARAM2 equals 'cold' ) local CUR_PROTECT IMMUNE_COLD
	if ( PARAM2 equals 'poison' ) local CUR_PROTECT IMMUNE_POISON
	if ( PARAM2 equals 'acid' ) local CUR_PROTECT IMMUNE_ACID
	if ( PARAM2 equals 'lightning' ) local CUR_PROTECT IMMUNE_LIGHTNING

	local OLD_PROTECT CUR_PROTECT
	local IN_RECDUCT PARAM1

	local NEW_PROTECT CUR_PROTECT
	subtract NEW_PROTECT IN_RECDUCT

	if ( NEW_PROTECT < 0 ) local FINAL_REDUCT 0.0

	callevent float_to_percent FINAL_REDUCT

	if ( CUR_PROTECT < OLD_PROTECT ) 
	{
		if PARAM1 > 0
		gplayermessage ent_me You are now more resistant to PARAM2 ( FLOAT_RETURN )
		local DAMAGE_REDUCT FINAL_REDUCT
	}
	if ( CUR_PROTECT > OLD_PROTECT )
	{
		playermessage ent_me You are now more vulnerable to PARAM2 damage! ( FLOAT_RETURN )
		local DAMAGE_REDUCT FINAL_REDUCT
	}
	if ( PARAM1 <= 0 )
	{
		gplayermessage ent_me You are now immuned to PARAM2 damage!
		local DAMAGE_REDUCT 0.0
	}

	takedmg PARAM2 DAMAGE_REDUCT
	callevent set_player_immunes
}

{ ext_flash_bang 
	if $dist(game.monster.origin,PARAM1) < PARAM2
	effect screenfade ent_me 3 1 (255,255,255) 255 fadein
}



{ update_parry

	

	local PARRY_MULTI 1.0

	
	local LEFT_PARRY_SKILL skill.
	stradd LEFT_PARRY_SKILL $get(PLR_LEFT_HAND,scriptvar,'WEAPON_PRIMARY_SKILL')
	local LEFT_PARRY $get(ent_me,LEFT_PARRY_SKILL)
	
	if ( $get(PLR_LEFT_HAND,handpref) == 2 ) local LEFT_PARRY 0 
	if ( $get(PLR_LEFT_HAND,handpref) == 4 )
	{
		if LEFT_PARRY_SKILL isnot skill.martialarts
		multiply LEFT_PARRY 1.5 
	}
	if ( $get(PLR_LEFT_HAND,scriptvar,'AM_SHIELD') )
	{
		local LEFT_PARRY 0
		local LEFT_PARRY_MULTI $get(PLR_LEFT_HAND,scriptvar,'PARRY_MULTI_OUT')
		if ( LEFT_PARRY_MULTI isnot not_equipped ) add PARRY_MULTI LEFT_PARRY_MULTI
	}

	
	local RIGHT_PARRY_SKILL skill.
	stradd RIGHT_PARRY_SKILL $get(PLR_RIGHT_HAND,scriptvar,'WEAPON_PRIMARY_SKILL')
	local RIGHT_PARRY $get(ent_me,RIGHT_PARRY_SKILL)
	
	if ( $get(PLR_RIGHT_HAND,handpref) == 2 ) local RIGHT_PARRY 0 
	if ( $get(PLR_RIGHT_HAND,handpref) == 4 )
	{
		if RIGHT_PARRY_SKILL isnot skill.martialarts
		multiply RIGHT_PARRY 1.5 
	}
	if ( $get(PLR_RIGHT_HAND,scriptvar,'AM_SHIELD') )
	{
		local RIGHT_PARRY 0
		local RIGHT_PARRY_MULTI $get(PLR_RIGHT_HAND,scriptvar,'PARRY_MULTI_OUT')
		if ( RIGHT_PARRY_MULTI isnot not_equipped ) add PARRY_MULTI RIGHT_PARRY_MULTI
	}

	local TOTAL_PARRY RIGHT_PARRY
	add TOTAL_PARRY LEFT_PARRY

	if ( PARRY_MULTI > 1.0 ) subtract PARRY_MULTI 1.0

	if ( TOTAL_PARRY == 0 )
	{
		
		if PARRY_MULTI > 1.0
		local TOTAL_PARRY $get(ent_me,skill.martialarts)
	}

	multiply TOTAL_PARRY PARRY_MULTI
	local TOTAL_PARRY $int(TOTAL_PARRY)

	local OLD_PARRY PL_PARRY
	setvard PL_PARRY TOTAL_PARRY


	consolemsg ent_me Parry_Value: ( left_v: $int(LEFT_PARRY) ) ( right_v: $int(RIGHT_PARRY) ) ( multi: PARRY_MULTI ) = TOTAL_PARRY
	if ( OLD_PARRY != PL_PARRY ) yplayermessage ent_me Your Parry value is now TOTAL_PARRY
	setstat parry TOTAL_PARRY

	callevent 0.5 set_two_hand
	callevent 1.0 set_swift_blade
}

{ set_two_hand
	
	local PLR_LEFT_HAND_TYPE $get(PLR_LEFT_HAND,itemname)
	local PLR_RIGHT_HAND_TYPE $get(PLR_RIGHT_HAND,itemname)
	

	if ( PLR_LEFT_HAND_TYPE startswith bows_ )
	{
		if PLR_RIGHT_HAND_TYPE startswith bows_
		local L_NO_REDUCT 1
	}
	if ( PLR_LEFT_HAND_TYPE startswith shield ) local L_NO_REDUCT 1
	if ( PLR_RIGHT_HAND_TYPE startswith shield ) local L_NO_REDUCT 1
	if ( PLR_RIGHT_HAND_TYPE startswith magic_hand_ ) local L_NO_REDUCT 1
	if ( PLR_LEFT_HAND_TYPE startswith magic_hand_ ) local L_NO_REDUCT 1
	if ( $get(PLR_LEFT_HAND,handpref) == 2 ) local L_NO_REDUCT 1 
	if ( $get(PLR_RIGHT_HAND,handpref) == 2 ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE contains gauntlet ) local L_NO_REDUCT 1
	if ( PLR_LEFT_HAND equals PLR_RIGHT_HAND ) local L_NO_REDUCT 1
	if ( PLR_LEFT_HAND equals 0 ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND equals 0 ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith item ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith item ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith mana ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith mana ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith health ) local L_NO_REDUCT 1 
	if ( PLR_RIGHT_HAND_TYPE startswith health ) local L_NO_REDUCT 1 

	if ( $get(PLR_RIGHT_HAND,scriptvar,'MATCHED_SET') )
	{
		local L_MATCHED_SET_TYPE $get(PLR_RIGHT_HAND,scriptvar,'MATCHED_SET_TYPE')
		if ( $get(PLR_LEFT_HAND,scriptvar,'MATCHED_SET_TYPE') equals L_MATCHED_SET_TYPE )
		{
			local L_MATCHED_SET 1
			local L_NO_REDUCT 1
			setvard PLR_HAS_MATCHED_SET 1
		}
		else
		{
			setvard PLR_HAS_MATCHED_SET 0
		}
	}
	else
	{
		setvard PLR_HAS_MATCHED_SET 0
	}

	if ( !L_NO_REDUCT )
	{
		local OLD_REDUCT PLR_2H_REDUCT
		if ( PLR_LEFT_HAND_TYPE contains smallarms_ ) local SA_REDUCT 1
		if ( PLR_RIGHT_HAND_TYPE contains smallarms_ ) local SA_REDUCT 1
		if ( PLR_LEFT_HAND_TYPE equals PLR_RIGHT_HAND_TYPE ) local SA_REDUCT 1

		if ( SA_REDUCT )
		{
			setvard PLR_2H_REDUCT PLR_2HPEN_LIGHT 
			if ( OLD_REDUCT != PLR_2H_REDUCT ) yplayermessage ent_me Dual-wield penalty: PLR_2HPEN_LIGHT_REPORT (off-hand is light or matches)
		}
		else
		{
			setvard PLR_2H_REDUCT PLR_2HPEN_HEAVY 
			if ( OLD_REDUCT != PLR_2H_REDUCT ) yplayermessage ent_me Dual-wield penalty: PLR_2HPEN_HEAVY_REPORT
		}

	}
	else
	{
		local OLD_REDUCT PLR_2H_REDUCT
		setvard PLR_2H_REDUCT 1.0
		if ( OLD_REDUCT != PLR_2H_REDUCT )
		{
			if ( !PLR_HAS_MATCHED_SET )
			{
				yplayermessage ent_me No dual-wield penalty.
			}
		}

		if ( PLR_HAS_MATCHED_SET )
		{
			if !PLR_HAD_MATCHED_SET
			yplayermessage ent_me No dual-wield penalty due to matched set!
			setvard PLR_HAD_MATCHED_SET 1
		}
		else
		{
			setvard PLR_HAD_MATCHED_SET 0
		}
	}
}

{ set_swift_blade
	

	local ACTIVE_WEAPON $get(ent_me,active_item)

	local ACTIVE_SCRIPT  $get(ACTIVE_WEAPON,itemname)

	if ( ACTIVE_SCRIPT contains bows_ )
	{
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( ACTIVE_SCRIPT contains magic_hand_ )
	{
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PLR_SWIFT_BLADE == 0 )
	{
		if ( $get(ACTIVE_WEAPON,scriptvar,'ITEM_SWIFT_BLADE') )
		{
			callexternal ACTIVE_WEAPON ext_item_swift_blade remove
		}
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( !$get(ACTIVE_WEAPON,scriptvar,'ITEM_SWIFT_BLADE') )
	{
		local CUR_SPEED $get_attackprop(ACTIVE_WEAPON,0,delay.end)
		local CUR_STRIKE $get_attackprop(ACTIVE_WEAPON,0,delay.strike)
	}
	else
	{
		local CUR_SPEED $get(ACTIVE_WEAPON,scriptvar,'ITEM_BASE_SPEED')
		local CUR_STRIKE $get(ACTIVE_WEAPON,scriptvar,'ITEM_BASE_STRIKE')
	}

	callexternal ACTIVE_WEAPON ext_item_swift_blade CUR_SPEED CUR_STRIKE

	multiply CUR_SPEED PLR_SWIFT_BLADE
	multiply CUR_STRIKE PLR_SWIFT_BLADE
	multiply CUR_STRIKE 0.75

	local VIEWANIM_SPEED $get(ACTIVE_WEAPON,scriptvar,'BWEAPON_BASE_ANIM_SPEED')
	if ( CUR_SPEED > 0 ) divide VIEWANIM_SPEED CUR_SPEED
	setviewmodelprop ACTIVE_WEAPON animspeed VIEWANIM_SPEED


	attackprop ACTIVE_WEAPON 0 delay.end CUR_SPEED
	attackprop ACTIVE_WEAPON 0 delay.strike CUR_STRIKE
}

{ ext_set_swift_blade
	setvard PLR_SWIFT_BLADE PARAM1
}

{ game_equipped
	
	if ( G_DEVELOPER_MODE ) callevent 1.0 report_hands
	callevent ext_set_hand_id $get(PARAM1,hand_index) $get(PARAM1,id)

	if G_DEVELOPER_MODE
	local L_H0 $get(ent_me,hand0)
	local L_H1 $get(ent_me,hand1)

	local L_H0 $get(L_H0,itemname)
	local L_H1 $get(L_H1,itemname)


	gplayermessage G_DEV_PLAYER h0: L_H0 h1: L_H1

}

{ ext_set_hand_id 

	

	

	
	setvard PL_ACTIVE_SKILL $get(PARAM2,scriptvar,'WEAPON_PRIMARY_SKILL')
	if ( PARAM1 equals 0 )
	{
		setvard PLR_RIGHT_HAND PARAM2
		callexternal PLR_LEFT_HAND ext_off_hand_change PLR_RIGHT_HAND
	}
	if ( PARAM1 equals 1 )
	{
		setvard PLR_LEFT_HAND PARAM2
		callexternal PLR_RIGHT_HAND ext_off_hand_change PLR_LEFT_HAND
	}
	if ( PARAM1 equals 2 )
	{
		setvard PLR_LEFT_HAND PARAM2
		setvard PLR_RIGHT_HAND PARAM2
		callexternal PLR_RIGHT_HAND ext_off_hand_change
	}

	if ( PARAM1 equals 3 )
	{
		if ( PLR_LEFT_HAND isnot PLR_RIGHT_HAND ) callexternal PLR_RIGHT_HAND ext_off_hand_change PLR_RIGHT_HAND
		if ( PLR_RIGHT_HAND isnot PLR_LEFT_HAND ) callexternal PLR_LEFT_HAND ext_off_hand_change PLR_RIGHT_HAND
	}

	
	setvard PLR_ACTIVE_WEAPON PARAM2
	if ( $get(PLR_ACTIVE_WEAPON,scriptvar,'SPECIAL_WEAPON') )
	{
		setvard PLR_SPECIAL_WEAPON 1
	}
	else
	{
		setvard PLR_SPECIAL_WEAPON 0
	}

	
	callevent 0.1 update_parry
}

{ ext_sphere_token_x 
	setvard PLR_SCAN_TOKEN $get_tsphere(PARAM1,PARAM2)
	
}

{ ext_sphere_token 
	setvard PLR_SCAN_TOKEN $get_tsphere(PARAM1,PARAM2,PARAM3)
	
}

{ ext_sphere_token_byrange 
	setvard PLR_SCAN_TOKEN $get_tsphere(PARAM1,PARAM2,PARAM3)
	if PLR_SCAN_TOKEN isnot none
	setvard PLR_SCAN_TOKEN $sort_entlist(PLR_SCAN_TOKEN,range)
}

{ ext_box_token 
	setvard PLR_SCAN_TOKEN $get_tbox(PARAM1,PARAM2,PARAM3)
	
}

{ ext_tokentest_set 

	if ( TOKEN_TEST equals 'TOKEN_TEST' ) setvard TOKEN_TEST "test1;test2;test3;test4;test5;"
	setvard OLD_TOKEN $get_token(TOKEN_TEST,PARAM1) 
	setvard TOKEN_IDX PARAM1
	token.set TOKEN_TEST PARAM1 PARAM2
	callevent 0.1 ext_toktentest_display
}

{ ext_tokentest_scramble 

	if ( TOKEN_TEST equals 'TOKEN_TEST' ) setvard TOKEN_TEST "test1;test2;test3;test4;test5;"
	token.scramble TOKEN_TEST
	consolemsg ent_me resetting token set now: TOKEN_TEST
}

{ ext_toktentest_display
	consolemsg ent_me changed OLD_TOKEN to $get_token(TOKEN_TEST,TOKEN_IDX) string: TOKEN_TEST
}

{ ext_tokentest_reset
	consolemsg ent_me resetting token test string
	setvard TOKEN_TEST "test1;test2;test3;test4;test5;"
}

{ ext_tokentest_find 
	local TOKEN_FIND $get_find_token(TOKEN_TEST,PARAM1)
	if ( TOKEN_FIND > -1 ) consolemsg ent_me Token PARAM1 found at index TOKEN_FIND
	if ( TOKEN_FIND == -1 ) consolemsg ent_me Token PARAM1 not found in TOKEN_TEST
}

{ ext_set_effect_lastdmg
	setvard EXT_LAST_EFFECT_DMG game.time
}


{ ext_potion_stability

	if ( PARAM1 equals end )
	{
		setvard IMMUNE_STUN 0
		setvard PL_IMMUNE_STUN 0
		setvard IMMUNE_PUSH 0
		setvard PL_IMMUNE_PUSH 0
		setvard PL_STABILITY_POTION 0
	}
	if PARAM1 isnot end 
	setvard IMMUNE_STUN 100
	setvard IMMUNE_PUSH 1
	setvard PL_IMMUNE_PUSH 1
	setvard PL_STABILITY_POTION 0

	
}

{ ext_set_vote_delay
	setvard VOTE_DISABLED 1
	callevent 20.0 reset_vote_delay
}

{ reset_vote_delay
	setvard VOTE_DISABLED 0
}

{ ext_reset_names
	quest set ent_me n 0
}

{ ext_update_score 
	setvard PLR_TOTAL_DMG PARAM1
}


{ ext_last_artie_used 
	setvard PLR_LAST_ARTIE PARAM1
}

{ ext_player_gag
	gagplayer $get(ent_me,id) 1
	if ( PARAM1 startswith PARAM )
	{
		local MUTE_FOREVAH 1
		local TIME_STR " "
	}
	else
	{
		local TIME_STR "Your speech will be restored in "
		strconc TIME_STR $int(PARAM1) minutes
	}
	
	gplayermessage ent_me The admin gods have sewn your lips shut!
	infomsg ent_me "The admin gods have sewn your lips shut!" TIME_STR
	playsound 0 10 scientist/shutup.wav
	if !MUTE_FOREVAH
	
	local MINUTES_TO_UNGAG PARAM1
	multiply MINUTES_TO_UNGAG 60
	callevent MINUTES_TO_UNGAG ext_player_ungag
}

{ ext_player_ungag
	gplayermessage ent_me The admin gods have chosen to restore your speech.
	gagplayer $get(ent_me,id) 0
}

{ ext_send_tele_point 
	
	playsound 0 10 magic/spawn.wav
	setorigin $get(ent_me,id) PARAM1
	if ( G_DEVELOPER_MODE ) gplayermessage ent_me ext_send_tele_point PARAM1
	if FX_SPAMMERS < MAX_FX_SPAMMERS
	setvard OWNER_POS PARAM1
	calleventloop 18 beam_fx2
}

{ beam_fx2

	local BEAM_START OWNER_POS
	local BEAM_END OWNER_POS

	vectoradd BEAM_START $relpos($vec(0,BEAM_ROT,0),$vec(0,32,-32))
	vectoradd BEAM_END $relpos($vec(0,BEAM_ROT,0),$vec(0,32,128))
	effect beam point lgtning.spr 100 BEAM_START BEAM_END (255,0,255) 200 16 3

	add BEAM_ROT 20
}

{ ext_set_last_resist
	setvard PLR_LAST_RESIST_TIME game.time
	setvard PLR_LAST_RESIST_ELM PARAM1
}

{ ext_trouble
	setvard PLR_IS_TROUBLE_MAKER 1
}
{ ext_trouble_off
	setvard PLR_IS_TROUBLE_MAKER 0
}

{ ext_set_next_scroll
	
	setvard PLR_NEXT_SCROLL game.time
	add PLR_NEXT_SCROLL 5.0
}

{ ext_set_reward
	setvard PLR_GOT_REWARD PARAM1
}

{ ext_dridje_sphere
	setvard PLR_DRIDJE_SPHERE PARAM1
}

{ game_drain_death 
	
	dodamage ent_me direct 100 100% PARAM1 target
}

{ ext_dmgmulti
	
	dmgmulti PARAM1
}




{ ext_hoptest
	addvelocity ent_me $relvel(0,200,1000)
}

{ ext_setviewmodel
	setviewmodelprop PLR_ACTIVE_WEAPON submodel PARAM1 PARAM2
}

{ ext_token_range_test
	setvard ENT_LIST $get_tsphere(any,1024)
	if ENT_LIST isnot none
	setvard ENT_LIST $sort_entlist(ENT_LIST,'range')
	
	consolemsg ent_me Nearest to farthest
	calleventloop $get_token_amt(ENT_LIST) ext_token_range_test_list
}

{ ext_token_range_test_list
	local CUR_IDX game.script.iteration
	local CUR_ENT $get_token(ENT_LIST,CUR_IDX)
	consolemsg ent_me # CUR_IDX $get(CUR_ENT,name)
}

{ ext_set_map 

	

	if ( game.time > 5.0 )
	{
		quest set ent_me m PARAM1
		quest set ent_me d PARAM2 
		settrans PARAM2
	}
	setvard PLR_NEXT_TRANSITION PARAM3

	setvard PLR_CUR_TRANS PARAM2
	setvard PLR_NEXT_TRANS PARAM3
	
	callevent 0.1 ext_set_transitions
}

{ ext_set_transitions

	if PLR_IN_WORLD
	settrans ent_me PLR_CUR_TRANS
	setvarg G_FIRST_TRANS PLR_NEXT_TRANS
}


{ ext_trans_info
	
}


{ ext_scan_pet
	setvard T_SPHERE $get_tsphere(enemy,512)
	setvard TARGET_PET_TYPE PARAM1
	if T_SPHERE isnot none
	calleventloop $get_token_amt(T_SPHERE) ext_scan_pet_loop
}

{ ext_scan_pet_loop
	local CUR_TARGET $get_token(T_SPHERE,game.script.iteration)
	local PET_TYPE $get(CUR_TARGET,scriptvar,'NPC_PET_TYPE')
	if PET_TYPE equals TARGET_PET_TYPE
	setvard PLR_FOUND_PET CUR_TARGET
}



{ ext_hit_chance
	hitmulti ent_me PARAM1
}

{ ext_set_frozen 
	setvard I_R_FROZEN 1
	scriptflags ent_me add ext_set_frozen nopush 1 PARAM1 none
	callevent PARAM1 ext_set_unfrozen
}

{ ext_set_unfrozen
	setvard I_R_FROZEN 0
}

{ ext_glow_block
	setvard PLR_GLOW_BLOCK PARAM1
	if ( PLR_GLOW_BLOCK )
	{
		if PLR_HAS_GLOW
		playsound 2 10 magic/elecidlepop.wav
		playermessage ent_me Your light is snuffed out!
		callexternal GAME_MASTER gm_light_update remove $get(ent_me,index) $vec(COLOR_RATIO_R,COLOR_RATIO_G,COLOR_RATIO_B) RAD_RATIO
		callevent ext_set_glow 0
	}
	else
	{
		playermessage ent_me You have left the influence of the dark foce.
	}
}

{ ext_ent_list_sort 
   local SCAN_TYPE PARAM1
   local SCAN_RANGE PARAM2
   local SCAN_ORIGIN PARAM3
   local SORT_TYPE PARAM4
   callevent ext_sphere_token SCAN_TYPE SCAN_RANGE SCAN_ORIGIN
   setvard PLR_SCAN_TOKEN_SORTED $sort_entlist(PLR_SCAN_TOKEN,SORT_TYPE)
}

{ ext_fire_aura_activate 

	if !PLR_FAURA
	setvard PLR_FAURA 1
	setvard PLR_FAURA_DOT PARAM1
	setvard PLR_FAURA_AOE PARAM2
	

	setvard GAME_PVP game.pvp
	clientevent new all items/armor_faura_cl $get(ent_me,index) PLR_FAURA_AOE PLR_FAURA_CL_RATE
	setvard PLR_FAURA_CLIDX game.script.last_sent_id
	setvard PLR_FAURA_NEXT_CL game.time
	add PLR_FAURA_NEXT_CL PLR_FAURA_CL_RATE
	callevent fire_aura_loop
}

{ fire_aura_loop
	if PLR_FAURA
	callevent 1.0 fire_aura_loop

	local GAME_TIME game.time

	if ( GAME_TIME > PLR_FAURA_NEXT_CL )
	{
		clientevent new all items/armor_faura_cl $get(ent_me,index) PLR_FAURA_AOE PLR_FAURA_CL_RATE
		setvard PLR_FAURA_CLIDX game.script.last_sent_id
		setvard PLR_FAURA_NEXT_CL GAME_TIME
		add PLR_FAURA_NEXT_CL PLR_FAURA_CL_RATE
	}

	if $get(ent_me,isalive)
	local SCAN_AOE PLR_FAURA_AOE
	multiply SCAN_AOE 3
	setvard PLR_FAURA_SCAN $get_tsphere(enemy,SCAN_AOE) 
	
	if PLR_FAURA_SCAN isnot none
	
	calleventloop $get_token_amt(PLR_FAURA_SCAN) fire_aura_burn
}

{ fire_aura_burn
	local CUR_TARG $get_token(PLR_FAURA_SCAN,game.script.iteration)

	if $get(CUR_TARG,range) < PLR_FAURA_AOE

	if $get(CUR_TARG,relationship,ent_me) equals enemy
	if ( !GAME_PVP )
	{
		if $get(CUR_TARG,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if ( !IS_AFK ) applyeffect CUR_TARG effects/effect_burn 5.0 $get(ent_me,id) PLR_FAURA_DOT 1 1 spellcasting.fire
}

{ ext_fire_aura_remove
	setvard PLR_FAURA 0
	clientevent update all PLR_FAURA_CLIDX remove_me
}

{ ext_poison_aura_activate 

	if !PLR_PAURA
	setvard PLR_PAURA 1
	setvard PLR_PAURA_DOT PARAM1
	setvard PLR_PAURA_AOE PARAM2
	

	setvard GAME_PVP game.pvp
	clientevent new all effects/sfx_poison_aura $get(ent_me,index) PLR_PAURA_AOE PLR_PAURA_CL_RATE
	setvard PLR_PAURA_CLIDX game.script.last_sent_id
	setvard PLR_PAURA_NEXT_CL game.time
	add PLR_PAURA_NEXT_CL PLR_PAURA_CL_RATE
	callevent poison_aura_loop
}

{ poison_aura_loop
	if PLR_PAURA
	callevent 1.0 poison_aura_loop

	local GAME_TIME game.time

	if ( GAME_TIME > PLR_PAURA_NEXT_CL )
	{
		clientevent new all effects/sfx_poison_aura $get(ent_me,index) PLR_PAURA_AOE PLR_PAURA_CL_RATE
		setvard PLR_PAURA_CLIDX game.script.last_sent_id
		setvard PLR_PAURA_NEXT_CL GAME_TIME
		add PLR_PAURA_NEXT_CL PLR_PAURA_CL_RATE
	}

	if $get(ent_me,isalive)
	setvard PLR_PAURA_SCAN $get_tsphere(enemy,PLR_PAURA_AOE)
	
	if PLR_PAURA_SCAN isnot none
	
	calleventloop $get_token_amt(PLR_PAURA_SCAN) poison_aura_burn
}

{ poison_aura_burn
	local CUR_TARG $get_token(PLR_PAURA_SCAN,game.script.iteration)
	if $get(CUR_TARG,relationship,ent_me) equals enemy
	if ( !GAME_PVP )
	{
		if $get(CUR_TARG,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if ( !IS_AFK ) applyeffect CUR_TARG effects/effect_poison 5.0 $get(ent_me,id) PLR_PAURA_DOT 1 spellcasting.affliction
}

{ ext_poison_aura_remove
	setvard PLR_PAURA 0
	clientevent update all PLR_PAURA_CLIDX remove_me
}


{ ext_setmodelbody
	
	setmodelbody PARAM1 PARAM2
}

{ ext_setbodytype 

	

	setvard PLR_PREV_BODY_TYPE PLR_BODY_TYPE
	setvard PLR_BODY_TYPE PARAM1

	if ( PARAM2 isnot remove ) setvard PLR_LAST_WORN_ARMOR PARAM2

	local GENDER_ADJ 1
	if ( PLR_GENDER equals female )
	{
		if ( $get(ent_me,race) equals human ) add GENDER_ADJ 1
		if ( G_DEVELOPER_MODE ) messageall green ext_setbodytype is_female
	
	}

	if ( PARAM1 startswith 'PARAM' )
	{
		
		setvard PLR_BODY_TYPE PLR_PREV_BODY_TYPE
	}
	else
	{
		
		setvard PLR_PREV_BODY_TYPE PLR_BODY_TYPE
	}

	if ( PLR_BODY_TYPE equals normal )
	{
		setmodelbody 0 GENDER_ADJ	
		setmodelbody 1 GENDER_ADJ	
		setmodelbody 2 GENDER_ADJ	
		setmodelbody 3 GENDER_ADJ	
	}
	if ( PLR_BODY_TYPE equals leather ) 
	{
		setmodelbody 0 GENDER_ADJ	
		setmodelbody 1 GENDER_ADJ	
		setmodelbody 2 0			
		setmodelbody 3 GENDER_ADJ	
	}
	if ( PLR_BODY_TYPE equals platemail ) 
	{
		setmodelbody 0 0			
		setmodelbody 1 GENDER_ADJ	
		setmodelbody 2 0			
		setmodelbody 3 0			
	}

}

{ ext_register_helm
	setvard PLR_HELM_ID PARAM1
	if PLR_HELM_ID isnot none
	callclitemevent PLR_HELM_ID game_show $get(ent_me,race) $get(ent_me,gender) ext_register_helm
}

{ ext_register_armor
	setvard PLR_ARMOR_ID PARAM1
	if PLR_ARMOR_ID isnot none
	callclitemevent PLR_ARMOR_ID barmor_update_vest $get(ent_me,race) $get(ent_me,gender) ext_register_armor
}

{ ext_dwarf_test
	setmodelbody 0 1 
	setmodelbody 1 2 
	setmodelbody 2 1 
	callevent ext_hide_armor
}

{ ext_hide_armor
	callexternal PLR_HELM_ID ext_hide
}


{ ext_setheadtype 
	
	local GENDER_ADJ 0
	if ( PLR_GENDER equals female )
	{
		if ( $get(ent_me,race) equals human ) add GENDER_ADJ 2
	}
	add PARAM1 GENDER_ADJ
	setmodelbody 0 PARAM1
}

{ ext_setmodel 
	setmodel dwarf/reference.mdl
}

{ ext_setrender
	
	setrender PARAM1
}

{ ext_tod_lock
	setvard PLR_TOD_LOCK PARAM1
	clientevent update ent_me const.localplayer.scriptID lock_tod PLR_TOD_LOCK
}

{ ext_viewdist
	clientevent update ent_me const.localplayer.scriptID set_view_dist PARAM1
}

{ ext_urdual_slow
	setvard PLR_URDUAL_RELEASE_TIME PARAM1
}

{ ext_set_gender
	setvard PLR_GENDER $get(ent_me,gender) 
	clientevent update ent_me const.localplayer.scriptID set_cl_gender_race $get(ent_me,gender) $get(ent_me,race)
	if ( PLR_GENDER equals female )
	{
		clientcmd ent_me "ms_clgender 1"
		if ( $get(ent_me,race) equals human )
		{
			
			
			setmodelbody 0 2
			setmodelbody 1 2
			setmodelbody 2 2
			setmodelbody 3 2
			setvard PLR_SOUND_SWORDREADY SOUNDSET_HF_SWORDREADY
			setvard PLR_SOUND_SHOUT1 SOUNDSET_HF_SHOUT1
			setvard PLR_SOUND_JAB1 SOUNDSET_HF_JAB1
			setvard PLR_SOUND_JAB2 SOUNDSET_HF_JAB2
			setvard PLR_SOUND_BREATHFAST1 SOUNDSET_HF_BREATHFAST1
			setvard PLR_SOUND_BREATHFAST2 SOUNDSET_HF_BREATHFAST2
			setvard PLR_SOUND_BREATHFAST3 SOUNDSET_HF_BREATHFAST3
			setvard PLR_SOUND_DEATH SOUNDSET_HF_DEATH
			setvard PLR_SOUND_CHESTHIT1 SOUNDSET_HF_CHESTHIT1
			setvard PLR_SOUND_STOMACHHIT1 SOUNDSET_HF_STOMACHHIT1
			setvard PLR_SOUND_ARMHIT1 SOUNDSET_HF_ARMHIT1
			setvard PLR_SOUND_LEGHIT1 SOUNDSET_HF_LEGHIT1
			setvard PLR_SOUND_FALLPAIN1 SOUNDSET_HF_FALLPAIN1
			setvard PLR_SOUND_FALLPAIN2 SOUNDSET_HF_FALLPAIN2
			setvard PLR_SOUND_FALLPAIN3 SOUNDSET_HF_FALLPAIN3
			setvard PLR_SOUND_FALLPAIN4 SOUNDSET_HF_FALLPAIN4
		}
	}
	else
	{
		clientcmd ent_me "ms_clgender 0"
		if ( $get(ent_me,race) equals human )
		{
			
			
			setmodelbody 0 1
			setmodelbody 1 1
			setmodelbody 2 1
			setmodelbody 3 1
			setvard PLR_SOUND_SWORDREADY SOUNDSET_HM_SWORDREADY 
			setvard PLR_SOUND_SHOUT1 SOUNDSET_HM_SHOUT1 
			setvard PLR_SOUND_JAB1 SOUNDSET_HM_JAB1 
			setvard PLR_SOUND_JAB2 SOUNDSET_HM_JAB2 
			setvard PLR_SOUND_BREATHFAST1 SOUNDSET_HM_BREATHFAST1 
			setvard PLR_SOUND_BREATHFAST2 SOUNDSET_HM_BREATHFAST2 
			setvard PLR_SOUND_BREATHFAST3 SOUNDSET_HM_BREATHFAST3 
			setvard PLR_SOUND_DEATH SOUNDSET_HM_DEATH
			setvard PLR_SOUND_CHESTHIT1 SOUNDSET_HM_CHESTHIT1
			setvard PLR_SOUND_STOMACHHIT1 SOUNDSET_HM_STOMACHHIT1
			setvard PLR_SOUND_ARMHIT1 SOUNDSET_HM_ARMHIT1
			setvard PLR_SOUND_LEGHIT1 SOUNDSET_HM_LEGHIT1
			setvard PLR_SOUND_FALLPAIN1 SOUNDSET_HM_FALLPAIN1
			setvard PLR_SOUND_FALLPAIN2 SOUNDSET_HM_FALLPAIN2
			setvard PLR_SOUND_FALLPAIN3 SOUNDSET_HM_FALLPAIN3
			setvard PLR_SOUND_FALLPAIN4 SOUNDSET_HM_FALLPAIN4
		}
	}
}

{ ext_helm_expar
	
	clientevent update ent_me $get(PLR_HELM_ID,index) game_show $get(ent_me,race) $get(ent_me,gender) ext_helm_expar
}

{ ext_set_spiral 

	local SPIRAL_TYPE PARAM1
	setvard SPIRAL_DMG PARAM2

	setvard SPIRAL_SKILL archery

	
	

	if ( SPIRAL_TYPE equals fire )
	{
		setvard SPIRAL_DMG_TYPE fire_effect
		setvard SPIRAL_SPIRTE_FILE rjet1.spr
		setvard SPIRAL_SPRITE_FRAMES 5
		setvard SPIRAL_SPRITE_SCALE 0.75
		setvard SPIRAL_SPRITE_COLOR (255,255,255)
		setvard SPIRAL_GLOW_COLOR (255,0,0)
	}

	if ( SPIRAL_TYPE equals cold )
	{
		setvard SPIRAL_DMG_TYPE cold_effect
		setvard SPIRAL_SPIRTE_FILE char_breath.spr
		setvard SPIRAL_SPRITE_FRAMES 1
		setvard SPIRAL_SPRITE_SCALE 2.5
		setvard SPIRAL_SPRITE_COLOR (255,255,255)
		setvard SPIRAL_GLOW_COLOR (128,128,255)
	}

	if ( SPIRAL_TYPE equals lightning )
	{
		setvard SPIRAL_DMG_TYPE lightning_effect
		setvard SPIRAL_SPIRTE_FILE 3dmflaora.spr
		setvard SPIRAL_SPRITE_FRAMES 1
		setvard SPIRAL_SPRITE_SCALE 0.5
		setvard SPIRAL_SPRITE_COLOR (255,255,0)
		setvard SPIRAL_GLOW_COLOR (255,255,0)
	}
}

{ ext_bear_mode
	if !PLR_BEAR_MODE
	setvard PLR_BEAR_IMAGE_ID PARAM1
	setvard PLR_BEAR_MODE 1

	
	
	
	
	

	callevent ext_invis

	scriptflags ent_me add bear nopush 1 -1 none
	effect screenshake $relpos(0,0,0) 256 10 1 256
}

{ ext_bear_mode_end
	if PLR_BEAR_MODE
	
	setvard PLR_BEAR_MODE 0

	callevent ext_visible

	scriptflags ent_me remove bear
	
}

{ ext_test

}

{ ext_block_weather 
	setvard PLR_WEATHER_BLOCK PARAM1
}

{ ext_stam 
	drainstamina ent_me PARAM1
}

{ ext_stuck_adj
	if game.time > PLR_NEXT_STUCK_ADJ
	setvard PLR_NEXT_STUCK_ADJ game.time
	add PLR_NEXT_STUCK_ADJ 5.0
	local ADJ_ORG $get(ent_me,origin)
	vectoradd ADJ_ORG z 8
	local TRACE_START ADJ_ORG
	local TRACE_END ADJ_ORG
	vectoradd TRACE_END z 38
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
	if ( TRACE_LINE isnot TRACE_END )
	{
		dplayermessage ent_me "[/STUCK]: Not enough head room to adjust up."
	}
	else
	{
		setorigin ent_me ADJ_ORG
	}
}

{ ext_olof_setstatus
	
	setvard PLR_OLOF_STATUS PARAM1
	if ( PARAM1 equals reset ) setvard PLR_OLOF_STATUS 'PLR_OLOF_STATUS' 
}

{ ext_scarab_latch
	add SCARABS_ATTACHED PARAM1
	if ( SCARABS_ATTACHED < 0 ) setvard SCARABS_ATTACHED 0
}

{ ext_reset_model
	callevent ext_set_gender
	callevent 0.1 ext_setbodytype
}

{ ext_invis
	
	setprop ent_me rendermode 5
	setprop ent_me renderamt 1 
	callexternal all ext_render_items $get(ent_me,id) 5 0
}

{ ext_visible
	
	setprop ent_me rendermode 0
	setprop ent_me renderamt 255
	callexternal all ext_render_items $get(ent_me,id) 0 255
}

{ ext_hold_person 
	scriptflags ent_me add hold_person nopush 1 PARAM1 none
	
}


{ ext_acid_feaura_activate 

	if !PLR_FEAURA
	setvard PLR_FEAURA 1
	setvard PLR_FEAURA_DOT PARAM1
	setvard PLR_FEAURA_AOE PARAM2

	setvard GAME_PVP game.pvp
	clientevent new all effects/sfx_poison_aura $get(ent_me,index) PLR_FEAURA_AOE PLR_PAURA_CL_RATE 
	setvard PLR_FEAURA_CLIDX game.script.last_sent_id
	setvard PLR_FEAURA_NEXT_CL game.time
	add PLR_FEAURA_NEXT_CL PLR_PAURA_CL_RATE
	callevent acid_feaura_loop
}

{ acid_feaura_loop
	if PLR_FEAURA
	callevent 1.0 acid_feaura_loop

	local GAME_TIME game.time

	if ( GAME_TIME > PLR_FEAURA_NEXT_CL )
	{
		clientevent new all effects/sfx_poison_aura $get(ent_me,index) PLR_FEAURA_AOE PLR_PAURA_CL_RATE
		setvard PLR_FEAURA_CLIDX game.script.last_sent_id
		setvard PLR_FEAURA_NEXT_CL GAME_TIME
		add PLR_FEAURA_NEXT_CL PLR_PAURA_CL_RATE
	}

	if $get(ent_me,isalive)
	setvard PLR_FEAURA_SCAN $get_tsphere(enemy,PLR_FEAURA_AOE)
	if PLR_FEAURA_SCAN isnot none
	calleventloop $get_token_amt(PLR_FEAURA_SCAN) acid_feaura_burn
}

{ acid_feaura_burn
	local CUR_TARG $get_token(PLR_FEAURA_SCAN,game.script.iteration)
	if $get(CUR_TARG,relationship,ent_me) equals enemy
	if ( !GAME_PVP )
	{
		if $get(CUR_TARG,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if ( !IS_AFK ) applyeffect CUR_TARG effects/effect_acid 5.0 $get(ent_me,id) PLR_FEAURA_DOT 1 spellcasting.affliction
}

{ ext_acid_feaura_remove
	setvard PLR_FEAURA 0
	clientevent update all PLR_FEAURA_CLIDX remove_me
}

{ ext_force_itemevent 

	callclitemevent PARAM1 PARAM2
}

{ ext_weather_change

	if PLR_IN_WORLD

	

	setvard PLR_WEATHER PARAM1

	if ( G_WEATHER_LOCK isnot 0 )
	{
		setvard PLR_WEATHER G_WEATHER_LOCK
	}
	else
	{
		if PLR_LOCK_CLEAR
		setvard PLR_WEATHER clear
	}

	if ( PLR_WEATHER equals storm ) setvard PLR_WEATHER rain_storm

	clientevent update ent_me const.localplayer.scriptID weather_change PLR_WEATHER
}

{ ext_weather_force_change
	

	setvard PLR_WEATHER PARAM1

	clientevent update ent_me const.localplayer.scriptID weather_force_change PARAM1
}


{ ext_repel_shield 

	

	local L_DUR PARAM1
	callevent L_DUR ext_end_repel_shield
	setvard PLR_REPEL_SHIELD_ACTIVE 1
	setvard PLR_REPEL_SHIELD_RADIUS PARAM2
	setvard PLR_REPEL_SHIELD_SPECIAL PARAM3
	setvard GAME_PVP game.pvp
	callevent loop_repel_shield

	if ( PLR_REPEL_SHIELD_SPECIAL equals darkfire )
	{
		clientevent new all effects/sfx_raura $get(ent_me,index) L_DUR
		setvard PLR_DAURA_CL_IDX game.script.last_sent_id
		svplaysound 1 10  magic/chant_loop.wav
	}
}

{ loop_repel_shield

	if PLR_REPEL_SHIELD_ACTIVE
	callevent 0.2 loop_repel_shield
	setvard PLR_REPEL_SHIELD_TARGETS $get_tsphere(enemy,PLR_REPEL_SHIELD_RADIUS)
	
	if PLR_REPEL_SHIELD_TARGETS isnot none
	calleventloop $get_token_amt(PLR_REPEL_SHIELD_TARGETS) affect_targets_repel_shield
}

{ affect_targets_repel_shield
	local CUR_TARG $get_token(PLR_REPEL_SHIELD_TARGETS,game.script.iteration)
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PLR_REPEL_SHIELD_SPECIAL equals darkfire )
	{
		local SHADOW_DMG $get(ent_me,skill.spellcasting.affliction)
		
		applyeffect CUR_TARG effects/effect_shadowflames 5 $get(ent_me,id) SHADOW_DMG
	}

	local TARG_HP $get(CUR_TARG,hp)
	local MY_HP_X $get(ent_me,maxhp)
	multiply MY_HP_X 4
	if ( TARG_HP < 1500 ) local DO_PUSH 1
	if ( TARG_HP < MY_HP_X ) local DO_PUSH 1
	if DO_PUSH
	local TARG_ORG $get(CUR_TARG,origin)
	local MY_ORG $get(ent_me,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	if ( game.time > NEXT_REPEL_SHIELD_SOUND )
	{
		setvard NEXT_REPEL_SHIELD_SOUND game.time
		add NEXT_REPEL_SHIELD_SOUND 0.5
		playsound 0 5 doors/aliendoor3.wav
	}
	setvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,1000,0))
}

{ ext_end_repel_shield
	if ( PLR_REPEL_SHIELD_SPECIAL equals darkfire )
	{
		clientevent update all PLR_DAURA_CL_IDX remove_fx
		svplaysound 1 0 magic/chant_loop.wav
	}
	
	setvard PLR_REPEL_SHIELD_ACTIVE 0
}

{ ext_set_dark_level 
	setvard PLR_DARK_LEVEL PARAM1
	quest set ent_me dl PLR_DARK_LEVEL
	callexternal ent_me player_calc_holy_resistance
}

{ ext_tossprojectile 
	tossprojectile PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8 PARAM9 
	setvard PLR_LAST_PROJECTILE $get(ent_lastprojectile,id)
}

{ ext
	if ( PARAM1 isnot off )
	{
		setvard PLR_DEBUG_PARAM PARAM1
		setvard PLR_DBG_LOOP_ON 1
		callevent ext
	}
	else
	{
		setvard PLR_DBG_LOOP_ON 0
	}
}

{ ext
	if PLR_DBG_LOOP_ON
	callevent 0.1 ext

}

{ ext_sfaura_start
	setvard PLR_SFAURA_SIZE 65
	setvard PLR_SFAURA_ITEM PARAM1 
	setvard GAME_PVP game.pvp
	setvard PLR_SFAURA_DOT $get(ent_me,skill.spellcasting.fire)
	multiply PLR_SFAURA_DOT 0.5
	setvard PLR_SFAURA_MAXPUSH $get(ent_me,maxhp)
	multiply PLR_SFAURA_MAXPUSH 4
	local SIZE_RATIO PLR_SFAURA_SIZE
	divide SIZE_RATIO PLR_SFAURA_MAXSIZE
	clientevent new all effects/sfx_sfaura $get(ent_me,index) SIZE_RATIO 20.0
	setvard PLR_SFAURA_CL_IDX game.script.last_sent_id
	svplaysound 1 10 ambience/rocketrumble1.wav
	setvard PLR_SFAURA_ACTIVE 1
	callevent sfaura_loop
	callevent 20.0 sfaura_fx_refresh
}

{ sfaura_loop
	if PLR_SFAURA_ACTIVE

	if ( $get(ent_me,mp) <= 10 )
	{
		dplayermessage ent_me "Shadowfire Blade: Insufficient mana for Shadowfire Aura"
		callevent ext_sfaura_end
	}
	if !EXIT_SUB

	callevent 0.2 sfaura_loop
	givemp ent_me $neg(PLR_SFAURA_MP_COST)

	if ( PLR_SFAURA_SIZE < PLR_SFAURA_MAXSIZE )
	{
		add PLR_SFAURA_SIZE PLR_SFAURA_GROWTH_RATE
	}
	if ( PLR_SFAURA_SIZE > PLR_SFAURA_MAXSIZE )
	{
		
		setvard PLR_SFAURA_SIZE PLR_SFAURA_MAXSIZE
	}

	setvard PLR_SFAURA_TARGS $get_tsphere(enemy,PLR_SFAURA_SIZE)
	

	if PLR_SFAURA_TARGS isnot none
	calleventloop $get_token_amt(PLR_SFAURA_TARGS) sfaura_affect_targets
}

{ sfaura_affect_targets
	local CUR_TARG $get_token(PLR_SFAURA_TARGS,game.script.iteration)
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local TRACE_START $get(ent_me,origin)
	local TRACE_END $get(CUR_TARG,origin)
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,worldonly)
	if TRACE_LINE equals TRACE_END

	applyeffect CUR_TARG effects/effect_burn 5.0 $get(ent_me,id) PLR_SFAURA_DOT 1 1

	local RESIST_ROLL 1.0
	subtract RESIST_ROLL $randf(0,1.0)
	local FIRE_RESIST $get_takedmg(CUR_TARG,fire)
	if RESIST_ROLL < FIRE_RESIST

	if ( game.time > PLR_NEXT_FSAURA_SOUND )
	{
		setvard PLR_NEXT_FSAURA_SOUND game.time
		add PLR_NEXT_FSAURA_SOUND 1.0
		playsound 0 5 ambience/flameburst1.wav
	}

	if ( $get(CUR_TARG,hp) < 1500 ) local DO_PUSH 1
	if ( $get(CUR_TARG,hp) < PLR_SFAURA_MAXPUSH ) local DO_PUSH 1
	if DO_PUSH
	local TARG_ORG $get(CUR_TARG,origin)
	local MY_ORG $get(ent_me,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	setvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,1000,0))
}

{ sfaura_fx_refresh
	if PLR_SFAURA_ACTIVE

	local SIZE_RATIO PLR_SFAURA_SIZE
	divide SIZE_RATIO PLR_SFAURA_MAXSIZE

	svplaysound 1 10 ambience/rocketrumble1.wav

	
	clientevent new all effects/sfx_sfaura $get(ent_me,index) SIZE_RATIO 20.0
	setvard PLR_SFAURA_CL_IDX game.script.last_sent_id
	callevent 20.0 sfaura_fx_refresh
}

{ ext_sfaura_end 
	svplaysound 1 0 ambience/rocketrumble1.wav
	setvard PLR_SFAURA_ACTIVE 0
	clientevent update all PLR_SFAURA_CL_IDX remove_fx
	if ( PARAM1 isnot remote ) callexternal PLR_SFAURA_ITEM ext_faura_ended
}

{ ext_toss_spear 
	setvard PLR_SPEAR_CHARGE_LEVEL PARAM1
	tossprojectile PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8
	setvard PLR_LAST_PROJECTILE $get(ent_lastprojectile,id)
}

{ ext_holy_aura 

	

	if !PLR_HAURA_ACTIVE

	local L_DUR PARAM1
	callevent L_DUR ext_end_holy_aura
	setvard PLR_HAURA_ACTIVE 1
	setvard PLR_HAURA_RADIUS PARAM2
	setvard PLR_HAURA_POWER PARAM3
	callevent loop_holy_aura
	svplaysound 2 10 ambience/alien_powernode.wav

	local L_AURA_SIZE PLR_HAURA_RADIUS
	multiply L_AURA_SIZE 1.5
	local L_AURA_MDL_OFS 25
	if ( PLR_HAURA_RADIUS > 256 ) add L_AURA_MDL_OFS 1
	clientevent new all effects/sfx_seal_follow $get(ent_me,index) 10.0 L_AURA_MDL_OFS 1 (255,200,0) L_AURA_SIZE
	setvard PLR_HAURA_CL_ID game.script.last_sent_id
	callevent 10.0 holy_aura_refresh
}

{ holy_aura_refresh
	if PLR_HAURA_ACTIVE
	local L_AURA_SIZE PLR_HAURA_RADIUS
	multiply L_AURA_SIZE 1.5
	local L_AURA_MDL_OFS 25
	if ( PLR_HAURA_RADIUS > 256 ) add L_AURA_MDL_OFS 1
	clientevent new all effects/sfx_seal_follow $get(ent_me,index) 10.0 L_AURA_MDL_OFS 1 (255,200,0) L_AURA_SIZE
	setvard PLR_HAURA_CL_ID game.script.last_sent_id
	callevent 10.0 holy_aura_refresh
}

{ loop_holy_aura

	if PLR_HAURA_ACTIVE
	callevent 0.5 loop_holy_aura
	setvard PLR_HAURA_TARGETS $get_tsphere(any,PLR_HAURA_RADIUS)
	
	if PLR_HAURA_TARGETS isnot none
	calleventloop $get_token_amt(PLR_HAURA_TARGETS) affect_targets_holy_aura
}

{ affect_targets_holy_aura
	local CUR_TARG $get_token(PLR_HAURA_TARGETS,game.script.iteration)
	if ( $get(CUR_TARG,relationship,ent_me) equals ally ) local HEAL_TARGET 1
	if ( $get(CUR_TARG,isplayer) ) local HEAL_TARGET 1
	if ( $get(CUR_TARG,scriptvar,'PLAYING_DEAD') )
	{
		if !$get(CUR_TARG,isplayer)
		local HEAL_TARGET 0
	}

	if ( HEAL_TARGET )
	{
		applyeffect CUR_TARG effects/effect_rejuv2 PLR_HAURA_POWER $get(ent_me,skill.spellcasting.divination) $get(ent_me,id)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local NME_UNHOLY $get_takedmg(CUR_TARG,holy)
	if NME_UNHOLY > 0

	if $can_damage(CUR_TARG,ent_me)

	local TARG_HP $get(CUR_TARG,hp)
	local MY_HP_X $get(ent_me,skill.spellcasting.divination)
	multiply MY_HP_X 100
	if ( TARG_HP < 1500 ) local DO_PUSH 1
	if ( TARG_HP < MY_HP_X ) local DO_PUSH 1
	if DO_PUSH
	local TARG_ORG $get(CUR_TARG,origin)
	local MY_ORG $get(ent_me,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	if ( game.time > NEXT_HAURA_SOUND )
	{
		setvard NEXT_HAURA_SOUND game.time
		add NEXT_HAURA_SOUND 0.5
		playsound 0 5 doors/aliendoor3.wav
	}
	local PUSH_STR 300
	multiply PUSH_STR NME_UNHOLY
	setvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,PUSH_STR,0))
}

{ ext_end_holy_aura
	if ( PLR_HAURA_ACTIVE )
	{
		svplaysound 2 0 ambience/alien_powernode.wav
		clientevent update all PLR_HAURA_CL_ID remove_fx
	}
	setvard PLR_HAURA_ACTIVE 0
}

{ ext_setskin 
	setprop ent_me skin PARAM1
}

{ ext_pole_lshield
	if !PLR_PLSHIELD_ACTIVE
	setvard PLR_PLSHIELD_ACTIVE 1
	setvard PLR_PLSHIELD_DOT $get(ent_me,skill.spellcasting.lightning)
	multiply PLR_PLSHIELD_DOT 0.25
	setvard PLR_PLSHIELD_PUSH 500 
	setvard GAME_PVP game.pvp
	clientevent new all items/polearms_ph_spin_cl $get(ent_me,index)
	setvard PLR_PLSHIELD_CL_IDX game.script.last_sent_id
	svplaysound 1 10 magic/bolt_loop.wav
	callevent pole_lshield_loop
}

{ pole_lshield_loop
	if PLR_PLSHIELD_ACTIVE
	callevent 0.5 pole_lshield_loop

	givemp ent_me -1
	if ( $get(ent_me,mp) <= 1 )
	{
		dplayermessage ent_me "Stormpharaoh's Lance: Insufficient mana for Lightning Shield"
		callevent ext_pole_lshield_end mana
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local SCAN_LOC $get(ent_me,origin)
	setvard OWNER_YAW $get(ent_me,angles.yaw)
	vectoradd SCAN_LOC $relpos($vec(0,OWNER_YAW,0),$vec(0,64,0))
	setvard PLR_PLSHIELD_TARGETS $get_tsphere(enemy,64,SCAN_LOC)
	if PLR_PLSHIELD_TARGETS isnot none
	calleventloop $get_token_amt(PLR_PLSHIELD_TARGETS) pole_lshield_affect_targets
}

{ pole_lshield_affect_targets
	local CUR_TARGET $get_token(PLR_PLSHIELD_TARGETS,game.script.iteration)
	if ( !GAME_PVP )
	{
		if $get(CUR_TARGET,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local PUSH_STR PLR_PLSHIELD_PUSH
	multiply PUSH_STR $get_takedmg(CUR_TARGET,lightning)
	addvelocity CUR_TARGET $relpos($vec(0,OWNER_YAW,0),$vec(0,PUSH_STR,10))
	applyeffect CUR_TARGET effects/effect_shock_dmg 5.0 $get(ent_me,id) PLR_PLSHIELD_DOT

	if ( game.time > NEXT_PLSHIELD_SOUND )
	{
		setvard NEXT_PLSHIELD_SOUND game.time
		add NEXT_PLSHIELD_SOUND 0.5
		playsound 2 5 magic/bolt_end.wav
	}
}

{ ext_pole_lshield_end
	if ( PLR_PLSHIELD_ACTIVE )
	{
		svplaysound 1 0 magic/bolt_loop.wav
		clientevent update all PLR_PLSHIELD_CL_IDX end_fx
	}
	setvard PLR_PLSHIELD_ACTIVE 0
}

{ svilla_music


	if ( G_SORCV_FRIENDLY )
	{
		
		playmp3 ent_me 8.0 music/PathAnubis.mp3
	}
	else
	{
		playmp3 ent_me 4.0 music/crimson_field.mp3
	}
}

{ svilla_music2


	if ( G_SORCV_FRIENDLY )
	{
		playmp3 ent_me 8.0 music/haunted_desert.mp3
	}
	else
	{
		playmp3 ent_me 4.0 music/crimson_field.mp3
	}
}

{ ext_stop_music

	playmp3 ent_me 0 stop.mp3
}

{ ext_svilla_hostile
	setvarg G_SORCV_FRIENDLY 0
}


{ ext_weather_set_clear
	setvard PLR_LOCK_CLEAR 1
	callevent ext_weather_change clear
}

{ ext_weather_manual_change
	setvard PLR_LOCK_CLEAR 0
	local NEW_WEATHER PARAM1
	callevent ext_weather_change NEW_WEATHER
}

{ trig_srocv_player_on_shelf
	local ALCH_ID $get_by_name(sorc_alchie)
	callexternal ALCH_ID ext_player_on_shelf
}

{ trig_srocv_player_on_table
	local ALCH_ID $get_by_name(sorc_alchie)
	callexternal ALCH_ID ext_player_on_table
}

{ ext_bravery
	gplayermessage ent_me You will take no penalty for your next death
	setvard PLR_BRAVERY 1
	noxploss ent_me 1
	playsound 0 10 voices/human/male_guard_hail.wav
}

{ ext_dmg_adjust 
	if ( PARAM1 equals fire ) setvard PLR_DMG_ADJUST_FIRE PARAM2
}

{ ext_dmg_add_dot 
	if ( PARAM1 equals fire ) setvard PLR_ADD_FIRE_DOT PARAM2
}


{ set_self_adjusting
	
	setvard NPC_SELF_ADJUST 1
}

{ trig_damage 
	
	local TRIG_NAME PARAM1
	local TRIG_DMG PARAM2
	local TRIG_DMG_TYPE PARAM3
	stradd TRIG_DMG_TYPE _effect
	callexternal GAME_MASTER gm_setname TRIG_NAME
	xdodamage ent_me direct TRIG_DMG 100% GAME_MASTER GAME_MASTER none TRIG_DMG_TYPE
}

{ ext_webbed

	if ( WEBS_ATTACHED == 0 )
	{
		setvard WEBS_ATTACHED 0
		callevent 10.0 ext_webs_decay
	}
	add WEBS_ATTACHED 1
	clientevent update ent_me const.localplayer.scriptID fx_web_set_webs WEBS_ATTACHED
}

{ ext_web_reset
	setvard WEBS_ATTACHED 0
}

{ ext_webs_decay
	subtract WEBS_ATTACHED 1
	if ( WEBS_ATTACHED > 0 )
	{
		playermessage ent_me The webs loosen a bit
		callevent 10.0 ext_webs_decay
	}
	clientevent update ent_me const.localplayer.scriptID fx_web_set_webs WEBS_ATTACHED
}

{ ext_set_cocooned
	setvard COCOON_ATTACHED PARAM1
	if ( COCOON_ATTACHED ) setvard WEBS_ATTACHED 0
}

{ ext_dmgpoint_bonus 
	callevent add_damage_points $int(PARAM1)

	local OUT_MSG "+"
	stradd OUT_MSG $int(PARAM1)
	stradd OUT_MSG " damage points "
	stradd OUT_MSG PARAM2 
	gplayermessage ent_me OUT_MSG
}

{ ext_darkenbloom

	setvard PLR_DARKEN_BLOOM PARAM1
	clientevent update ent_me const.localplayer.scriptID cl_darken_bloom PARAM1
}


{ ext_bloom_cycle
	add PLR_BLOOM_CYCLE	1
	if ( PLR_BLOOM_CYCLE == 1 )
	{
		clientcmd ent_me "ms_bloom_darken -1"
		clientcmd ent_me "ms_bloom_level 4"
		clientevent update ent_me const.localplayer.scriptID cl_darken_bloom PLR_DARKEN_BLOOM
		gplayermessage ent_me Bloom Level: Auto ( PLR_DARKEN_BLOOM )
		consolemsg ent_me bloom cvars: ms_bloom_darken -1 ms_bloom_level 4
	}
	if ( PLR_BLOOM_CYCLE == 2 )
	{
		clientcmd ent_me "ms_bloom_darken 0"
		clientcmd ent_me "ms_bloom_level 4"
		consolemsg ent_me bloom cvars: ms_bloom_darken 0 ms_bloom_level 4
		gplayermessage ent_me Bloom Level: Max (0)
	}
	if ( PLR_BLOOM_CYCLE == 3 )
	{
		clientcmd ent_me "ms_bloom_darken 1"
		clientcmd ent_me "ms_bloom_level 4"
		gplayermessage ent_me Bloom Level: Dim I (4)
		consolemsg ent_me bloom cvars: ms_bloom_darken 1 ms_bloom_level 4
	}
	if ( PLR_BLOOM_CYCLE == 4 )
	{
		clientcmd ent_me "ms_bloom_darken 6"
		clientcmd ent_me "ms_bloom_level 4"
		gplayermessage ent_me Bloom Level: Dim II (6)
		consolemsg ent_me bloom cvars: ms_bloom_darken 6 ms_bloom_level 4
	}
	if ( PLR_BLOOM_CYCLE == 5 )
	{
		clientcmd ent_me "ms_bloom_darken 20"
		clientcmd ent_me "ms_bloom_level 4"
		gplayermessage ent_me Bloom Level: Dim III (20)
		consolemsg ent_me bloom cvars: ms_bloom_darken 20 ms_bloom_level 4
	}
	if ( PLR_BLOOM_CYCLE == 6 )
	{
		clientcmd ent_me "ms_bloom_darken -1"
		clientcmd ent_me "ms_bloom_level 0"
		gplayermessage ent_me Bloom Level: Off
		consolemsg ent_me bloom cvars: ms_bloom_darken -1 ms_bloom_level 0
		setvard PLR_BLOOM_CYCLE 0
	}

}

{ ext_playrandomsound

	if ( PARAM5 equals 'PARAM5' )
	{
		playrandomsound PARAM1 PARAM2 PARAM3 PARAM4
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM6 equals 'PARAM6' )
	{
		playrandomsound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM7 equals 'PARAM7' )
	{
		playrandomsound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM8 equals 'PARAM8' )
	{
		playrandomsound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PARAM9 equals 'PARAM9' )
	{
		playrandomsound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	playrandomsound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8 PARAM9
}

{ ext_set_goblin_latched 
	setvard I_R_GOBLIN_LATCHED PARAM1
}

{ ext_orcfor_boss_musak 
	if ( PARAM1 == 1 )
	{
		setvard PLR_ORCFOR_MUSIC 1
		playmp3 ent_me 0.85 music/seruboss1.mp3
	}

	if ( PARAM1 == 2 )
	{
		if PLR_ORCFOR_MUSIC != 2
		setvard PLR_ORCFOR_MUSIC 2
		playmp3 ent_me 1.2 music/seruboss2.mp3
	}

	if ( PARAM1 == 3 )
	{
		local MY_ORG $get(ent_me,origin)
		local BOSS_ORG PARAM2
		if $dist(MY_ORG,BOSS_ORG) < 768
		playmp3 ent_me 0 stop.mp3
	}
}

{ ext_fire_xbow 
	tossprojectile PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8 PARAM9 
	setvard PLR_LAST_PROJECTILE $get(ent_lastprojectile,id)
}

{ ext_gabe_musak 
	if ( PARAM1 equals stop )
	{
		playmp3 ent_me 0 stop.mp3
		playsound 0 10 gabe_loop.wav
	}
	else
	{
		playmp3 ent_me 0.8 music/gabe1.mp3
	}
}

{ ext_bomb_test
	tossprojectile view 500 400 0 proj_fire_bomb (0,64,32)
}

{ ext_changelevel_prep
	setvard PLR_RECON_WAIT 20
	callevent ext_weather_force_change clear
	clientevent update ent_me const.localplayer.scriptID cl_get_rc_delay
	
}

{ ext_get_rc_time 
	setvard PLR_RECON_WAIT PARAM1
	if PLR_RECON_WAIT < 2
	setvard PLR_RECON_WAIT 2
}

{ ext_set_bou
	if ( !PLR_BOU_SPLOIT ) callevent 10.0 ext_set_bou_end
	setvard PLR_BOU_SPLOIT 1
	takedmg all 3.0
}

{ ext_set_bou_end
	setvard PLR_BOU_SPLOIT 0
	takedmg all 1.0
}


{ ext_volcano_init 
	setvard PLR_VOLCANO_DOT PARAM1
	setvard PLR_VOLCANO_DMG PARAM1
	setvard PLR_VOLC_PVP game.pvp
	if ( !PARAM2 )
	{
		setvard PLR_VOLCANO_DMG $get(ent_me,skill.spellcasting.fire)
		setvard PLR_VOLCANO_DOT PLR_VOLCANO_DMG
		multiply PLR_VOLCANO_DOT 1.5
	}
}

{ ext_volcano_hit

	
	
	
	local L_DMG PLR_VOLCANO_DMG
	
	

	if ( game.time > PLR_NEXT_VOLCANO_ROCK )
	{
		setvard PLR_NEXT_VOLCANO_ROCK game.time
		add PLR_NEXT_VOLCANO_ROCK 0.5
	}
	else
	{
		local L_DMG 0 
	}

	xdodamage PARAM1 64 L_DMG 0.01 ent_me ent_me spellcasting.fire fire dmgevent:volcano
}

{ ext_volcano_single
	local L_DMG PLR_VOLCANO_DMG
	multiply L_DMG 3.0
	local L_TARG_HIT $get_by_idx(PARAM1,id)
	xdodamage L_TARG_HIT direct L_DMG 0.01 ent_me ent_me spellcasting.fire fire dmgevent:volcano
}


{ volcano_dodamage
	
	
	if $get(PARAM2,relationship,ent_me) equals enemy
	if ( !PLR_VOLC_PVP )
	{
		if $get(PARAM2,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	applyeffect PARAM2 effects/effect_burn 5 $get(ent_me,id) PLR_VOLCANO_DOT
	
}

{ ext_bank_lock
	applyeffect ent_me effects/effect_templock $get(ent_me,id)
	callevent 1.0 ext_bank_lock_release
}

{ ext_bank_lock_release
	callevent ext_remove_lock
}

{ ext_keldorn_troll
	if !$item_exists(ent_me,scroll2_trollcano)
	callexternal GAME_MASTER gm_keldorn_troll $get(ent_me,id)
}


{ ext_alance_init
	setvard GAME_PVP game.pvp
	setvard AFL_BURST_DOT $get(ent_me,skill.spellcasting.affliction)
	multiply AFL_BURST_DOT 0.75
}

{ alance_dodamage
	

	if PARAM1
	if ( !GAME_PVP )
	{
		if $get(PARAM2,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	applyeffect PARAM2 effects/effect_poison 5.0 $get(ent_me,id) AFL_BURST_DOT

	local TARG_HP $get(PARAM2,hp)
	local MY_HP_X $get(ent_me,maxhp)
	multiply MY_HP_X 4
	if ( TARG_HP < 1500 ) local DO_PUSH 1
	if ( TARG_HP < MY_HP_X ) local DO_PUSH 1
	if DO_PUSH
	local TARG_ORG $get(PARAM2,origin)
	local MY_ORG $get(ent_me,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	setvelocity PARAM2 $relvel($vec(0,NEW_YAW,0),$vec(0,1000,0))
}

{ ext_drainstamina
	drainstamina ent_me PARAM1
}

{ ext_clbeam
	local BEAM_TARG $get_tsphere(enemy,1024)
	local BEAM_TARG $get_token(BEAM_TARG,0)
	
	clientevent update ent_me const.localplayer.scriptID set_test_beam $get(ent_me,index) $get(BEAM_TARG,index)
}

{ ext_repel 

	local AOE_DMG PARAM1
	local AOE_RAD PARAM2
	local AOE_FAL PARAM3
	local DMG_TYPE PARAM4
	local DMG_SKILL PARAM5

	setvard PLR_REPEL_PUSH_STR PARAM6
	setvard PLR_REPEL_MAX_HP PARAM7

	xdodamage $get(ent_me,origin) AOE_RAD AOE_DMG AOE_FAL ent_me ent_me DMG_SKILL DMG_TYPE dmgevent:repel

	
}

{ repel_dodamage
	if PARAM1
	if $get(PARAM2,relationship,ent_me) equals enemy
	if ( !GAME_PVP )
	{
		if $get(PARAM2,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( PLR_REPEL_MAX_HP > 0 )
	{
		if $get(PARAM2,hp) > PLR_REPEL_MAX_HP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	local TARG_ORG $get(PARAM2,origin)
	local MY_ORG $get(ent_me,origin)
	local TARG_ANG $angles(MY_ORG,TARG_ORG)
	local NEW_YAW TARG_ANG
	setvelocity PARAM2 $relvel($vec(0,NEW_YAW,0),$vec(0,PLR_REPEL_PUSH_STR,110))
}

{ ext_lcod 
	setvard PLR_LCOD_POS PARAM1
	setvard PLR_LCOD_DMG PARAM2
	setvard PLR_LCOD_DUR PARAM3

	

	local CL_POS PLR_LCOD_POS
	vectorset CL_POS z $get_ground_height(PLR_LCOD_POS)
	
	
	
	svsound.play3d magic/pulsemachine_noloop.wav 8 PLR_LCOD_POS 0.8 5 100
	setvard PLR_NEXT_LCOD_SOUND game.time
	add PLR_NEXT_LCOD_SOUND 1.88

	clientevent new all effects/sfx_cod CL_POS PLR_LCOD_DUR 3 98 
	setvard PLR_LCOD_ACTIVE 1
	callevent lcod_loop
	callevent PLR_LCOD_DUR lcod_end
}

{ lcod_loop
	if PLR_LCOD_ACTIVE
	callevent 1.0 lcod_loop
	
	
	xdodamage PLR_LCOD_POS PLR_LCOD_AOE PLR_LCOD_DMG 0 ent_me ent_me spellcasting.affliction dark_effect

	if game.time > PLR_NEXT_LCOD_SOUND
	svsound.play3d magic/pulsemachine_noloop.wav 8 PLR_LCOD_POS 0.8 5 100
	setvard PLR_NEXT_LCOD_SOUND game.time
	add PLR_NEXT_LCOD_SOUND 1.88
}

{ lcod_end
	
	
	setvard PLR_LCOD_ACTIVE 0
}

{ ext_gcod 
	setvard PLR_GCOD_POS PARAM1
	setvard PLR_GCOD_DMG PARAM2
	setvard PLR_GCOD_DUR PARAM3

	local CL_POS PLR_GCOD_POS
	vectorset CL_POS z $get_ground_height(PLR_GCOD_POS)
	
	
	
	svsound.play3d magic/pulsemachine_noloop.wav 10 PLR_GCOD_POS 0.8 3 100
	setvard PLR_NEXT_GCOD_SOUND game.time
	add PLR_NEXT_GCOD_SOUND 1.88

	clientevent new all effects/sfx_cod CL_POS PLR_GCOD_DUR 5 196 
	setvard PLR_GCOD_ACTIVE 1
	callevent gcod_loop
	callevent PLR_GCOD_DUR gcod_end
}

{ gcod_loop
	if PLR_GCOD_ACTIVE
	callevent 0.5 gcod_loop
	xdodamage PLR_GCOD_POS PLR_GCOD_AOE PLR_GCOD_DMG 0 ent_me ent_me spellcasting.affliction dark_effect

	if game.time > PLR_NEXT_GCOD_SOUND
	svsound.play3d magic/pulsemachine_noloop.wav 10 PLR_GCOD_POS 0.8 3 100
	setvard PLR_NEXT_GCOD_SOUND game.time
	add PLR_NEXT_GCOD_SOUND 1.88

}

{ gcod_end
	
	
	setvard PLR_GCOD_ACTIVE 0
}

{ ext_wraith_active 
	setvard PLR_WRAITH_ACTIVE PARAM1
}

{ ext_fissure 
	local MY_YAW $get(ent_me,viewangles) 
	local MY_YAW $vec.yaw(MY_YAW)

	setvard FISSURE_YAW MY_YAW
	setvard FISSURE_START $get(ent_me,origin)
	
	setvard FISSURE_END FISSURE_START
	vectoradd FISSURE_END $relpos($vec(0,FISSURE_YAW,0),$vec(0,768,0))
	vectorset FISSURE_END z $get_ground_height(FISSURE_END)
	vectoradd FISSURE_END z 32

	local TRACE_LINE $get_traceline(FISSURE_START,FISSURE_END,worldonly)
	

	if ( G_DEVELOPER_MODE )
	{
		effect beam point lgtning.spr 10 FISSURE_START FISSURE_END (255,0,255) 255 0 10
	}


	setvard FISSURE_LENGTH $dist(FISSURE_START,FISSURE_END)

	setvard FISSURE_COUNT 0
	setvard FISSURE_ACTIVE 1

	setvard FISSURE_DMG $get(ent_me,skill.spellcasting.fire)

	
	local NO_ROCKS 0
	if ( G_FISSURES equals 'G_FISSURES' ) setvarg G_FISSURES 0
	add G_FISSURES 1
	if ( G_FISSURES > 1 ) local NO_ROCKS 1
	clientevent new all effects/sfx_fissure FISSURE_START FISSURE_YAW FISSURE_END FISSURE_LENGTH NO_ROCKS

	setvard FISSURE_COUNT 0
	setvard FISSURE_MAX_COUNT $math(divide,FISSURE_LENGTH,78)
	if ( FISSURE_MAX_COUNT < 1 ) setvard FISSURE_MAX_COUNT 1
	setvard FISSURE_ORG FISSURE_START
	setvard FISSURE_DIR $dir(FISSURE_ORG,FISSURE_END)
	callevent fissure_loop
}

{ fissure_loop
	add FISSURE_COUNT 1
	local L_FISSURE_CHECK_POS FISSURE_ORG
	local L_FISSURE_MOVEAMT FISSURE_DIR
	vectormultiply L_FISSURE_MOVEAMT $math(multiply,78,FISSURE_COUNT)
	vectoradd L_FISSURE_CHECK_POS L_FISSURE_MOVEAMT
	vectoradd L_FISSURE_CHECK_POS z 128
	vectorset L_FISSURE_CHECK_POS z $get_ground_height(L_FISSURE_CHECK_POS)
	xdodamage L_FISSURE_CHECK_POS 78 FISSURE_DMG 100% ent_me ent_me spellcasting.fire fire_effect dmgevent:fissure
	if ( FISSURE_COUNT < FISSURE_MAX_COUNT )
	{
		callevent 0.20 fissure_loop
	}
	else
	{
		subtract G_FISSURES 1
	}
	if ( G_DEVELOPER_MODE )
	{
		local L_VEC_UP L_FISSURE_CHECK_POS
		vectoradd L_VEC_UP z 64
		effect beam point lgtning.spr 10 L_FISSURE_CHECK_POS L_VEC_UP (255,0,255) 255 0 5
	}
}


	

{ fissure_dodamage 
	if PARAM1
	if ( !GAME_PVP )
	{
		if $get(PARAM2,isplayer)
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if $get(ent_me,relationship,PARAM2) equals enemy
	applyeffect PARAM2 effects/effect_burn 5.0 $get(ent_me,id) FISSURE_DMG

	local TARG_HP $get(PARAM2,hp)
	local MAX_HP_PUSH $get(ent_me,maxhp)
	multiply MAX_HP_PUSH 3
	if ( MAX_HP_PUSH < 2000 ) local MAX_HP_PUSH 2000
	if TARG_HP < MAX_HP_PUSH

	
	local RND_RL $rand(1,2)
	if ( RND_RL == 1 ) local RND_RL 400
	if ( RND_RL == 2 ) local RND_RL -400
	setvelocity PARAM2 $relvel($vec(0,FISSURE_YAW,0),$vec(RND_RL,0,110))	
}

{ ext_summon_unique 
	token.add PLR_UNIQUE_SUMMONS PARAM1
}

{ ext_unsummon_unique 
	local TOKEN_IDX $get_find_token(PLR_UNIQUE_SUMMONS,PARAM1)
	token.del PLR_UNIQUE_SUMMONS TOKEN_IDX
}

{ ext_reset_nomove 
	
	setvard PLR_EFFECT_REMOVE_NOMOVE PARAM1
}

{ ext_epilepsy_time_begin
	hud.addimgicon ent_me bepilepsy2 bepilepsy2 15 20 75 60 0.25 
	setvard PLR_EPILEPSY_ACTIVE 1
	setvard PLR_EPILEPSY_COUNT 0
	setvard PLR_EPILEPSY_DELAY 0.5
	playmp3 ent_me 5.0 music/b-b-b-beetlejuice.mp3
	callevent 0.25 epilepsy_loop
	hud.addimgicon ent_me bepilepsy1 bepilepsy1 15 20 75 60 4.0
}

{ epilepsy_loop
	if PLR_EPILEPSY_ACTIVE

	callevent PLR_EPILEPSY_DELAY epilepsy_loop

	local L_DUR PLR_EPILEPSY_DELAY
	multiply L_DUR 0.5
	hud.addimgicon ent_me bepilepsy2 bepilepsy2 15 20 75 60 L_DUR


	add PLR_EPILEPSY_COUNT 0.5
	if ( PLR_EPILEPSY_COUNT > 4 )
	{
		hud.addimgicon ent_me bepilepsy1 bepilepsy1 15 20 75 60 1.0
		setvard PLR_EPILEPSY_DELAY 0.05
	}
	if ( PLR_EPILEPSY_COUNT > 25 ) setvard PLR_EPILEPSY_ACTIVE 0
}

{ ext_epilepsy_time_end
	playmp3 ent_me 5.0 UndeadX1Fortress.mp3
}

{ ext_summon_register_pet 
	if ( PLR_ACTIVE_PETS equals 'PLR_ACTIVE_PETS' ) setvard PLR_ACTIVE_PETS ''
	if ( PLR_ACTIVE_PET_TYPES equals 'PLR_ACTIVE_PET_TYPES' ) setvard PLR_ACTIVE_PET_TYPES ''
	token.add PLR_ACTIVE_PETS $get(PARAM1,id)
	token.add PLR_ACTIVE_PET_TYPES $get(PARAM1,scriptvar,'COMPANION_TYPE')
	
	if ( PLR_N_ACTIVE_PETS equals 'PLR_N_ACTIVE_PETS' )
	{
		setvard PLR_N_ACTIVE_PETS 1 
	}
	else
	{
		add PLR_N_ACTIVE_PETS 1
	}
}

{ ext_summon_pets_new 
	setvard PLR_SUMMON_MENU_DISABLE game.time
	add PLR_SUMMON_MENU_DISABLE 5.0

	if ( PLR_ACTIVE_PETS equals 'PLR_ACTIVE_PETS' ) setvard PLR_ACTIVE_PETS ''
	if ( PLR_ACTIVE_PET_TYPES equals 'PLR_ACTIVE_PET_TYPES' ) setvard PLR_ACTIVE_PET_TYPES ''
	local SUMMON_SCRIPT monsters/companion/
	stradd SUMMON_SCRIPT PARAM2
	local MY_YAW $get(ent_me,viewangles)
	local MY_YAW $vec.yaw(MY_YAW)
	local SUMMON_POS $get(ent_me,origin)
	vectoradd SUMMON_POS $relpos($vec(0,MY_YAW,0),$vec(0,128,0))
	vectorset SUMMON_POS z $get_ground_height(SUMMON_POS)
	createnpc SUMMON_SCRIPT SUMMON_POS $get(ent_me,id)
	

	local reg.npcmove.endpos SUMMON_POS
	vectoradd reg.npcmove.endpos z 16
	local reg.npcmove.testonly 0
	npcmove ent_lastcreated none

	if( game.ret.npcmove.dist <= 0 )
	{
		setvard PLR_SUMMON_MENU_DISABLE game.time 
		dplayermessage ent_me "You cannot summon pet here"
		deleteent ent_lastcreated
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	token.add PLR_ACTIVE_PETS $get(ent_lastcreated,id)
	token.add PLR_ACTIVE_PET_TYPES $get(ent_lastcreated,scriptvar,'COMPANION_TYPE')
	if ( PLR_N_ACTIVE_PETS equals 'PLR_N_ACTIVE_PETS' )
	{
		setvard PLR_N_ACTIVE_PETS 1 
	}
	else
	{
		add PLR_N_ACTIVE_PETS 1
	}
}

{ ext_unsummon_pets_new 
	setvard PLR_SUMMON_MENU_DISABLE game.time
	add PLR_SUMMON_MENU_DISABLE 5.0

	local PET_ID PARAM2
	local PET_IDX $get_find_token(PLR_ACTIVE_PETS,PET_ID)
	token.del PLR_ACTIVE_PETS PET_IDX

	local PET_TYPE $get(PET_ID,scriptvar,'COMPANION_TYPE')
	local PET_TYPE_IDX $get_find_token(PLR_ACTIVE_PET_TYPES,PET_TYPE)
	
	token.del PLR_ACTIVE_PET_TYPES PET_TYPE_IDX

	subtract PLR_N_ACTIVE_PETS 1
	if !PARAM3
	callexternal PET_ID companion_unsummon
}

{ ext_ice_staff_on
	svplaysound 2 10 magic/freezeray_loop.wav
}

{ ext_ice_staff_off
	svplaysound 2 0 magic/freezeray_loop.wav
}

{ ext_ravenmace_sound
	 svplaysound 2 10 amb/wind.wav
}

{ ext_register_fists

	setvard PLR_FISTS_ID PARAM1
}


{ set_push_resist
	setvard MSC_PUSH_RESIST PARAM1
}

{ ext_lesser_leadfoot
	setvard MSC_PUSH_RESIST PARAM1
	setvard IMMUNE_STUN PARAM2
	setvard PLR_IMMUNE_STUN PARAM2
	setvard PLR_LESSER_LEADFOOT 1
	
	setvard PLR_LESSER_LEADFOOT_STUN PARAM2 
	if ( !$get(ent_me,nopush) ) gplayermessage ent_owner Your stun resistance is now PLR_IMMUNE_STUN
}

{ ext_toggle_halo
	if PLR_HAS_HALO
	if ( PLR_HALO_ON )
	{
		setvard PLR_HALO_ON 0
		clientevent update all const.localplayer.scriptID cl_set_halo 0 $get(ent_me,index)
	}
	else
	{
		setvard PLR_HALO_ON 1
		clientevent update all const.localplayer.scriptID cl_set_halo 1 $get(ent_me,index)
	}
}

{ ext_jump_beam 
	
	
	

	
	

	local BEAM_ORG PARAM1
	local IS_ENT PARAM2
	local ENT_IDX $int(PARAM3)
	local L_CL_IDX PARAM4

	local SCAN_ORG BEAM_ORG
	vectorset SCAN_ORG z $get_ground_height(SCAN_ORG)
	vectoradd SCAN_ORG 32

	local BEAM_TARGS $get_tsphere(enemy,256,SCAN_ORG)
	if ( BEAM_TARGS isnot none ) local BEAM_TARGS $sort_entlist(BEAM_TARGS,range,SCAN_ORG)




	local DO_RANDOM_JUMP 0

	if ( BEAM_TARGS isnot none )
	{
		local CUR_TARG $get_token(BEAM_TARGS,0)
		if ( IS_ENT )
		{
			local N_TARGS $get_token_amt(BEAM_TARGS)
			if ( N_TARGS > 1 )
			{
				local CUR_TARG $get_token(BEAM_TARGS,1)
			}
			if $get(CUR_TARG,index) equals ENT_IDX
			local DO_RANDOM_JUMP 1 
		}		
	}
	else
	{
		local DO_RANDOM_JUMP 1 
	}

	if ( !DO_RANDOM_JUMP )
	{
		
		
		
		array.add ARRAY_JUMP_BEAM_TARGS CUR_TARG
		array.add ARRAY_JUMP_BEAM_IDS L_CL_IDX

		clientevent update all L_CL_IDX update_targ $get(CUR_TARG,index)
	}
	else
	{
		clientevent update all L_CL_IDX update_targ -1
	}
}


{ ext_jump_beam_burn 
	

	
	local L_BEAM_IDX $get_arrayfind(ARRAY_JUMP_BEAM_IDS,PARAM1)

	if ( L_BEAM_IDX > -1 )
	{
		local L_TARG $get_array(ARRAY_JUMP_BEAM_TARGS,L_BEAM_IDX)
		array.del ARRAY_JUMP_BEAM_TARGS L_BEAM_IDX
		array.del ARRAY_JUMP_BEAM_IDS L_BEAM_IDX
	}
	else
	{
		local EXIT_SUB 1
	
	}
	if !EXIT_SUB
	

	local L_DMG $get(ent_me,skill.spellcasting.lightning)
	local L_DMG_DIRECT L_DMG
	multiply L_DMG_DIRECT 3
	xdodamage L_TARG direct L_DMG_DIRECT 100% ent_me ent_me spellcasting.lightning lightning_effect
	local L_DOT L_DMG
	multiply L_DOT 0.5
	applyeffect L_TARG effects/effect_shock_dmg 5.0 $get(ent_me,id) L_DOT
}

{ ext_delay_playsound 
	setvard PLR_DEL_PLAYSOUND_CHAN PARAM2
	setvard PLR_DEL_PLAYSOUND_VOL PARAM3
	setvard PLR_DEL_PLAYSOUND_WAV PARAM4
	callevent PARAM1 ext_delay_playsound2
}

{ ext_delay_playsound2
	playsound PLR_DEL_PLAYSOUND_CHAN PLR_DEL_PLAYSOUND_VOL PLR_DEL_PLAYSOUND_WAV
}

{ ext_tosscre 
	setvard PLR_CRE_HAND PARAM1
	setvard PLR_CRE_TYPE PARAM2
	if ( PLR_CRE_HAND == 1 )
	{
		local L_OFS (20,20,18)
	}
	else
	{
		local L_OFS (-20,20,18)
	}
	tossprojectile view 200 0 0 proj_crescent L_OFS
	setvard PLR_LAST_PROJECTILE $get(ent_lastprojectile,id)
}

{ ext_set_glow
	setvard PLR_HAS_GLOW PARAM1
	setvard IR_GLOWING PARAM1 
	if !PLR_HAS_GLOW
	callevent 0.1 restore_item_lights 
}

{ restore_item_lights
	callexternal all ext_restore_item_lights
}

{ ext_teleportfx1
	callevent 0.1 ext_teleportfx1_delay
}

{ ext_teleportfx1_delay
	local MY_FEET $get(ent_me,origin)
	vectorset MY_FEET z $get_ground_height(MY_FEET)
	clientevent update all const.localplayer.scriptID cl_tele1_fx MY_FEET
}

{ ext_teleportfx2
	playsound 0 10 debris/beamstart8.wav
}

{ extsoc_show_scores 



	local L_POINTS_RED PARAM1
	local L_POINTS_BLUE PARAM2

	local L_SPR_DUR 3.0
	if ( L_POINTS_RED == 5 ) local L_GAME_WON 1
	if ( L_POINTS_BLUE == 5 ) local L_GAME_WON 2
	if ( L_GAME_WON > 0 ) local L_SPR_DUR 10.0

	local L_SCORE_SPRITE L_POINTS_RED
	local L_SCORE_SPRITE $int(L_SCORE_SPRITE)
	stradd L_SCORE_SPRITE _red
	hud.addimgicon ent_me L_SCORE_SPRITE sc1 20 10 20 30 L_SPR_DUR

	local L_SCORE_SPRITE L_POINTS_BLUE
	local L_SCORE_SPRITE $int(L_SCORE_SPRITE)
	stradd L_SCORE_SPRITE _blue
	hud.addimgicon ent_me L_SCORE_SPRITE sc2 60 10 20 30 L_SPR_DUR


	hud.addimgicon ent_me red sc3 20 0 20 10 L_SPR_DUR
	hud.addimgicon ent_me vs sc4 40 0 20 10 L_SPR_DUR
	hud.addimgicon ent_me blue sc5 60 0 20 10 L_SPR_DUR

	if ( L_GAME_WON > 0 )
	{
		if ( L_GAME_WON == 1 )
		{
			hud.addimgicon ent_me red_wins sc6 25 40 50 25 1.0
			setvard PLR_SOCCER_TEAMSPR red_wins
			callevent 2.0 extsoc_flash_win
			callevent 4.0 extsoc_flash_win
			callevent 6.0 extsoc_flash_win
			callevent 8.0 extsoc_flash_win
		}
		else
		{
			hud.addimgicon ent_me blue_wins sc6 25 40 50 25 1.0
			setvard PLR_SOCCER_TEAMSPR blue_wins
			callevent 2.0 extsoc_flash_win
			callevent 4.0 extsoc_flash_win
			callevent 6.0 extsoc_flash_win
			callevent 8.0 extsoc_flash_win
		}
	}
}

{ extsoc_flash_win
	hud.addimgicon ent_me PLR_SOCCER_TEAMSPR sc6 25 40 50 25 1.5
}

{ ext_hud_icon 
	hud.addimgicon ent_me PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7
}


{ ext_shender_telfdoor

	callevent ext_darkenbloom 2
	if !PLR_DID_SHENDER_EVENT
	local WIN_ELF $get_by_name(telf_win)
	if ( $get(WIN_ELF,isalive) )
	{
		if $get(GAME_MASTER,scriptvar,'GM_BUNNY_KILLED')
		setvard PLR_DID_SHENDER_EVENT 1
		callexternal WIN_ELF ext_bunny_comment
	}
}

{ ext_music_combat 
	setvard PLR_COMBAT_MUSIC_ACTIVE 1
	
	local L_CALLER $get_by_idx(PARAM2,id)

	local FLAG_NAME cmusak
	stradd FLAG_NAME $int(PARAM2)

	
	if ( PARAM4 equals none )
	{
	
	}

	if ( PARAM1 equals add )
	{
		if ( !$get_scriptflag(ent_me,FLAG_NAME,name_exists) )
		{
			local L_MUSIC_LENGTH PARAM3
			local L_MUSIC_FILE PARAM4
			if ( PARAM3 startswith 'PARAM' )
			{
				
				
				if ( PLR_COMBAT_MUSIC_LENGTH == 0 )
				{
				
					setvard PLR_COMBAT_MUSIC_LENGTH G_COMBAT_MUSIC_LENGTH
					setvard PLR_COMBAT_MUSIC G_COMBAT_MUSIC
				}

				local L_MUSIC_LENGTH PLR_COMBAT_MUSIC_LENGTH
				local L_MUSIC_FILE PLR_COMBAT_MUSIC
			}
			local L_MUSIC_VALUE L_MUSIC_LENGTH
			stradd L_MUSIC_VALUE ";"
			stradd L_MUSIC_VALUE L_MUSIC_FILE
		
			scriptflags ent_me add FLAG_NAME cmusak L_MUSIC_VALUE
		}
		else
		{
		
		}
	}
	else
	{
		local L_VALUE $get_scriptflag(ent_me,FLAG_NAME,name_value)
	
		scriptflags ent_me edit FLAG_NAME cmusak L_VALUE 8.0
	}
}

{ ext_fade_music
	setvard PLR_FADING_MUSIC 0
	if ( $get_scriptflag(ent_me,cmusak,type_exists) ) dbg ext_fade_music CBM abort fade to idle, re-entered combat
	if !$get_scriptflag(ent_me,cmusak,type_exists) 
	
	if ( PLR_IDLE_MUSIC isnot PLR_CURRENT_MUSIC )
	{
	
		playmp3 ent_me 0 stop.mp3
	}
	if PLR_IDLE_MUSIC isnot none
	callevent 3.0 ext_music_idle
}

{ ext_music_idle 

	if ( $get_scriptflag(ent_me,cmusak,type_exists) )
	{
	
	}
	if !$get_scriptflag(ent_me,cmusak,type_exists) 
	if PLR_IDLE_MUSIC !equals none 
	if ( PLR_IDLE_MUSIC_LENGTH != 0 )
	{
		if ( PLR_IDLE_MUSIC isnot PLR_CURRENT_MUSIC )
		{
		
			playmp3 ent_me PLR_IDLE_MUSIC_LENGTH PLR_IDLE_MUSIC
		}
		else
		{
		
		}
	}
	else
	{
	
		playmp3 ent_me 0 stop.mp3
	}
}

{ ext_conflict
	conflictcheck
}

{ ext_showflags
	local L_TEST $get_scriptflag(ent_me,listall)
}

{ ext_dumpvars
	conflictcheck dumpvars
}

{ set_music 
	setvard PLR_IDLE_MUSIC PARAM1
	setvard PLR_IDLE_MUSIC_LENGTH PARAM2
	setvard PLR_COMBAT_MUSIC PARAM3
	setvard PLR_COMBAT_MUSIC_LENGTH PARAM4


	if ( PARAM5 == 1 )
	{
		if ( $get_scriptflag(ent_me,type_exists,cmusak) )
		{
			callevent set_end_combat_music
		}
		else
		{
			if ( PLR_IDLE_MUSIC !equals none )
			{
				if PLR_CURRENT_MUSIC isnot PLR_IDLE_MUSIC
				callevent ext_music_idle
			}
		}
	}

	if ( PARAM5 == 2 ) playmp3 ent_me PLR_COMBAT_MUSIC_LENGTH PLR_COMBAT_MUSIC
}

{ set_combat_music 
	
	setvard PLR_COMBAT_MUSIC PARAM1
	setvard PLR_COMBAT_MUSIC_LENGTH PARAM2

	if PARAM3
	if PLR_CURRENT_MUSIC isnot PLR_COMBAT_MUSIC
	callevent ext_play_music $get(ent_me,id) PLR_COMBAT_MUSIC_LENGTH PLR_COMBAT_MUSIC
}

{ set_cbm 

	local L_PASS1 PARAM1
	local L_PASS2 PARAM2
	local L_PASS3 PARAM3
	callevent set_combat_music L_PASS1 L_PASS2 L_PASS3
}

{ set_idle_music 
	setvard PLR_IDLE_MUSIC PARAM1
	setvard PLR_IDLE_MUSIC_LENGTH PARAM2

	if PARAM3 == 1
	if ( $get_scriptflag(ent_me,type_exists,cmusak) )
	{
		callevent set_end_combat_music
	}
	else
	{
		if ( PLR_IDLE_MUSIC !equals none )
		{
			if PLR_CURRENT_MUSIC isnot PLR_IDLE_MUSIC
			callevent ext_music_idle
		}
	}
}

{ set_end_combat_music

	scriptflags ent_me cleartype cmusak
}

{ ext_svplaysound_kiss

	svplaysound PARAM1 PARAM2 PARAM3 PARAM4 PARAM5
}

{ ext_clear_valid_gauntlets 
	quest set ent_me mv 0
	quest set ent_me m 0
}

{ ext_reset_lodagond_chests
	quest set ent_me lc 0
}

{ ext_dump_quests
	quest dump ent_me
}

{ ext_dump_bank

	calleventloop 9 ext_dump_bank_loop

}

{ ext_dump_bank_loop
	local CUR_BANK b
	stradd CUR_BANK $int(game.script.iteration)
	setvard PLR_TEMP $get_quest_data(ent_me,CUR_BANK)
	setvard PLR_TEMP2 CUR_BANK

	calleventloop $get_token_amt(PLR_TEMP) ext_dump_bank_loop2
}

{ ext_dump_bank_loop2

}



{ ext_btest
	local SPAWN_POINT $get(ent_me,origin)
	local MY_ANGLES $get(ent_me,angles)
	local MY_YAW $vec.yaw(MY_ANGLES)
	vectoradd SPAWN_POINT $relpos($vec(0,MY_YAW,0),$vec(0,128,0))

	clientevent new all monsters/bat_large_corpse_cl SPAWN_POINT 2
}

{ ext_chill_stack_add 

	local STACK_SIZE PARAM1
	multiply STACK_SIZE $get_takedmg(ent_me,cold)
	add EXT_CHILL_STACKS STACK_SIZE
}

{ ext_haste_cooldown
	setvard PLR_NEXT_HASTE game.time
	add PLR_NEXT_HASTE 20.0
}

{ ext_register_corrode
	setvard PLR_CORRODE_DURATION PARAM1
}

{ ext_clevent_me
	clientevent update ent_me const.localplayer.scriptID PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8
}

{ ext_clevent_all
	clientevent update all const.localplayer.scriptID PARAM1 PARAM2 PARAM3 PARAM4 PARAM5 PARAM6 PARAM7 PARAM8
}

{ ext_got_quest_item 
	if ( PARAM1 !startswith track )
	{
		if ( $get_array_amt(ARRAY_QUEST_ITEMS) == -1 )
		{
			array.create ARRAY_QUEST_ITEMS
		
			
		}

		array.add ARRAY_QUEST_ITEMS PARAM1

		if ( $lcase(game.map.name) startswith rmine )
		{
			if !PLR_FOUND_FIRST_STICK
			setvard PLR_FOUND_FIRST_STICK 1
			infomsg ent_me " " "Hrmmm... The fuse is broken... But maybe we can use this, somewhere..."
		}
	}


}

{ ext_quest_items_return 
	setvard PLR_RETURN 0
	local L_FIND_ITEM $get_arrayfind(ARRAY_QUEST_ITEMS,PARAM1)

	if L_FIND_ITEM !equals '[ERROR_NO_ARRAY]'
	if L_FIND_ITEM > -1
	setvard PLR_TEMP PARAM1
	calleventloop $get_array_amt(ARRAY_QUEST_ITEMS) ext_quest_items_return_scan
}

{ ext_quest_items_return_scan
	local CUR_IDX game.script.iteration

	if $get_array(ARRAY_QUEST_ITEMS,CUR_IDX) equals PLR_TEMP
	add PLR_RETURN 1

}

{ ext_quest_items_strip_type 
	local L_FIND_ITEM $get_arrayfind(ARRAY_QUEST_ITEMS,PARAM1)

	if L_FIND_ITEM !equals '[ERROR_NO_ARRAY]'
	if L_FIND_ITEM > -1
	setvard PLR_TEMP PARAM1
	calleventloop $get_array_amt(ARRAY_QUEST_ITEMS) ext_quest_items_strip_type_scan
}

{ ext_quest_items_strip_type_scan
	local CUR_IDX game.script.iteration

	if $get_array(ARRAY_QUEST_ITEMS,CUR_IDX) equals PLR_TEMP
	array.set ARRAY_QUEST_ITEMS CUR_IDX "none"

}

{ ext_dump_quest_items
	calleventloop $get_array_amt(ARRAY_QUEST_ITEMS) ext_dump_quest_items_loop
}

{ ext_dump_quest_items_loop
	consolemsg ent_me $get_array(ARRAY_QUEST_ITEMS,game.script.iteration)
}

{ ext_check_quest_item 
	local L_ITEM_NAME PARAM1
	local L_FIND_ITEM $get_arrayfind(ARRAY_QUEST_ITEMS,PARAM1)
	local L_CALLER_ID PARAM2



	if ( L_FIND_ITEM equals '[ERROR_NO_ARRAY]' )
	{
		
		
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( L_FIND_ITEM > -1 )
	{
		array.set ARRAY_QUEST_ITEMS L_FIND_ITEM 0
		callexternal L_CALLER_ID ext_receive_quest_item L_ITEM_NAME
	
		
	}
}

{ ext_mana_regen_loop
	if $get_scriptflag(ent_me,mana_regen,type_exists)
	callevent 1.0 ext_mana_regen_loop
	local L_CUR_MP $get(ent_me,mp)
	local L_MAX_MP $get(ent_me,maxmp)
	if ( L_CUR_MP < L_MAX_MP )
	{
		subtract L_MAX_MP L_CUR_MP
		givemp L_MAX_MP
	}
}

{ ext_sy_quest_cheat
	callevent ext_got_quest_item ap
	callevent ext_got_quest_item ap
	callevent ext_got_quest_item ap
	callevent ext_got_quest_item ap
	callevent ext_got_quest_item ap
	callevent ext_got_quest_item km
	callevent ext_got_quest_item km
	callevent ext_got_quest_item km
	callevent ext_got_quest_item km
	callevent ext_got_quest_item km
	callevent ext_got_quest_item bs
	callevent ext_got_quest_item bp
	callevent ext_got_quest_item la
}

{ game_music 

	if ( PLR_CURRENT_MUSIC !equals PLR_PREV_MUSIC )
	{
		setvard PLR_PREV_MUSIC PLR_CURRENT_MUSIC
		setvard PLR_PREV_MUSIC_LENGTH PLR_CURRENT_MUSIC_LENGTH
	}
	setvard PLR_CURRENT_MUSIC PARAM1
	setvard PLR_CURRENT_MUSIC_LENGTH PARAM2



	if PARAM3

	setvard PLR_IDLE_MUSIC PLR_CURRENT_MUSIC
	setvard PLR_IDLE_MUSIC_LENGTH PLR_CURRENT_MUSIC_LENGTH
}


{ ext_seal_test 
	local L_SEAL_ORG $get(ent_me,origin)
	vectorset L_SEAL_ORG z $get_ground_height(L_SEAL_ORG)
	clientevent new all effects/sfx_seal_instant L_SEAL_ORG PARAM1 140 35
}


{ ext_proj_test 
	local L_TARGS $get_tsphere(enemy,1024)
	setvard PROJ_ELEMENT_TARGET $get_token(L_TARGS,0)
	setvard PROJ_ELEMENT_TYPE PARAM1

	tossprojectile view 200 0 1 proj_elemental_guided $get(ent_me,origin) notoffset
}

{ ext_contents_test
	local TRACE_START $get(ent_me,origin)
	local TRACE_END TRACE_START
	vectoradd TRACE_END $relpos($vec(0,game.monster.angles.yaw,0),$vec(0,64,0))
	local TRACE_LINE $get_traceline(TRACE_START,TRACE_END,contents)
	gplayermessage ent_me cont $get_contents(TRACE_END) trace TRACE_LINE
	effect beam point lgtning.spr 5 TRACE_START TRACE_END (255,255,0) 255 0 1
	
}

{ ext_arrow_hit 
	



	if ( PARAM1 isnot world )
	{
		local L_TARG_ID $get_by_idx(PARAM1,id)
		
		local L_BASE_DMG $get_token(PARAM2,0)
		local L_BASE_DMG_TYPE $get_token(PARAM2,1)
		local L_WEAPON_IDX $get_token(PARAM2,2)
		local L_PROJ_NAME $get_token(PARAM2,3)

		local L_WEAPON_ID $get_by_idx(L_WEAPON_IDX,id)

		

		
		local L_FINAL_DMG L_BASE_DMG
		local L_STAT skill.
		local L_BASE_STAT $get(L_WEAPON_ID,scriptvar,'WEAPON_PRIMARY_SKILL')
		stradd L_STAT L_BASE_STAT
		stradd L_STAT .power.ratio
		multiply L_FINAL_DMG $get(ent_me,L_STAT)
		local L_DMG_MULTI $get(L_WEAPON_ID,scriptvar,'WEAPON_DMG_MULTI')
		if ( L_DMG_MULTI > 0 ) multiply L_FINAL_DMG L_DMG_MULTI

		

		local L_SPECIAL_DMG 0

		
		
		if ( L_PROJ_NAME equals proj_arrow_gholy )
		{
			local L_HOLY_DMG $get(ent_me,skill.spellcasting.divination)
			add L_HOLY_DMG 5
			callexternal L_TARG_ID turn_undead L_HOLY_DMG $get(ent_me,id)
		}
		else if ( L_PROJ_NAME equals proj_arrow_fbow )
		{
			local L_FREEZE_MANA_COST 10
			local L_CDOT_DMG $get(ent_me,skill.spellcasting.ice)
			local L_FDOT_DMG CDOT_DMG
			divide L_CDOT_DMG 1.8
			divide L_FDOT_DMG 2

			local L_PROJ_TYPE 0
			if ( $get(ent_me,mp) >= L_FREEZE_MANA_COST ) 
			{
			  local L_PROJ_TYPE $rand(0,1)
			}
			if ( !L_PROJ_TYPE )
			{
				  applyeffect L_TARG_ID effects/effect_frostbite_dmg $randf(5,10) $get(ent_me,id) L_CDOT_DMG spellcasting.ice
			}
			else
			{
				  givemp ent_me $neg(FREEZE_MANA_COST)
				  applyeffect L_TARG_ID effects/freeze_solid 8.0 $get(ent_me,id) L_FDOT_DMG spellcasting.ice
			}
		}
		else if ( L_PROJ_NAME equals proj_arrow_frost )
		{
			applyeffect L_TARG_ID effects/effect_frostbite_dmg $rand(5,10) $get(ent_me,id) 5 archery
		}
		else if ( L_PROJ_NAME equals proj_arrow_gpoison )
		{
			applyeffect L_TARG_ID effects/greater_poison $rand(5,10) $get(ent_me,id) $randf(12,33) archery
		}
		else if ( L_PROJ_NAME equals proj_arrow_lightning )
		{
			applyeffect L_TARG_ID effects/effect_shock_dmg $rand(5,10) $get(ent_me,id) $randf(10,25) archery
		}
		else if ( L_PROJ_NAME equals proj_arrow_poison )
		{
			applyeffect L_TARG_ID effects/effect_poison 15 $get(ent_me,id) $randf(2,3) 0 archery
		}

		if !L_SPECIAL_DMG
		xdodamage L_TARG_ID direct L_FINAL_DMG 100% ent_me L_WEAPON_ID L_BASE_STAT L_BASE_DMG_TYPE
		
	}
	else
	{
		
	
	}
}

{ ext_set_local_version 

	setvard PLR_DLL_VERSION PARAM1
}

{ ext_proj_mana_cl 

	

	local L_PASS_TARGS PARAM1
	callexternal $get_by_idx(PARAM2,id) ext_affect_targets L_PASS_TARGS
}

{ ext_reset_bank
	add PLR_RESET_BANK 1
	if ( PLR_RESET_BANK == 1 )
	{
		consolemsg ent_me WARNING: This will delete ALL items in your Galat chest and cannot be undone!
		consolemsg ent_me Type resetbank again to confirm!
	}
	else
	{
		setvard PLR_RESET_BANK 0
		quest set ent_me b0 0
		quest set ent_me b1 0
		quest set ent_me b2 0
		quest set ent_me b3 0
		quest set ent_me b4 0
		quest set ent_me b5 0
		quest set ent_me b6 0
		quest set ent_me b7 0
		quest set ent_me b8 0
		quest set ent_me b9 0
		consolemsg ent_me Your Galat Chest bank strings have been reset.
		consolemsg ent_me Please wait a few moments before disconnecting.
	}
}

{ ext_targeted_by_mob 

	local L_NPC PARAM1

	if $get(L_NPC,isalive)

	local L_COMBAT_TAG combat
	stradd L_COMBAT_TAG $int($get(L_NPC,index))
	scriptflags ent_me add L_COMBAT_TAG combat

	if ( G_DEVELOPER_MODE )
	{	
		if !PLR_IN_COMBAT
		if $get_scriptflag(ent_me,combat,type_exists)
		hud.addstatusicon ent_me PLR_COMBAT_ICON combat 9999
		bplayermessage ent_me "!!!!!! entered combat !!!!!!"

	}

	setvard PLR_IN_COMBAT 1

	if !$get(L_NPC,scriptvar,'NPC_NOT_MUSICAL')

	if ( $get(L_NPC,scriptvar,'NPC_CUSTOM_COMBAT_MUSIC') )
	{
		local L_NPC_HAS_CUSTOM 1
		setvard PLR_COMBAT_MUSIC $get(L_NPC,scriptvar,'NPC_CMUSIC_FILE')
		setvard PLR_COMBAT_MUSIC_LENGTH $get(L_NPC,scriptvar,'NPC_CMUSIC_LENGTH')
		if ( PLR_CURRENT_MUSIC isnot PLR_COMBAT_MUSIC )
		{
			callevent set_end_combat_music
		}
		setvard PLR_CBM_CUSTOM PLR_COMBAT_MUSIC
		setvard PLR_CBM_CUSTOM_STARTER L_NPC
	}

	if ( PLR_CURRENT_MUSIC isnot PLR_COMBAT_MUSIC )
	{
		
		if !L_NPC_HAS_CUSTOM 

		if ( PLR_CBM_CUSTOM equals PLR_CURRENT_MUSIC )
		{
			if $get(PLR_CBM_CUSTOM_STARTER,isalive)
			local L_NO_REDEFINE 1 
		}

		if ( !L_NO_REDEFINE )
		{
			if ( G_COMBAT_MUSIC isnot none )
			{
				if PLR_COMBAT_MUSIC equals none
				setvard PLR_COMBAT_MUSIC G_COMBAT_MUSIC
				setvard PLR_COMBAT_MUSIC_LENGTH G_COMBAT_MUSIC_LENGTH
			}
			if ( PLR_COMBAT_MUSIC isnot none )
			{
			
				callevent set_end_combat_music
			}
			else
			{
			
				local EXIT_SUB 1
			}
		}
		else
		{
		
		}
	}
	if !EXIT_SUB

	if ( $get(PLR_CBM_CUSTOM_STARTER,isalive) )
	{
		
		if !L_NPC_HAS_CUSTOM 
		if ( PLR_CURRENT_MUSIC equals PLR_CBM_CUSTOM )
		{
			
		
			local EXIT_SUB 1
		}
	}
	if !EXIT_SUB

	if PLR_COMBAT_MUSIC isnot none
	callevent ext_music_combat add $get(L_NPC,index) PLR_COMBAT_MUSIC_LENGTH PLR_COMBAT_MUSIC
}

{ ext_untargeted_by_mob 
	local L_NPC PARAM1

	local L_COMBAT_TAG combat
	stradd L_COMBAT_TAG $int($get(L_NPC,index))
	scriptflags ent_me remove L_COMBAT_TAG
	if ( !$get_scriptflag(ent_me,combat,type_exists) )
	{
		if ( G_DEVELOPER_MODE )
		{
			if PLR_IN_COMBAT
			bplayermessage ent_me "====== exited combat ======"
			hud.killstatusicon ent_me combat
		}		
		setvard PLR_IN_COMBAT 0
	}



	if !$get(L_NPC,scriptvar,'NPC_NOT_MUSICAL')

	local L_MUSIC_TAG cmusak
	stradd L_MUSIC_TAG $int($get(L_NPC,index))
	if ( $get_scriptflag(ent_me,L_MUSIC_TAG,name_exists) )
	{
		callevent ext_music_combat remove $get(L_NPC,index)
	}
	else
	{
	
	}
}

{ game_touched_local_trans 
	if PLR_LOCAL_TRANS isnot PARAM2

	if ( PARAM1 !startswith '_' )
	{
		local L_TITLE "Local transition to "
		stradd L_TITLE PARAM1
	}
	else
	{
		local L_TITLE $string_from(L_TITLE,_)
	}

	local L_DESC "Press Use (E) to activate." 
	if ( PARAM3 )
	{
		
		stradd L_DESC " Requires all active players be present."
	}

	infomsg ent_me L_TITLE L_DESC

	setvard PLR_LOCAL_TRANS PARAM2
	setvard PLR_LTRAN_ORG $get(ent_me,origin)
	setvard PLR_LTRAN_MINS PARAM4
	setvard PLR_LTRAN_MAXS PARAM5

	local L_NEW_SPAWN PARAM6
	if ( L_NEW_SPAWN isnot none ) callevent ext_setspawn L_NEW_SPAWN

	callevent loop_check_local_trans
}

{ loop_check_local_trans
	if PLR_LOCAL_TRANS isnot none
	if ( !$within_box(ent_me,$vec(0,0,0),PLR_LTRAN_MINS,PLR_LTRAN_MAXS) ) 
	{
		setvard PLR_LOCAL_TRANS none
	
	}
	else
	{
		callevent 1.0 loop_check_local_trans
	}
}

{ ext_setspawn 

	settrans ent_me PARAM1
	quest set ent_me d PARAM1
}

{ ext_remove_afk
	if ( G_DEVELOPER_MODE ) 
	{
		if IS_AFK
		bplayermessage ent_me "====== No longer afk"
	}
	setvard IS_AFK 0
	setvard PLR_LAST_ATK_TIME game.time
}

{ ext_poison_bolt 

	local L_PBOLT_DURATION 15.0
	setvard PLR_PBOLT_AOE 64 
	local L_PBOLT_LOC PARAM1

	

	scriptflags ent_me add stack_pbolt pbolt L_PBOLT_LOC L_PBOLT_DURATION
	clientevent new all effects/sfx_poison_cloud L_PBOLT_LOC PLR_PBOLT_AOE L_PBOLT_DURATION
	if !PLR_PBOLT_ACTIVE
	setvard GAME_PVP game.pvp
	setvard PLR_PBOLT_ACTIVE 1
	setvard PLR_PBOLT_COUNTER 0
	setvard PLR_PBOLT_DOT $get(ent_me,skill.spellcasting.affliction)
	callevent ext_poison_bolt_loop
}

{ ext_poison_bolt_loop
	
	if PLR_PBOLT_ACTIVE

	setvard PLR_PBOLT_ARRAY $get_scriptflag(ent_me,pbolt,type_array)

	if ( PLR_PBOLT_ARRAY isnot none )
	{
		local L_NBOLTS $get_array_amt(PLR_PBOLT_ARRAY)
		subtract L_NBOLTS 1
		if ( PLR_PBOLT_COUNTER > L_NBOLTS ) setvard PLR_PBOLT_COUNTER 0 
		setvard PLR_PBOLT_ORG $get_array(PLR_PBOLT_ARRAY,PLR_PBOLT_COUNTER)
		callevent ext_poison_bolt_dmg
		
		local L_BOLT_SCAN_SPEED 1.0

		if ( L_NBOLTS > 0 ) divide L_BOLT_SCAN_SPEED L_NBOLTS
		if ( L_BOLT_SCAN_SPEED < 0.1 ) local L_BOLT_SCAN_SPEED 0.2 

		add PLR_PBOLT_COUNTER 1

		callevent L_BOLT_SCAN_SPEED ext_poison_bolt_loop
	}
	else
	{
		setvard PLR_PBOLT_ACTIVE 0
	}
}


{ ext_poison_bolt_dmg
	local L_SCAN_POINT PLR_PBOLT_ORG
	vectoradd L_SCAN_POINT z 32 

	
	

	setvard PLR_PBOLT_TARGS $get_tsphere(enemy,PLR_PBOLT_AOE,L_SCAN_POINT)
	
	if PLR_PBOLT_TARGS isnot none
	calleventloop $get_token_amt(PLR_PBOLT_TARGS) ext_poison_bolt_affect
}

{ ext_poison_bolt_affect
	local CUR_TARG $get_token(PLR_PBOLT_TARGS,game.script.iteration)
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	applyeffect CUR_TARG effects/greater_poison 10.0 $get(ent_me,id) PLR_PBOLT_DOT spellcasting.affliction
}


{ npc_suicide
	

}

{ ext_speak 
	
	saytext PARAM1
}

{ ext_req_viewmodel_idx

	clientevent update ent_me const.localplayer.scriptID ext_svreq_viewmodel_idx
}

{ ext_rec_viewmodel_idx
	setvard PLR_ACTIVE_VIEWMODEL PARAM1
	local L_VIEWMODEL_ID $get_by_idx(PLR_ACTIVE_VIEWMODEL,id)

	effect beam follow lgtning.spr L_VIEWMODEL_ID 1 30 30.0 200 (255,0,0)
	setprop L_VIEWMODEL_ID rendermode 5
	setprop L_VIEWMODEL_ID renderamt 255
}

{ ext_remove_effect

	removeeffect ent_me PARAM1
}

{ ext_crest_request
	setvard PLR_LAST_CREST game.time
	add PLR_LAST_CREST 180.0
}

{ ext_clientcmd
	clientevent update ent_me const.localplayer.scriptID ext_cl_clientcmd PARAM1
}

{ ext_set_fquest
	local L_FEL_QUEST $get_quest_data(ent_me,f)
	if ( L_FEL_QUEST equals complete )
	{
		helptip ent_me generic "One Time Quest Already Completed" "You cannot obtain another Felewyn Shard by completing this quest again."
	}
	else
	{
		if ( $get_token(L_FEL_QUEST,0) isnot 1 )
		{
			helptip ent_me generic "One Time Quest Acquired" "The Felwyn Shard quest can only be completed one time per character.|The quest is completed upon acquisition of one of the Shards of the Felewyn Blade."
			quest set ent_me f 1
		}
	}
}

{ ext_clreq_bloom_check 

	
	hud.addimgicon ent_me ms_bloom_test btest 10 0 80 100 10.0
}

{ ext_debug_que 

	if ( !$get_array(ARRAY_DEBUG,exists) ) array.create ARRAY_DEBUG

	array.add ARRAY_DEBUG PARAM1

	if !PLR_DEBUG_ARRAY_ACTIVE
	setvard PLR_DEBUG_ARRAY_ACTIVE 1
	callevent ext_debug_que_loop
}

{ ext_debug_que_loop
	if PLR_DEBUG_ARRAY_ACTIVE
	local L_OUT $get_array(ARRAY_DEBUG,0)
	if ( $len(L_OUT) > 100 ) 
	{
		local L_LOUT $left(L_OUT,100)
		stradd L_LOUT *
	}
	consolemsg ent_me L_OUT
	array.del ARRAY_DEBUG 0
	if ( $get_array_amt(ARRAY_DEBUG) > 0 )
	{
		callevent 0.1 ext_debug_que_loop
	}
	else
	{
		setvard PLR_DEBUG_ARRAY_ACTIVE 0
	}
}

{ ext_quake_fx 
	local L_DUR PARAM1
	playsound 1 10 magic/volcano_start.wav 0.8 60
	svplaysound 2 10 magic/volcano_loop.wav 0.8 60
	clientevent new ent_me effects/sfx_quake $get(ent_me,index) 1 256 L_DUR
	effect screenshake $get(ent_me,origin) 50 10 L_DUR 512
	callevent L_DUR ext_quake_fx_end
}

{ ext_quake_fx_end
	svplaysound 2 0 magic/volcano_loop.wav
}

{ ext_teleportdest
	teleportdest PARAM1 PARAM2
}

{ ext_iexist_test
	consolemsg ent_me item_exist_test $item_exists(ent_me,PARAM1,PARAM2)
}

{ ext_shield_up 
	if ( PARAM1 )
	{
		setvard PLR_SHIELD_UP 1
		scriptflags ent_me add shieldnp nopush 1 -1 none
	}
	else
	{
		setvard PLR_SHIELD_UP 0
		scriptflags ent_me remove shieldnp
	}
}

{ ext_debug_draw_circle 
	setvard DBG_CIRCLE_POS PARAM1
	setvard DBG_CIRCLE_RAD PARAM2
	setvard DBG_CIRCLE_ROT 0
	setvard DBG_CIRCLE_COLOR (255,0,255)
	setvard DBG_CIRCLE_DUR 20.0
	if ( PARAM3 !startswith PARAM ) setvard DBG_CIRCLE_DUR PARAM3
	if ( PARAM4 !startswith PARAM ) setvard DBG_CIRCLE_COLOR PARAM3
	
	calleventloop 16 ext_debug_draw_circle_loop
}

{ ext_debug_draw_circle_loop
	local L_BEAM_START DBG_CIRCLE_POS
	vectoradd L_BEAM_START $relpos($vec(0,DBG_CIRCLE_ROT,0),$vec(0,DBG_CIRCLE_RAD,0))
	add DBG_CIRCLE_ROT 22.5

	local L_BEAM_END DBG_CIRCLE_POS
	vectoradd L_BEAM_END $relpos($vec(0,DBG_CIRCLE_ROT,0),$vec(0,DBG_CIRCLE_RAD,0))
	

	effect beam point lgtning.spr 20 L_BEAM_START L_BEAM_END DBG_CIRCLE_COLOR 200 0 DBG_CIRCLE_DUR
}

{ ext_set_next_circle_heal
	setvard NEXT_CIRCLE_HEAL PARAM1
}

{ ext_dburst 
	setvard PLR_DBURST_ORG PARAM1
	setvard PLR_DBURST_AOE PARAM2
	setvard PLR_DBURST_PUSH PARAM3
	local L_USE_SOUND PARAM4
	setvard GAME_PVP game.pvp

	setvard PLR_DBURST_SKILL $get(ent_me,skill.spellcasting.affliction)
	setvard PLR_DBURST_DMG PLR_DBURST_SKILL
	multiply PLR_DBURST_DMG 3
	setvard PLR_DBURST_DOT PLR_DBURST_SKILL
	multiply PLR_DBURST_DOT 0.5

	setvard PLR_DBURST_MAXPUSH $get(ent_me,maxhp)
	multiply PLR_DBURST_MAXPUSH 4

	clientevent new all effects/sfx_dburst PLR_DBURST_ORG PLR_DBURST_AOE L_USE_SOUND
	
	

	xdodamage PLR_DBURST_ORG PLR_DBURST_AOE PLR_DBURST_DMG 0.1 ent_me ent_me spellcasting.affliction dark_effect dmgevent:dburst
}

{ dburst_dodamage
	if PARAM1
	if ( $get(PARAM2,isplayer) )
	{
		if !GAME_PVP
		exitevent
	}
	if $get(PARAM2,relationship,ent_me) equals enemy
	applyeffect PARAM2 effects/effect_defile 15.0 $get(ent_me,id) PLR_DBURST_DOT 0.5

	if PLR_DBURST_PUSH

	local CUR_TARG PARAM2

	local TARG_HP $get(CUR_TARG,hp)
	if ( TARG_HP < PLR_DBURST_MAXPUSH ) local DO_PUSH 1
	if DO_PUSH
	local TARG_ORG $get(CUR_TARG,origin)
	local TARG_ANG $angles(PLR_DBURST_ORG,TARG_ORG)
	setvelocity CUR_TARG $relvel($vec(0,TARG_ANG,0),$vec(0,1000,0))
}

{ ext_report_armor
	consolemsg PARAM2 $get(ent_me,name) Armor: $get_takedmg(ent_me,all) PARAM1 : $get_takedmg(ent_me,PARAM1)
}

{ ext_ce_wave 

	local L_POS PARAM1
	vectoradd L_POS z 32
	if ( PARAM2 equals holy )
	{
		local L_DMG $get(ent_me,skill.spellcasting.divination)
		multiply L_DMG 2
		xdodamage L_POS 128 $get(ent_me,skill.spellcasting.divination) 0 ent_me ent_me spellcasting.divination holy_effect dmgevent:holywave
	}
}

{ holywave_dodamage
	if PARAM1
	if ( $get(PARAM2,relationship,ent_me) equals enemy ) local L_ENEMY 1
	if ( $get(PARAM2,isplayer) ) local L_ENEMY 0
	if ( L_ENEMY )
	{
		if !$get(PARAM2,isplayer)
		local L_DOT $get(ent_me,skill.spellcasting.divination)
		multiply L_DOT 0.5
		applyeffect PARAM2 effects/effect_holy_dmg 5.0 $get(ent_me,id) L_DOT
	}
	else
	{
		local L_DOT $get(ent_me,skill.spellcasting.divination)
		multiply L_DOT 4
		effect glow PARAM2 (0,255,0) 256 0.5 0.5
		if ( $get(PARAM2,isplayer) )
		{
			local L_ADD_DMG_POINTS 1
			gplayermessage PARAM2 $get(ent_me,name) heals you for $int(L_DOT) hp
		}
		if ( $get(PARAM2,scriptvar,'NPC_CRITICAL') ) local L_ADD_DMG_POINTS 1
		if ( L_ADD_DMG_POINTS )  callexternal ent_me add_dmg_points L_DOT
		
		if ( $get(PARAM2,hp) < $get(PARAM2,maxhp) )
		{
			givehp PARAM2 L_DOT
			gplayermessage ent_me You heal $get(PARAM2,name) for $int(L_DOT) hp
		}
		else
		{
			gplayermessage ent_me $get(PARAM2,name) is at maximum health
		}
	}
}
