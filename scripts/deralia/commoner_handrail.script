
{
	const NO_JOB 1
	const NO_RUMOR 1
}

#include monsters/base_npc
#include monsters/base_chat

{ npc_spawn

   hp		1
   width	5
   height	64
   race		beloved
   name		Commoner
   roam		0
   setmodel	npc/human1.mdl
   setmodelbody 0 1
   setmodelbody 1 2
   setidleanim handrailidle
   
   invincible	1

	catchspeech say_hi hello hi hail greet hey
	catchspeech say_boat boat row oar
}

{ say_hi

   setvard RND_TALK $rand(1,3)
   if ( RND_TALK == 1 ) callevent respond1
   if ( RND_TALK == 2 ) callevent respond2
   if ( RND_TALK == 3 )	callevent respond3
}

{ respond1
   saytext Sometimes I just come here and look up at the stars.
}
{ respond2
   saytext ...
}
{ respond3
	saytext I'd love to row that boat and never come back, if these arms weren't so sore.
	setvard MENTIONED_BOAT 1
}

{ game_menu_getoptions


		local reg.mitem.title 	"Ask about the row boat"
		local reg.mitem.type 	callback
		local reg.mitem.callback say_boat
		local reg.mitem.cb_failed payment_failed
		menuitem.register

		if MENTIONED_BOAT
				
		local reg.mitem.title 	"Rent the Rowboat (5 gp)"
		local reg.mitem.type 	payment
		local reg.mitem.data	gold: 5
		local reg.mitem.callback borrow_boat
		local reg.mitem.cb_failed payment_failed
		menuitem.register
}

{ borrow_boat
	callexternal GAME_MASTER gm_map_vote $get(ent_me,id) gertenheld_cape "Row to Gertenheld" 1
	callevent 0.1 set_map
}

{ set_map
	callexternal players ext_set_map gertenheld_cape from_deralia from_deralia
}

{ payment_failed

	saytext Not worth my time to get you the oars, at that price.
}

{ say_boat

	setvard MENTIONED_BOAT 1

	if !BUSY_CHATTING

	setvard CHAT_STEPS 3
	setvard CHAT_STEP 0
	setvard BUSY_CHATTING 1
	setvard CHAT_STEP1 "That little boat down there? I... Rent it out sometimes."
	setvard CHAT_STEP2 "It doesn't go as far as Charon's gallion, of course. But the little thing can get into places where his beast can't."
	setvard CHAT_STEP3 "For instance, you can row up to Gertenheld cape, and see the lightouse. Easy row that."
	callevent chat_loop
}
