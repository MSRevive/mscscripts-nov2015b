
{ 	[server]
	const EFFECT_ID player_haste
	const EFFECT_NAME "Haste"
	const EFFECT_ACTIONTYPE normal
	const EFFECT_SCRIPT $currentscript

	setvard game.effect.id EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type EFFECT_ACTIONTYPE

	local reg.effect.name	EFFECT_ID
	local reg.effect.flags	EFFECT_ACTIONTYPE
	local reg.effect.script	EFFECT_SCRIPT

	setvard game.effect.removeondeath 1

	registereffect
}

{ [server] game_activate 
	setvard ESPEED_DURATION PARAM1
	setvard ESPEED_SPEED_RATIO PARAM2
	setvard ESPEED_TAG PARAM3

	local L_ACTION add
	if ( $get_scriptflag(ent_me,ESPEED_TAG,name_exists) ) local L_ACTION edit

	scriptflags ent_me L_ACTION ESPEED_TAG speed ESPEED_SPEED_RATIO ESPEED_DURATION

	if ( EFFECT_DUPLICATED )
	{
	
		if ( G_DEVELOPER_MODE ) gplayermessage G_DEV_PLAYER $currentscript duplicated
	}

	if !EFFECT_DUPLICATED

	callevent get_total_speed

	if ( $get(ent_me,isplayer) )
	{
		clientevent new ent_me $currentscript ESPEED_DURATION ESPEED_SPEED_RATIO
		setvard ESPEED_CL_IDX game.script.last_sent_id
		setvard game.effect.movespeed $math(multiply,ESPEED_SPEED_RATIO,100)
	}

    setvard game.effect.anim.framerate ESPEED_SPEED_RATIO



	if ( ESPEED_DURATION > -1 ) callevent ESPEED_DURATION effect_die 1
}

{ [shared] effect_die 
	if ( game.serverside )
	{
		if PARAM1
		clientevent update ent_me ESPEED_CL_IDX effect_die
	}
	removescript
}

{ [server] game_duplicated
	setvard EFFECT_DUPLICATED 1
	callexternal ent_me ext_effect_speed update
	callevent effect_die
}

{ [server] ext_effect_speed 
	if !EFFECT_DUPLICATED

	if ( PARAM1 equals update )
	{
		callevent get_total_speed
	}

	if ( PARAM1 equals remove )
	{
	
		scriptflags ent_me remove PARAM2
		if ( !$get_scriptflag(ent_me,speed,type_exists) )
		{
			clientevent update ent_me ESPEED_CL_IDX effect_die
			callevent effect_die
		}
	}
}

{ [client] client_activate

	setvard game.cleffect.move_scale.forward 	PARAM2
	setvard game.cleffect.move_scale.right 		PARAM2
	if ( PARAM1 > -1 ) callevent PARAM1 effect_die
}

{ [server] get_total_speed
	if ( !$get_scriptflag(ent_me,speed,type_exists) )
	{
		callevent effect_die 1
		exitevent
	}

	local L_SPEED_FLAG_TOTAL $get_scriptflag(ent_me,speed,type_value)
	local L_SPEED_FLAG_COUNT $get_scriptflag(ent_me,speed,type_count)

	local L_NEW_SPEED $math(divide,L_SPEED_FLAG_TOTAL,L_SPEED_FLAG_COUNT)

	setvard ESPEED_SPEED_RATIO L_NEW_SPEED


}

{ [server] ext_scriptflag_expired
	if !EFFECT_DUPLICATED
	callevent 0.1 get_total_speed
}
