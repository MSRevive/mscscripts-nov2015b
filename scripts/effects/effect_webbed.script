{
	const EFFECT_ID			webbed
	const EFFECT_NAME		"webbed"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript
}

{ game_precache

	local reg.effect.name	EFFECT_ID
	local reg.effect.flags	EFFECT_ACTIONTYPE
	local reg.effect.script	EFFECT_SCRIPT

	setvard game.effect.removeondeath 1

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.flags	EFFECT_ACTIONTYPE

	setvard game.effect.canjump 0

	registereffect
}

{ [server] game_activate 


	if ( $get(ent_me,scriptvar,'COCOON_ATTACHED') )
	{
		
		setvard RESISTED_EFFECT 2
		callevent effect_die
		exitevent
	}

	setvard WEBBER_ID PARAM3

	if ( $get(WEBBER_ID,isplayer) )
	{
		local L_RESIST_STR $get(ent_me,name)
		stradd L_RESIST_STR " is too large to be encased in webs!"
		if ( $get(ent_me,width) >= 64 ) setvard RESISTED_EFFECT 1
		if ( $get(ent_me,height) > 96 ) setvard RESISTED_EFFECT 1

		
		
		
		
		
		

		local L_MAX_WEB_STR $get(WEBBER_ID,maxhp)
		multiply L_MAX_WEB_STR 3
		if $get(ent_me,hp) > L_MAX_WEB_STR
		stradd L_RESIST_STR " is too strong to be encased in webs!"
		setvard RESISTED_EFFECT 1
	}
	if ( RESISTED_EFFECT )
	{
		if ( $get(WEBBER_ID,isplayer) ) playermessage WEBBER_ID L_RESIST_STR
		callevent effect_die
	}
	if !RESISTED_EFFECT

	setvard game.effect.canjump 0

	setvard EFFECT_ACTIVE 1
	if ( $get(ent_me,isplayer) )
	{
		setvard OLD_WEBS $get(ent_me,scriptvar,'WEBS_ATTACHED')
		clientevent update ent_me const.localplayer.scriptID web_effect $get(ent_me,index) OLD_WEBS
		playermessage ent_me You have been webbed!
	}
	setvard COCOON_STRENGTH PARAM1
	setvard WEB_STRENGTH PARAM2
	setvard COCOON_START 11
	subtract COCOON_START WEB_STRENGTH
	setvard COC_DURATION PARAM4
	setvard COC_DOT PARAM5

	local L_CUR_ANIM_RATE $get(ent_me,scriptvar,'BASE_FRAMERATE')
	if ( L_CUR_ANIM_RATE == 0 ) local L_CUR_ANIM_RATE 1.0
	if ( $get(ent_me,isplayer) ) local L_CUR_ANIM_RATE 1.0
	setvard ORIG_FRAMERATE L_CUR_ANIM_RATE

	callexternal $get(ent_me,id) ext_set_slowed 1
	callevent effect_loop
}

{ [server] effect_loop
	if EFFECT_ACTIVE
	callevent 0.25 effect_loop

	local WEB_COUNT $get(ent_me,scriptvar,'WEBS_ATTACHED')

	

	if ( WEB_COUNT == 0 )
	{
		callevent effect_die
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( WEB_COUNT >= COCOON_START )
	{
		local COCOON_ORG $get(ent_me,origin)
		if ( $get(ent_me,isplayer) ) vectoradd COCOON_ORG z -30
		setvard DID_COCOON 1
		createnpc monsters/summon/cocoon COCOON_ORG $get(ent_me,id) COCOON_STRENGTH COC_DURATION COC_DOT WEBBER_ID
		setvard RESISTED_EFFECT 0
		callevent effect_die
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( !$get(ent_me,isalive) )
	{
		callevent effect_die
	}

	local SPEED_REDUCE WEB_COUNT
	multiply SPEED_REDUCE 10
	local CUR_SPEED 100
	subtract CUR_SPEED SPEED_REDUCE


	local L_SLOW_RATIO $math(divide,WEB_COUNT,10)
	local L_SLOW_RATIO $math(subtract,1,L_SLOW_RATIO)
	local L_CUR_ANIM_RATE ORIG_FRAMERATE
	multiply L_CUR_ANIM_RATE L_SLOW_RATIO
	

	setvard game.effect.movespeed CUR_SPEED
	setvard game.effect.canjump 0
	setvard game.effect.anim.framerate L_CUR_ANIM_RATE
}

{ [server] effect_die


	if ( !$get(ent_me,isplayer) )
	{
		callexternal ent_me ext_webs_remove
	}

	if ( !RESISTED_EFFECT )
	{
		setvard EFFECT_ACTIVE 0
		setvard game.effect.movespeed 100%
		setvard game.effect.canjump 1
		setvard game.effect.anim.framerate ORIG_FRAMERATE
		callexternal $get(ent_me,id) ext_set_slowed 0
		if ( $get(ent_me,isplayer) )
		{
			clientevent update ent_me const.localplayer.scriptID fx_web_set_webs 0
		}
		if ( !DID_COCOON ) playermessage ent_me The webs have fallen away.
	}
	removescript
}
