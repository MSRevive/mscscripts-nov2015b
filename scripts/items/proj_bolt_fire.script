
{
	const HITSCAN_BOLT 1
	const MODEL_WORLD weapons/projectiles.mdl
	const SOUND_HITWALL1 weapons/bow/arrowhit1.wav
	const SOUND_HITWALL2 weapons/bow/arrowhit1.wav
  	const MODEL_BODY_OFS 49
	const ARROW_BODY_OFS 49

	const PROJ_DAMAGE_AOE_RANGE 250

	const PROJ_DAMAGE_TYPE fire

	const PROJ_ANIM_IDLE none

	const PROJ_IGNORENPC 1

	const PROJ_DAMAGE $rand(400,500)
  	const PROJ_STICK_DURATION	1 	
  	const ARROW_SOLIDIFY_ON_WALL	1  	
  	const ARROW_BREAK_CHANCE	100%	
}

#include items/proj_arrow_base

{ arrow_spawn

	name 	a|Dwarven Bolt
	desc 	An awkward bolt made by the dwarves. "Handle with care"
	weight 	0.1
	size 	1
	value	500
	gravity		0
	groupable	25
	sethudsprite hand arrows
	sethudsprite trade expbolt

	
	setprop ent_me rendermode 5
	setprop ent_me renderamt 0
}



{ [override] [server] hitscan_bolt

	setvard MY_OWNER $get(ent_expowner,id)
	setvard MY_XBOW $get(MY_OWNER,active_item)

	setvard BOLT_DAMAGE $get(MY_OWNER,skill.archery)
	multiply BOLT_DAMAGE 0.01
	multiply BOLT_DAMAGE PROJ_DAMAGE
	local DMG_MULTI $get(MY_XBOW,scriptvar,'HITSCAN_DMG_MULTI')
	if ( DMG_MULTI > 0 ) multiply BOLT_DAMAGE DMG_MULTI

	setvard START_TRACE $get(ent_me,origin)
	
	
	

	local V_MY_DEST $relpos($vec($neg(game.monster.angles.pitch),game.monster.angles.yaw,game.monster.angles.roll),$vec(0,8000,0))
	local MY_DEST START_TRACE
	vectoradd MY_DEST V_MY_DEST

	setvard MY_START_ANG $get(ent_me,angles)

	setprop ent_me avelocity 0
	setprop ent_me velocity 0
	setprop ent_me movetype 0


	
	if ( G_DEVELOPER_MODE ) effect beam point lgtning.spr 20 START_TRACE MY_DEST (255,0,255) 200 0 1.0
	xdodamage START_TRACE MY_DEST 0 100% MY_OWNER ent_me none target dmgevent:*boltscan
	
}

{ go_splodie
	if !DID_SPLODIE
	movetype	none
	setorigin ent_me TRACE_END
	setvard DID_SPLODIE 1
	playrandomsound 0 10 weapons/explode3.wav
	attachlight torch
	attachsprite explode1.spr trans 12 1.5
	expiretime 1
}

{ boltscan_dodamage
	if ( $get(PARAM2,isalive) )
	{
		setvard TRACE_END $get(PARAM2,origin)
		setvard DIRECT_HIT 1
	}
	else
	{
		local L_END PARAM4
		local L_TRACE $get_traceline(START_TRACE,L_END,worldonly)
		setvard TRACE_END L_TRACE
	}


	if ( G_DEVELOPER_MODE )
	{
		local L_TRACE_END TRACE_END
		vectoradd L_TRACE_END z 64
		effect beam point lgtning.spr 20 TRACE_END L_TRACE_END (255,0,0) 200 0 5.0

		local L_END PARAM4
		vectoradd L_END z 64
		effect beam point lgtning.spr 20 PARAM4 L_END (0,0,255) 200 0 5.0
	}
	callevent 0.1 splode_dmg
}

{ splode_dmg
	xdodamage TRACE_END 128 BOLT_DAMAGE 0.5 MY_OWNER ent_me archery PROJ_DAMAGE_TYPE dmgevent:*bolt
	setorigin ent_me TRACE_END
	
	callevent 0.1 go_splodie
}

{ bolt_dodamage 
	if ( $get(PARAM2,isalive) )
	{
	
		
		if PARAM1
		local HIT_TARG PARAM2
		if $get(HIT_TARG,maxhp) < 1000
		if $get(HIT_TARG,race) isnot human
		if $get(HIT_TARG,race) isnot hguard
		
		local TARG_ORG $get(HIT_TARG,origin)
		local NEW_YAW $angles(TRACE_END,TARG_ORG)
		if ( !DIRECT_HIT )
		{
			addvelocity PARAM2 $relvel($vec(0,NEW_YAW,0),$vec(10,400,600))
		}
		else
		{
			addvelocity PARAM2 $relvel($vec(0,NEW_YAW,0),$vec(10,-400,600))
		}
	}
}


{ game_fall

	setprop ent_me rendermode 0
	setprop ent_me renderamt 255
}

{ game_tossprojectile
	setprop ent_me rendermode 5
	setprop ent_me renderamt 0
}
