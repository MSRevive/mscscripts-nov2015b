

{ [server]
	const EFFECT_ID			effect_stun
	const EFFECT_NAME		"Stun"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	const EFFECT_BLOCK_RESISTS 1

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const SOUND_RESIST body/armour3.wav
}

#include effects/effect_base	allowduplicate

{ [server] game_activate 

	

	if ( $get(ent_me,nopush) )
	{
		if !$get(ent_me,scriptvar,'NPC_CAN_BE_STUNNED') 
		gplayermessage ent_me You are immune to the stun effect. 
		dplayermessage PARAM4 $get(ent_me,name) is immune to stun effects.
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if !$get(ent_me,scriptvar,IS_LURE) 
	
	callevent resist_check stun $get(PARAM4,id) 

	if !RESISTED_EFFECT

	setvard local.effect.duration 	PARAM1

	setvard game.effect.movespeed 30%
	setvard game.effect.canjump 0
	setvard game.effect.anim.framerate 0.4

	

	if ( PARAM3 equals 1 ) setvard game.effect.canattack 0

	local slowduration $randf(0.2,local.effect.duration)
	if( PARAM2 equals 1 ) applyeffect ent_me effects/effect_push slowduration (0,0,0) 1 1

	if ( $get(ent_me,isplayer) )
	{
		hud.addstatusicon ent_me alpha_stun stun local.effect.duration
	}

	if ( FX_SPAMMERS < MAX_FX_SPAMMERS ) 
	{
		
		
		clientevent new all effects/sfx_stunring $get(ent_me,index) local.effect.duration $get(ent_me,height)
	}

	callexternal $get(ent_me,id) ext_set_slowed 1

	callevent local.effect.duration effect_die
}

{ effect_die
	callexternal $get(ent_me,id) ext_set_slowed 0
}

{ game_precache

	precachefile			effects/sfx_stunring
}

{ game_death
	
	callevent effect_die
}

{ game_duplicated

	callevent game_death
}
