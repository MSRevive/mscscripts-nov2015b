//This is based on an older script and may have some bad conventions as a result.

//Thothie - misc minor improvements sound + animation
//Standard Troll
{
	const FREQ_THROW $randf(15.0,30.0)

	setvard SOUND_STRUCK1	weapons/cbar_hitbod1.wav
	setvard SOUND_STRUCK2	weapons/cbar_hitbod1.wav
	setvard SOUND_STRUCK3	weapons/cbar_hitbod1.wav
	const SOUND_PAIN	monsters/troll/trollpain.wav
	const SOUND_ATTACK	monsters/troll/trollattack.wav
	const SOUND_DEATH	monsters/troll/trolldeath.wav
	const SOUND_WALK1	monsters/troll/step1.wav
	const SOUND_WALK2	monsters/troll/step2.wav
	const SOUND_IDLE	monsters/troll/trollidle2.wav
	setvard DROP_GOLD 		1
	setvard DROP_GOLD_MIN		20
	setvard DROP_GOLD_MAX		65
	setvar ANIM_IDLE idle0
	setvar ANIM_RUN	run
	setvar ANIM_WALK	walk
	setvard ANIM_DEATH	die_fall
	setvard ANIM_ATTACK	double_punch	//this varies, needs to be a var
	setvard ATTACK_RANGE	75
	setvard ATTACK_HITRANGE	150
	setvard CAN_FLINCH	0
	setvard MOVE_RANGE	100
	setvard CAN_HUNT	1
	setvard HUNT_AGRO	1
	setvard NPC_GIVE_EXP		300
	setvard NPC_MUST_SEE_TARGET 0
    //setvard STUCK_CHECK_FREQUENCY 1.2 //I move pretty damn fast, so stuck check more often
	
	precache SOUND_DEATH

	precachefile monsters/base_monster
	precachefile monsters/base_npc_attack
	precachefile monsters/base_npc

	precache weapons/axemetal1.wav
	precache weapons/axemetal2.wav
	precache debris/concrete1.wav
	precache weapons/cbar_hitbod1.wav

}

{ game_precache
	precachefile monsters/summon/spiked_ball
}

#include monsters/base_monster_new

{ npc_spawn

	hp 600
	takedmg all 0.25
	//ov 100
	width 100
	height 100
	name a|Battle Troll
	if ( game.map.name equals ww3d ) setvard IS_GUARDIAN 1
	if ( G_SHAD_PRESENT ) callevent bo_zombie_mode
	if ( IS_GUARDIAN )
	{
		name a|Temple Guardian
		race demon
		setprop ent_me skin 1
		setvard IMMUNE_VAMPIRE 1
		blood none
		callevent 0.1 guard_attribs
	}
	if ( !IS_GUARDIAN ) race orc
	roam 1
	setmodel monsters/troll_armored.mdl
	setidleanim idle1
	setmoveanim ANIM_WALK
	setactionanim ANIM_ATTACK
	hearingsensitivity 5

	callevent 10.0 random_idle

	callevent troll_spawn
}

{ guard_attribs
	takedmg holy 2.0
	setvard SOUND_STRUCK1	weapons/axemetal1.wav
	setvard SOUND_STRUCK2	weapons/axemetal2.wav
	setvard SOUND_STRUCK3	debris/concrete1.wav
}

{ attack_1 //called by model double_punch @ frame 13 and 26

	playsound 2 10 SOUND_ATTACK
	setvar PUSH_VEL $relvel(10,200,10)
	if ( $get(NPCATK_TARGET,range) < ATTACK_HITRANGE ) callevent npcatk_dodamage ENTITY_ENEMY direct $randf(30,40) 75% blunt
	
	if ( $rand(1,4) == 1 ) setvard ANIM_ATTACK hit_down
}

{ attack_2 //called by model hit_down @ frame 20

	playsound 2 10 SOUND_ATTACK
	if ( $get(NPCATK_TARGET,range) < ATTACK_HITRANGE ) callevent npcatk_dodamage ENTITY_ENEMY direct $randf(55,65) 75% blunt
	setvard ANIM_ATTACK double_punch
}

{ npc_struck
   
	volume 5
	playsound 2 5 SOUND_PAIN
}

{ stomp_1	//called by model run @ frame 10 (newer June 27,2006+ model only)

   playsound 2 8 SOUND_WALK1
}

{ stomp_2	//called by model run @ frame 30 (newer June 27,2006+ model only)

   playsound 2 8 SOUND_WALK2
}

{ game_hearsound

   volume 10
   playsound 2 SOUND_IDLE
}

//idle anims
{ random_idle
	////dbg CanHear CAN_HEAR
	callevent 10.0 random_idle
	if !IS_HUNTING
	if !$cansee(enemy)
	if !IS_FLEEING
	local ANIM_SELECT $rand(0,3)
	if ( ANIM_SELECT == 0 ) setvar ANIM_IDLE idle0
	if ( ANIM_SELECT == 1 ) setvar ANIM_IDLE idle1
	if ( ANIM_SELECT == 2 ) setvar ANIM_IDLE idle2
	if ( ANIM_SELECT == 3 ) setvar ANIM_IDLE idle3
	if ( ADVANCED_SEARCHING ) setvar ANIM_IDLE idle0
	playanim once ANIM_IDLE
}

{ warcry	//called by model idle2 @ frame 28

	playsound 2 10 SOUND_IDLE
}

{ npcatk_targetvalidate

	if !DID_WARCRY
	if $get(ent_lastseen,isplayer)

	setvard AS_ATTACKING game.time	
	playanim critical idle2
	setvard DID_WARCRY 1
	setvard NEXT_THROW game.time
	add NEXT_THROW 1.0
}

{ my_target_died
	setvard DID_WARCRY 0
	if !$cansee(player)
	playanim critical idle2
}

{  game_damaged_other

   if PUSH_VEL isnot 'PUSH_VEL'

   addvelocity ent_laststruckbyme PUSH_VEL//Push the player a bit

}

{ bo_zombie_mode
	name a|Petrified Troll
	race demon
	setprop ent_me skin 1
	setvard IMMUNE_VAMPIRE 1
	blood none
	callevent 0.1 guard_attribs
}

{ npc_targetsighted
	if DID_WARCRY
	if game.time > NEXT_THROW
	if $get(NPCATK_TARGET,range) > ATTACK_RANGE
	setvard NEXT_THROW game.time
	add NEXT_THROW FREQ_THROW
	setmovedest NPCATK_TARGET 9999
	setvard THROW_TARGET NPCATK_TARGET
	setvard IN_THROW 1
	roam 0
	callevent npcatk_suspend_ai
	playanim critical throw_rock
	callevent 5.0 exit_throw //in case flinches or otherwise stuck
}

{ rock_pickup
	setmodelbody 1 1
}

{ rock_throw
	createnpc monsters/summon/spiked_ball $relpos(0,64,100) $get(ent_me,id) $relvel(0,500,110) 0.3
	callevent exit_throw
}

{ exit_throw
	if IN_THROW
	setmodelbody 1 0
	setvard IN_THROW 0
	roam 1
	callevent npcatk_resume_ai
}

{ set_bonus_orc_for
	if $lcase(game.map.name) equals orc_for
	setvard NPC_BONUS_XP_RATIO 3.0
}