//Stun
//Any npc
//Param1 - Duration
//Param2 - Inflicter (unused at present - save maybe to pass resistance checks back to player)
//Param3 - Set to 1 = can't attack

{ [server]
	const EFFECT_ID			effect_stun
	const EFFECT_NAME		"Stun"
	const EFFECT_ACTIONTYPE		nostack
	const EFFECT_SCRIPT		$currentscript

	const EFFECT_BLOCK_RESISTS 1

	setvard game.effect.id		EFFECT_ID
	setvard game.effect.displayname	EFFECT_NAME
	setvard game.effect.type	EFFECT_ACTIONTYPE

	const SOUND_RESIST body/armour3.wav

	const HEAVY_STUN 1
}

#include effects/effect_base	allowduplicate
#include effects/effect_drunk

{ [server] game_activate //<duration> [push] [cantattak] [attacker]

	if ( $get(ent_me,nopush) )
	{
		if !$get(ent_me,scriptvar,'NPC_CAN_BE_STUNNED') //a few monsters can't be pushed, but can still be stunned
		gplayermessage ent_me You are immune to the stun effect. //those immune to push are immune to stun
		dplayermessage PARAM4 $get(ent_me,name) is immune to stun effects.
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if !$get(ent_me,scriptvar,IS_LURE) //do not stun lures
	callevent resist_check stun $get(PARAM4,id)
	if !RESISTED_EFFECT

	setvard local.effect.duration 	PARAM1

	setvard game.effect.movespeed 30%
	setvard game.effect.canjump 0
	setvard game.effect.anim.framerate 0.4

	local slowduration $randf(0.2,local.effect.duration)
	if( PARAM2 equals 1 ) applyeffect ent_me effects/effect_push slowduration (0,0,0) 1
	if( PARAM3 equals 1 ) setvard game.effect.canattack 0

	if ( $get(ent_me,isplayer) )
	{
		playsound CHAN_ITEM 10 $get(ent_me,scriptvar,'PLR_SOUND_LEGHIT1')
		hud.addstatusicon ent_me alpha_stun stun local.effect.duration
	}

	if ( FX_SPAMMERS < MAX_FX_SPAMMERS ) 
	{
		//createnpc effects/sfx_stunring (0,0,0) $get(ent_me,id) local.effect.duration
		//setvard script.stunring $get(ent_lastcreated,id)
		clientevent new all effects/sfx_stunring $get(ent_me,index) local.effect.duration $get(ent_me,height)
	}

	if ( $get(ent_me,isplayer) ) clientevent new ent_me $currentscript local.effect.duration
	setvard local.effect.clientscript game.script.last_sent_id
	callexternal $get(ent_me,id) ext_set_slowed 1
	callevent local.effect.duration effect_die
}

{ game_death
	//deleteent script.stunring	//Delete the ring from here, because skellys stay 'alive' after death
	callevent effect_die
}

{ game_duplicated

	callevent game_death
}

{ effect_die
	callexternal $get(ent_me,id) ext_set_slowed 0
}