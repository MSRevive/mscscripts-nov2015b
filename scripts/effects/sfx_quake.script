{

}

{ client_activate 

	
	setvard FX_SRC PARAM1
	setvard FX_MOBILE PARAM2
	setvard FX_AOE PARAM3
	setvard FX_DURATION PARAM4
	setvard FX_DO_SOUND PARAM5

	setvard FX_ACTIVE 1

	setvard FX_RATIO $math(divide,FX_AOE,1024) 
	setvard FX_ROCKS_PER_CIRC $ratio(FX_RATIO,4,16)
	setvard FX_ROCKS_PER_CIRC $int(FX_ROCKS_PER_CIRC)
	setvard FX_N_CIRCS $ratio(FX_RATIO,2,8)
	setvard FX_N_CIRCS $int(FX_N_CIRCS)

	callevent quake_loop
	callevent FX_DURATION end_fx
}

{ quake_loop
	if FX_ACTIVE
	callevent 1.0 quake_loop
	if ( FX_MOBILE )
	{
		setvard FX_ORIGIN $getcl(FX_SRC,origin)
	}
	else
	{
		setvard FX_ORIGIN FX_SRC
	}
	vectorset FX_ORIGIN z $get_ground_height(FX_ORIGIN)

	setvard CIRC_COUNT 0
	setvard CIRC_STEP $math(divide,FX_AOE,FX_N_CIRCS)
	calleventloop FX_N_CIRCS do_circles
}

{ do_circles
	local CUR_CIRC game.script.iteration
	setvard CIRC_RAD $math(multiply,CIRC_STEP,CUR_CIRC)
	add CIRC_RAD CIRC_STEP
	setvard ROT_COUNT $randf(0,359.99)
	local L_NROCKS $math(divide,CUR_CIRC,FX_N_CIRCS)
	local L_NROCKS $ratio(L_NROCKS,4,FX_ROCKS_PER_CIRC)
	local L_NROCKS $int(L_NROCKS)
	setvard ROT_STEP $math(divide,359.99,L_NROCKS)

	
	

	calleventloop L_NROCKS do_rocks
}

{ do_rocks
	local L_POS FX_ORIGIN
	vectoradd L_POS $relpos($vec(0,ROT_COUNT,0),$vec(0,CIRC_RAD,0))
	cleffect tempent model rockgibs.mdl L_POS setup_rock update_rock

	add ROT_COUNT ROT_STEP
	if ( ROT_COUNT > 359.99 ) subtract ROT_COUNT 359.99
}

{ setup_rock
	cleffect tempent set_current_prop death_delay 3.0
	cleffect tempent set_current_prop scale $randf(1.4,2.0)
	cleffect tempent set_current_prop gravity 	0
	cleffect tempent set_current_prop velocity (0,0,50)
	cleffect tempent set_current_prop collide none
	cleffect tempent set_current_prop framerate 1.0
	cleffect tempent set_current_prop frames 50
	cleffect tempent set_current_prop body $rand(1,2)
	cleffect tempent set_current_prop sequence 0
	cleffect tempent set_current_prop angles $vec(0,RND_YAW,0)
	cleffect tempent set_current_prop rendermode texture
	cleffect tempent set_current_prop renderamt 255
	cleffect tempent set_current_prop fuser1 game.time
}

{ update_rock
	if ( !FX_ACTIVE )
	{
		local L_CUR_VEL game.tempent.velocity
		vectoradd L_CUR_VEL z -100
		if ( $vec.z(L_CUR_VEL) < -600 ) vectorset L_CUR_VEL z -600
		cleffect tempent set_current_prop bouncefactor 1
		cleffect tempent set_current_prop velocity L_CUR_VEL
		cleffect tempent set_current_prop gravity 1
		cleffect tempent set_current_prop collide world
	}

	local CUR_ANG game.tempent.angles
	local L_YAW $vec.yaw(CUR_ANG)
	add L_YAW 1
	if ( L_YAW > 359.99 ) subtract L_YAW 359.99
	cleffect tempent set_current_prop angles $vec(0,L_YAW,0)

	
	local CUR_REND game.time
	subtract CUR_REND game.tempent.fuser1
	local CUR_REND $math(divide,CUR_REND,3)
	local CUR_REND $ratio(CUR_REND,512,0)
	if ( CUR_REND > 255 ) local CUR_REND 255
	
	cleffect tempent set_current_prop renderamt CUR_REND
}

{ end_fx
	setvard FX_ACTIVE 0
	callevent 4.0 remove_fx
}

{ remove_fx
	removescript
}
