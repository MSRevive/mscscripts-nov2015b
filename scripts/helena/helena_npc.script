

#include [server] monsters/debug



{ game_spawn
	callevent 0.1 helena_setup_homepos
}

{ helena_setup_homepos 

	local L_MAP_NAME $lcase(game.map.name)
	if L_MAP_NAME equals helena
	invincible 0
}

{ game_struck

	callevent helena_flee $get(ent_laststruck,id) "struck"
}

{ game_heardsound

	local LAST_HEARD $get(ent_lastheard,id)
	if $get(LAST_HEARD,range) < 128
	if !$get(LAST_HEARD,isplayer) 
	if ( $get(LAST_HEARD,race) equals orc ) callevent helena_flee LAST_HEARD "heard_orc"
	if ( $get(LAST_HEARD,race) equals rogue ) callevent helena_flee LAST_HEARD "heard_bandit"
}

{ helena_flee

	

	roam 1

	setvard SCAREY_GUY PARAM1

	setmoveanim run1
	setmovedest SCAREY_GUY 1024 flee
	callevent 10.0 helena_stopflee
}

{ helena_stopflee

	setmoveanim walk_scared
}

{ helena_raid_go 

	


	hearingsensitivity 4

	setvard RAID_ON 1
	setvard HELENA_RETURNED_HOME 0

	setvard HELENA_SAVED 0
	if ( NPC_HOME_LOC equals 'NPC_HOME_LOC' )
	{
		
		setvard NPC_HOME_LOC game.monster.origin
		setvard NPC_HOME_ANG game.monster.angles
	}
	invincible  0
	setmoveanim walk_scared
	setidleanim crouch_idle

	
	menu.autoopen 0
	setvard STORE_CLOSED 1
}

{ helena_raid_end 

	
	setvard HELENA_TELE_HOME_TIME game.time
	add HELENA_TELE_HOME_TIME 60.0
	if ( STORE_NAME isnot 'STORE_NAME' ) callevent helena_return_home
	if ( STORE_NAME equals 'STORE_NAME' ) callevent helena_made_it_home
}

{ helena_return_home

	setmovedest NPC_HOME_LOC 10
	if ( $dist(game.monster.origin,NPC_HOME_LOC) < 10 )
	{
		callevent helena_made_it_home
		local EXIT_SUB 1
	}
	if ( game.time >= HELENA_TELE_HOME_TIME )
	{
		callevent helena_made_it_home
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	if game.time < HELENA_TELE_HOME_TIME
	if !HELENA_RETURNED_HOME
	local RND_DELAY $randf(4,9)
	callevent RND_DELAY helena_return_home
}

{ helena_made_it_home

	
	
	setvard OVERCHARGE 50
	setvard SELL_RATIO 1.0	
	setvard STORE_CLOSED 0
	setvard HELENA_SAVED 1


	if ( STORE_NAME isnot 'STORE_NAME' )
	{
		setorigin ent_me NPC_HOME_LOC
		local OLD_YAW $vec.yaw(NPC_HOME_ANG)
		setangle face $vec(0,OLD_YAW,0)
		setidleanim idle1
		setmoveanim idle1
		roam 0
		setmovedest none
		npcstore.remove STORE_NAME allitems
		callevent vendor_addstoreitems

		menu.autoopen 1
	}
	else
	{
		setidleanim idle1
		setmoveanim walk
	}

	setvard HELENA_RETURNED_HOME 1

	setvard RAID_ON 0
}

{ basevendor_offerstore

	if HELENA_SAVED
	local RND_SAY $rand(1,3)
	
	if ( RND_SAY == 1 ) saytext Thank you for saving our little town. For you, I'll offer a discount rate.
	if ( RND_SAY == 2 ) saytext For the saviors of Helena, we offer discount rates.
	if ( RND_SAY == 3 ) saytext Thank you again, please, consider everything on discount. 
	say [.56] [.4] [.58] [.66]
}

{ game_struck

	if !DEFAULT_HUMAN 
	callevent call_for_help $get(ent_laststruck,id)
}

{ [override] call_for_help 
	saytextrange 1024
	local RAND_SCREAM $rand(1,4)
	if ( RAND_SCREAM == 1 ) saytext Help! Help!
	if ( RAND_SCREAM == 2 ) saytext Guards! Call the guards!
	if ( RAND_SCREAM == 3 ) saytext Save me!
	if ( RAND_SCREAM == 4 ) saytext Help! Help! I'm being repressed!
	callexternal all civilian_attacked PARAM1 $get(PARAM1,isplayer)
}

{ game_death
	if !DEFAULT_HUMAN
	callexternal all civilian_attacked $get(ent_laststruck,id) $get(ent_laststruck,isplayer)
}
