{
	
	const FINGER_ADJ '$relpos($vec(0,MY_YAW,0),$vec(0,30,54))'
	const SOUND_BEAM weather/Storm_exclamation.wav
	const ANIM_BEAM battleaxe_swing1_L
	setvard FREQ_REPULSE $randf(15,20)
	const DMG_PUSH_BEAM $randf(2,6)

	setvard DROP_GOLD 1			
	setvard DROP_GOLD_AMT $rand(15,30)	
	setvard NPC_GIVE_EXP 220
	
	
	
	setvard ANIM_ATTACK swordswing1_L 
	setvard FLINCH_CHANCE 45%
	
	setvard MOVE_RANGE 256
	setvard ATTACK_RANGE 2000
	const ATTACK_SPEED 300
	const ATTACK_CONE_OF_FIRE 2
	
	const MELE_RANGE 96
	const MELE_HITRANGE 128
	const ATTACK_ACCURACY 80
	const ANIM_SWIPE swordswing1_L
	const ANIM_FIRE swordswing1_L
	const ANIM_WARCRY warcry
	const ANIM_KICK kick
	setvar ANIM_IDLE idle1
	const SWIPE_DAMAGE '$rand(25,65)'
	const SOUND_MELEMISS zombie/claw_miss1.wav
	const SOUND_MELEHIT zombie/claw_strike3.wav

	const SOUND_SHOCK1 debris/zap8.wav
	const SOUND_SHOCK2 debris/zap3.wav
	const SOUND_SHOCK3 debris/zap4.wav

	const SOUND_DEATH_SHOOT magic/spawn.wav
	const SOUND_WARCRY1 monsters/orc/attack1.wav
	const SOUND_WARCRY2 monsters/orc/attack3.wav
	const FROST_BOLT_DAMAGE '$rand(25,60)'
	const FROST_STRIKE_DAMAGE '$rand(10,20)'
	const FROST_BOLT_FREQ 1.5
	const DEATH_SCRIPT monsters/summon/summon_lightning_storm

	setvard BALL_SIZE 3
	setvard BALL_DMG 10
	setvard BALL_TYPE lightning
}

#include monsters/orc_base_ranged
#include monsters/orc_base

{ orc_spawn

	hp 750
	name a|Shadahar Shaman Initiate
	hearingsensitivity 8
	takedmg lightning 0.0
	takedmg poison 2.0
	takedmg acid 2.0
	setstat spellcasting 30 30 30 30 30 30 30 

	setmodel monsters/sorc.mdl

	setmodelbody 0 0 
	setmodelbody 1 4 
	setmodelbody 2 0 
}

{ npc_selectattack

	local NME_RANGE $get(ent_lastseen,range)
	if ( FROST_BOLT_DELAY ) 
	{
		if ( NME_RANGE > MELE_HITRANGE ) setvard ANIM_ATTACK ANIM_FIRE
	}
	if ( !FROST_BOLT_DELAY ) setvard ANIM_ATTACK ANIM_FIRE
	if NME_RANGE < MELE_HITRANGE
	if !REPULSE_DELAY
	setvard REPULSE_DELAY 1
	callevent FREQ_REPULSE reset_repulse_delay
	setmoveanim ANIM_BEAM
	setidleanim ANIM_BEAM
	playanim critical ANIM_BEAM
	setvard BEAM_TARGET NPCATK_TARGET
	setvard AS_ATTACKING game.time
	setvard BEAM_COUNT 0
	callevent npcatk_suspend_ai
	callevent beam_push
}

{ reset_repulse_delay
	setvard REPULSE_DELAY 0
}


{ swing_sword 

	if ( $cansee(NPCATK_TARGET,MELE_RANGE) )
	{
		
		setvard ANIM_ATTACK ANIM_SWIPE
		callevent swipe_attack $get(NPCATK_TARGET,id)
		local EXIT_SUB 1
	}
	if !EXIT_SUB


	
	setvard ANIM_ATTACK ANIM_FIRE
	callevent throw_frostbolt
}

{ throw_frostbolt

	if $cansee(NPCATK_TARGET)
	callevent npcatk_faceattacker NPCATK_TARGET

	callevent FROST_BOLT_FREQ reset_frostbolt
	playrandomsound 0 10 SOUND_SHOCK1 SOUND_SHOCK2 SOUND_SHOCK3

	local FINAL_DAMAGE FROST_BOLT_DAMAGE
	if ( EXT_DAMAGE_ADJUSTMENT isnot 'EXT_DAMAGE_ADJUSTMENT' ) add FINAL_DAMAGE EXT_DAMAGE_ADJUSTMENT 
	tossprojectile NPCATK_TARGET ATTACK_SPEED FINAL_DAMAGE ATTACK_CONE_OF_FIRE proj_lightning_ball (0,48,48)
	setvard FROST_BOLT_DELAY 1
}

{ reset_frostbolt
	setvard ANIM_ATTACK ANIM_FIRE
	setvard DID_WARCRY 0
	setvard FROST_BOLT_DELAY 0
}

{ npc_death

	setsolid none
	setbbox none
	local BALL_DEST $relpos(0,0,2000)
	playsound 0 10 SOUND_DEATH_SHOOT
	local STORM_POS game.monster.origin
	vectorset STORM_POS z $get_ground_height(game.monster.origin)
	createnpc DEATH_SCRIPT STORM_POS $get(ent_me,id) $get(ent_me,angles.y) 40.0 15.0
}

{ swipe_attack 

	setvard SWIPE_SOUNDS 1
	local FINAL_DAMAGE SWIPE_DAMAGE
	if ( EXT_DAMAGE_ADJUSTMENT isnot 'EXT_DAMAGE_ADJUSTMENT' ) add FINAL_DAMAGE EXT_DAMAGE_ADJUSTMENT 
	if $get(ent_laststruckbyme,range) <= MELE_HITRANGE
	callevent npcatk_dodamage PARAM1 MELE_HITRANGE FINAL_DAMAGE ATTACK_ACCURACY
	if $rand(1,5) == 1
	applyeffect PARAM1 effects/effect_shock_dmg $rand(5,10) ent_me FROST_STRIKE_DAMAGE
}

{ game_dodamage 

	if SWIPE_SOUNDS

	if ( !PARAM1 ) playsound 0 10 SOUND_MELEMISS

	if ( PARAM1 ) playsound 0 10 SOUND_MELEHIT

	setvard SWIPE_SOUNDS 0
}

{ npc_struck

	if $get(ent_laststruck,range) < MELE_RANGE
	
	callevent npcatk_settarget $get(ent_laststruck,id)
	
}


{ beam_push

	add BEAM_COUNT 1
	if ( BEAM_COUNT == 30 )
	{
		callevent npcatk_resume_ai
		setidleanim ANIM_IDLE
		setmoveanim ANIM_RUN
		setvard BEAM_ON 0
	}
	if BEAM_COUNT < 30
	callevent 0.1 beam_push
	if ( !$get(BEAM_TARGET,isalive) ) setvard BEAM_COUNT 29
	if $get(BEAM_TARGET,isalive)
	callevent npcatk_faceattacker BEAM_TARGET
	local BEAM_START game.monster.origin
	local MY_YAW $vec.yaw(game.monster.angles)
	vectoradd BEAM_START FINGER_ADJ 
	local BEAM_END $get(BEAM_TARGET,origin)
	
	local BEAM_TRACE $get_traceline(BEAM_START,BEAM_END,worldonly)
	if BEAM_TRACE equals BEAM_END

	playsound 0 10 SOUND_BEAM
	effect beam end lgtning.spr 30 BEAM_START BEAM_TARGET 0 (200,255,50) 200 30 1.0

	local MY_YAW $vec.yaw(game.monster.angles)

	add MY_YAW BEAM_COUNT
	if ( MY_YAW > 359 ) subtract MY_YAW 359

	local VEL_SET $relpos($vec(0,MY_YAW,0),$vec(500,1000,30))

	setvelocity BEAM_TARGET VEL_SET

	dodamage BEAM_TARGET direct DMG_PUSH_BEAM 100% $get(ent_me,id) lightning
}

{ npcatk_hunt
	if NPCATK_TARGET isnot unset
	if ( !$cansee(NPCATK_TARGET) ) setvard ATTACK_MOVERANGE game.monster.moveprox
	if ( $cansee(NPCATK_TARGET) ) setvard ATTACK_MOVERANGE ATTACK_RANGE
}

{ set_turret
	setvard AM_TURRET 1
	setmoveanim ANIM_IDLE
	setvard ANIM_RUN ANIM_IDLE
	setvard ANIM_WALK ANIM_IDLE
	setvard NO_STUCK_CHECKS 1
	roam 0
}
{ npc_post_spawn
	
	if AM_TURRET
	setmoveanim ANIM_IDLE
	setvard ANIM_RUN ANIM_IDLE
	setvard ANIM_WALK ANIM_IDLE
	setvard NO_STUCK_CHECKS 1
	roam 0
}
