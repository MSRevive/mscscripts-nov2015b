{
	const LIGHTNING_SKELE 1

	const GOLD_BAGS 1
	const GOLD_BAGS_PPLAYER 1
	const GOLD_PER_BAG 50
	const GOLD_RADIUS 64
	const GOLD_MAX_BAGS 4

	setvard NPC_GIVE_EXP 300

	const SOUND_SHOCK1 debris/zap8.wav
	const SOUND_SHOCK2 debris/zap3.wav
	const SOUND_SHOCK3 debris/zap4.wav

	const ANIM_CIRCLE castspell

	const DMG_CHAIN $randf(40,60)
	setvar ANIM_RUN run
	const FINGER_ADJ '$relpos($vec(0,MY_YAW,0),$vec(0,0,54))'
	const DMG_SHOCK 10.0

	const CIRCLE_FREQ $randf(10,15)

	
	precache doors/aliendoor3.wav
	precache magic/spawn.wav
}

#include mscave/Shadahar

{ [override] skeleton_spawn

	name a|Lightning Forged Skeleton
	race undead
	roam 1
	takedmg all .7
	setmodel monsters/skeleton_enraged.mdl
	hearingsensitivity 8
	setmodelbody	0 6 
	setmodelbody	1 8 

	if ( $lcase(game.map.name) equals thanatos ) setvard SET_GREEK 1
	if ( SET_GREEK ) setmodelbody 0 10

	setmoveanim sitstand
	setidleanim sitstand

	takedmg lightning 0.0

	local MY_SKY game.monster.origin
	vectoradd MY_SKY (0,0,4096)
	local MY_CENTER $get(ent_me,origin)
	vectoradd MY_CENTER $vec(0,0,-48)
	
	
	
	effect beam point lgtning.spr 120 MY_CENTER MY_SKY (128,64,255) 200 3.0 1.0
	callevent 10.0 circle_check
	callevent 2.0 stand_complete
}

{ npc_post_spawn
	
	effect beam vector lgtning.spr 30 $get(ent_me,origin) $get(ent_me,origin) (200,255,50) 0 30 -1
	setvard BEAM_ID $get(ent_lastcreated,id)
}

{ game_death
	effect beam update BEAM_ID remove 0
}

{ stand_complete
	setidleanim idle1
	if !CYCLED_UP
	setmoveanim ANIM_WALK
}

{ resume_killin
	callevent npcatk_resume_ai
	setidleanim ANIM_IDLE
	setmoveanim ANIM_RUN
	setmodelbody 1 8
}

{ game_dodamage 

	
	effect beam update BEAM_ID start_origin $get(ent_me,attachpos,0)
	effect beam update BEAM_ID end_origin PARAM4

	if ( CHAIN_ON )
	{
		effect beam update BEAM_ID brightness 200
	}
	else
	{
		effect beam update BEAM_ID brightness 0
	}

	if PARAM1

	if ( CHAIN_ON )
	{
		if PARAM1
		local BEAM_START game.monster.origin
		local MY_YAW $vec.yaw(game.monster.angles)
		vectoradd BEAM_START FINGER_ADJ 
		local BEAM_END $get(PARAM2,origin)

		
		
		effect beam update BEAM_ID brightness 200
		applyeffect PARAM2 effects/effect_shock_dmg 3.0 $get(ent_me,id) DMG_SHOCK
		playrandomsound 0 10 SOUND_SHOCK1 SOUND_SHOCK2 SOUND_SHOCK3	
		if ( !$get(PARAM2,scriptvar,IMMUNE_PUSH) ) addvelocity PARAM2 $vec(0,-105,0)
	}
}

{ [override] circle_check

	local SEE_NME $cansee(enemy)
	local SHOCK_BUSY $get(G_SHOCKER,isalive)

	if ( !SEE_NME ) local CHECK_LATER 1
	if ( SHOCK_BUSY ) local CHECK_LATER 1
	if ( I_R_FROZEN ) local CHECK_LATER 1
	if ( !CYCLED_UP ) local CHECK_LATER 1
	if ( game.time < G_NEXT_SHOCK ) local  CHECK_LATER 1

	if ( CHECK_LATER ) callevent $randf(5,8) circle_check
	if !CHECK_LATER
	callevent CIRCLE_FREQ circle_check
	setvarg G_SHOCKER $get(ent_me,id)
	callevent circle_prep
}

{ [override] circle_prep
	callevent npcatk_suspend_ai
	setmoveanim ANIM_CIRCLE
	setidleanim ANIM_CIRCLE

	setvard TARGET_LIST unset
	playanim critical ANIM_CIRCLE
	createnpc monsters/summon/barrier $relpos(0,0,0) $get(ent_me,id) 96 1 0 1 0 0 4.0
	setvard BARRIER_ID $get(ent_lastcreated,id)
	playsound 0 10 SOUND_WARCRY
	callevent 0.5 circle_prep2
}

{ circle_prep2
	setvard TARGET_LIST ''
	setvard TARGET_LIST $get_tsphere(enemy,1024,game.monster.origin)

	dodamage $relpos(0,0,0) 1024 DMG_CHAIN 100% 0.0 reflective lightning
	callevent 0.1 stop_gathering_targets
	setmodelbody 1 9
}

{ stop_gathering_targets
	setvard GATHER_TARGETS 0
	setvard ZAP_TARGETS 1
	callevent 3.0 stop_zapping
	setvard CUR_TARGET 0
	setvard CHAIN_ON 1
	callevent zap_loop

}

{ zap_loop

	playanim once ANIM_CIRCLE
	if ZAP_TARGETS
	callevent 0.1 zap_loop
	setvard TARGET_LIST ''
	setvard TARGET_LIST $get_tsphere(enemy,1024)
	if ( TARGET_LIST isnot none )
	{
		add CUR_TARGET 1
		if ( CUR_TARGET > $get_token_amt(TARGET_LIST) ) setvard CUR_TARGET 0
		local BEAM_TARGET $get_token(TARGET_LIST,CUR_TARGET)
		if $get(BEAM_TARGET,isalive)
		setmovedest BEAM_TARGET 9999
		dodamage BEAM_TARGET 1024 DMG_CHAIN 100% lightning
	}
	
}

{ stop_zapping
	setvard ZAP_TARGETS 0
	setvard CHAIN_ON 0
	effect beam update BEAM_ID brightness 0
	callexternal BARRIER_ID remove_barrier
	callevent 1.0 resume_killin
	setvarg G_SHOCKER unset
	setvarg G_NEXT_SHOCK game.time
	add G_NEXT_SHOCK 10.0
	setmodelbody 1 8

	setmoveanim ANIM_RUN
	setidleanim ANIM_IDLE
}


