#scope server
//Skeleton base

//Special ability #1
//SKEL_RESPAWN_CHANCE - Chance the skeleton will come back to life
//SKEL_RESPAWN_LIVES - Max number of times the skeleton can come back

{
   const RE_FLEE_MAX 	25

	setvard AM_SKELETON 1

  const ANIM_RESPAWN_DEADIDLE dead_on_stomach

   setvar ANIM_IDLE 	idle1

   setvard MOVE_RANGE 	32
   setvard ATTACK_RANGE 	64
   setvard ATTACK_HITRANGE 127

   const ANIM_DEAD dead_on_back


	const IS_SKELETON 1

    setvard ANIM_ATTACK 	attack1
	setvard ANIM_DEATH dieheadshot

	const NPC_HANDLES_SUMMON_CIRCLES 1

   const SOUND_STRUCK1 	weapons/cbar_hitbod1.wav
   const SOUND_STRUCK2 	weapons/cbar_hitbod2.wav
   const SOUND_STRUCK3 	weapons/cbar_hitbod3.wav
   const SOUND_STRUCK4 	zombie/zo_pain2.wav
   const SOUND_STRUCK5 	zombie/zo_pain2.wav
   const SOUND_ATTACK1 	zombie/claw_miss1.wav
   const SOUND_ATTACK2 	zombie/claw_miss2.wav
   const SOUND_NULL null.wav
   setvard NPC_ALT_SOUND_DEATH 	zombie/zo_pain1.wav

	precache SOUND_DEATH
	precache SOUND_NULL

   setvard HUNT_AGRO 1		//base_npc_attack - Premptive attack
   setvard RETALIATE_CHANCE 75%   //base_npc_attack - Automatically switch targets to the last damaging player
   setvard CAN_FLEE 0		//base_npc_attack - Whether I run away at low health

   const SOUND_TURNED1		ambience/the_horror1.wav
   const SOUND_TURNED2		ambience/the_horror2.wav
   const SOUND_TURNED3		ambience/the_horror3.wav
   const SOUND_TURNED4		ambience/the_horror4.wav
   const SOUND_HOLY_STRIKE	doors/aliendoor1.wav

   precache SOUND_TURNED1
   precache SOUND_TURNED2
   precache SOUND_TURNED3
   precache SOUND_TURNED4
   precache SOUND_HOLY_STRIKE

	precache bonegibs.mdl
}

#include monsters/base_monster

{  npc_spawn

	local SLEEPER_TYPE $get_by_name(skels_sleep)
	local SLEEPER_ID $get(SLEEPER_TYPE,id)
	if ( $get(SLEEPER_ID,isalive) )
	{
		setvard SLEEPER 1
		//dbg skels_sleep detected, set to sleep - will not awaken until player comes near
	}

	local SLEEPER_TYPE $get_by_name(skels_deep_sleep)
	local SLEEPER_ID $get(SLEEPER_TYPE,id)
	if ( $get(SLEEPER_ID,isalive) )
	{
		setvard SLEEPER 1
		setvard DEEP_SLEEPER 1
		//dbg skels_deep_sleep detected, will not awaken until monsters/skels_wakeup is spawned or ms_npcscript skeleton_wakeup_call caled against me
	}

   if ( ANIM_WALK equals 'ANIM_WALK' ) setvar ANIM_WALK walk
   if ( ANIM_RUN equals 'ANIM_RUN' ) setvar ANIM_RUN walk


   	setmodel monsters/skeleton.mdl
	blood none
   	callevent skeleton_spawn
   	hp SKEL_HP
	if ( !CUSTOM_SIZE )
	{
	   	width 32
	   	height 80
	}
   	roam 1
	setvard STRUCK_HOLY 0
	setvard FRUSTRATED 0
	callevent 0.1 setup_body

	if ( SLEEPER )
	{
		callevent make_sleeper
	}
	if ( !DEEP_SLEEPER ) hearingsensitivity 10


	if !SLEEPER
   	race undead
   	setmoveanim ANIM_WALK
	callevent skeleton_attribs

	hearingsensitivity 4
}

{ skeleton_attribs

	//if !TAKE_DMG_SET
	//setvard TAKE_DMG_SET 1

	if ( SET_GREEK )
	{
		setmodelbody 0 10
	}

	setvard IMMUNE_POISON 1

	if ( !STONE_SKELETON )
	{
		takedmg slash .7
		takedmg pierce .5
		takedmg blunt 1.2
		takedmg fire 1.5
		takedmg holy 3.0
		takedmg cold 0.1
		takedmg poison 0.0
	}

	if ( STONE_SKELETON )
	{
		takedmg holy 2.0
		takedmg poison 0.0
		takedmg cold 0.1
	}
}

{ hide_name
	//hide name of sleepers
	setvard MY_NAME game.monster.name.full
	name ''
}

{  attack_1	// Deal damage

   	callevent attack_snd
   	dodamage ENTITY_ENEMY ATTACK_HITRANGE $randf(ATTACK_DAMAGE_LOW,ATTACK_DAMAGE_HIGH) ATTACK_HITCHANCE
}
   
{  npc_struck	//Hit by someone

	if !STAY_DEAD_DAMNIT
   	if ( !STRUCK_HOLY ) playrandomsound game.sound.body game.sound.maxvol SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3 SOUND_STRUCK4 SOUND_STRUCK5
	if ( STRUCK_HOLY ) playrandomsound game.sound.body game.sound.maxvol SOUND_HOLY_STRIKE
	setvard STRUCK_HOLY 0
}
   
{  attack_snd	//Attack grunts
   
   	playrandomsound game.sound.weapon 5 SOUND_ATTACK1 SOUND_ATTACK2 none none
}

{ npc_death

	local L_DEATHANIM $rand(0,4)
	setvard ANIM_DEATH dieheadshot
	if( LCL_DEATHANIM == 1 ) setvard ANIM_DEATH dieheadshot2
	if( LCL_DEATHANIM == 2 ) setvard ANIM_DEATH diesimple
	if( LCL_DEATHANIM == 3 ) setvard ANIM_DEATH diebackward
	if( LCL_DEATHANIM == 4 ) setvard ANIM_DEATH dieforward

	setmovedest none
	setidleanim ANIM_RESPAWN_DEADIDLE
	setmoveanim ANIM_RESPAWN_DEADIDLE

	setvard PLAYING_DEAD 1

	if ( HIT_BY_HOLY ) 
	{
		setvard STAY_DEAD_DAMNIT 1
		callevent really_dead
		callevent gib_me
	}
	if !HIT_BY_HOLY

	local GETUP_ROLL $rand(0,99)
	if ( HIT_BY_HOLY ) 
	{
		setvard STAY_DEAD_DAMNIT 1
		local GETUP_ROLL 1000
	}

	if GETUP_ROLL < SKEL_RESPAWN_CHANCE
	if SKEL_RESPAWN_TIMES < SKEL_RESPAWN_LIVES
	//if !IS_FLEEING	//skeletons killed with Holy Light do NOT get back up

	//dbg temp Begin Feigning ( GETUP_ROLL vs SKEL_RESPAWN_CHANCE )

	local L_DEATHANIM $rand(0,1)

	setvard ANIM_DEATH diesimple
	if( LCL_DEATHANIM == 1 ) setvard ANIM_DEATH dieforward

	if ( !HIT_BY_HOLY ) setalive 1
	//setsolid 		none
	setmovedest 		none
	setidleanim 		ANIM_RESPAWN_DEADIDLE
	setmoveanim			ANIM_RESPAWN_DEADIDLE
	invincible		1
	roam			0
	setvard NO_STUCK_CHECKS 1
	//setvard CAN_ATTACK 	0
	//setvard CAN_HUNT 	0
	setvard CAN_RETALIATE	0
	setvard CAN_HEAR	0
	setvard PLAYING_DEAD	1
	setvard CANT_TURN 1
	setvard CAN_FLEE 0
	setvard CANT_FLEE 1
	setvard FRUSTRATED 0
	setvard NPC_STUCK_CHECKS 0
	setvard DOWN_TIME $rand(5,15)
	setvard DOWN_TIME_COUNTER 0

	callevent skel_check_respawn
	callevent skel_death
}

{ skel_check_respawn

	setmovedest 		none
	setidleanim 		ANIM_RESPAWN_DEADIDLE
	setmoveanim			ANIM_RESPAWN_DEADIDLE

	if ( HIT_BY_HOLY ) callevent really_dead

	if PLAYING_DEAD
	add DOWN_TIME_COUNTER 1

	if ( DOWN_TIME_COUNTER < DOWN_TIME ) callevent 0.2 skel_check_respawn
	else callevent skel_respawn
}

{ skel_respawn

	playanim critical getup
	setidleanim	ANIM_IDLE
	setvard RE_FLEE_DELAY RE_FLEE_MAX
	incvar SKEL_RESPAWN_TIMES 1
   	hp SKEL_HP
	if ( NPC_HP_MULTI > 0 )
	{
		setvard NPC_MAXHP $get(ent_me,maxhp)
		multiply NPC_MAXHP NPC_HP_MULTI
		hp NPC_MAXHP
	}

	callevent 1.5 revived_skel
}

{ revived_skel

	setvard PLAYING_DEAD 0
	setvard CAN_FLEE 0
	setvard CANT_FLEE 1
	setvard CANT_TURN 0
	setmovedest	HUNT_LASTTARGET 9999
	setmoveanim	ANIM_WALK
	setidleanim ANIM_IDLE
	roam 			1
	invincible		0
	setvard CAN_ATTACK 	1
	setvard CAN_HUNT 	1
	setvard CAN_RETALIATE	1	
	setvard CAN_HEAR	1
	local TOTAL_XP NPC_GIVE_EXP
	divide TOTAL_XP SKEL_RESPAWN_TIMES
	local TOTAL_XP $int(TOTAL_XP)
	//dbg temp reset XP to TOTAL_XP
	skilllevel TOTAL_XP //if I don't reset my XP, it doubles every kill
	callevent 2.0 skel_resume_stuck_Checks
}

{ skel_resume_stuck_Checks

	callevent npcatk_target RESTORE_TARGET "killed_me_sorta"
	setvard NPC_STUCK_CHECKS 1
}

{ stay_down_damnit

	//The stupid HIT_BY_HOLY vard is getting reset by the fake death
	//so I have to loop this event to make sure skeletons struck by holy stay the f*ck down

	if ( PLAYING_DEAD )
	{
		setmovedest 		none
		setidleanim 		ANIM_RESPAWN_DEADIDLE
		setmoveanim			ANIM_RESPAWN_DEADIDLE
	}

	setvard HIT_BY_HOLY 1
	////dbg StayDownDamnit HIT_BY_HOLY
	callevent 1.0 stay_down_damnit
}

{ turn_undead
 
	//PARAM1 - incoming Holy damage
	//PARAM2 - exorcist

	if $get(ent_me,isalive)
	if !PLAYING_DEAD

	callevent stay_down_damnit

	setvard SKEL_RESPAWN_TIMES 99

	setvard HIT_BY_HOLY 1 //this is not sticking due to the way it dies

	local INC_HOLY_DMG PARAM1
	setvard THE_EXCORCIST PARAM2
	local EXCORCIST_FAITH $get(THE_EXCORCIST,skill.spellcasting.divination)
	local TURN_STRENGTH EXCORCIST_FAITH
	local MY_MAX_HP $get(ent_me,maxhp)
	local ME_ME $get(ent_me,id)

	effect glow ent_me (255,255,0) 512 1 1

	//Stop skeletons killed by holy light from getting back up
	local MY_CURRENT_HP $get(ent_me,hp)
	subtract MY_CURRENT_HP INC_HOLY_DMG
	if ( MY_CURRENT_HP <= 0 ) setvard IS_FLEEING 1

	setvard STRUCK_HOLY 1	//this reserves the sound

	xdodamage $get(ent_me,id) direct INC_HOLY_DMG 100 THE_EXCORCIST THE_EXCORCIST spellcasting.divination holy

	multiply TURN_STRENGTH 7

	If ( TURN_STRENGTH > 1000 ) local TURN_STRENGTH 1000 	//cap at 1000 (no turning bosses)
	
	////dbg Got-Dmg INC_HOLY_DMG Strength TURN_STRENGTH MyHP MY_CURRENT_HP

	if !IS_FLEEING			//can't turn already turned

	if TURN_STRENGTH > MY_CURRENT_HP

	local TURN_RESISTANCE MY_MAX_HP
	divide TURN_RESISTANCE 25
	local TURN_RESISTANCE $int(TURN_RESISTANCE)

	if ( TURN_RESISTANCE < 2 ) local TURN_RESISTANCE 2

	local TURNCHANCE $rand(1,TURN_RESISTANCE)

	//dbg MyTurnResistance TURN_RESISTANCE mymaxhp MY_MAX_HP Rolled TURNCHANCE
	if TURNCHANCE == 1

	//Minimum Turn Duration 5 seconds, max 20
	setvard TURN_DURATION $get(THE_EXCORCIST,skill.spellcasting.divination)
	if ( TURN_DURATION < 5 ) local TURN_DURATION 5
	if ( TURN_DURATION > 15 ) local TURN_DURATION 15

	if !STAY_DEAD_DAMNIT

	//Thothie - RUN AWAY!
	setvard FLEE_DISTANCE 2048

	volume 10
	playrandomsound 2 SOUND_TURNED1 SOUND_TURNED2 SOUND_TURNED3 SOUND_TURNED4

	//setvard CAN_ATTACK 0
	//setvard CAN_HUNT 0
	//setvard HUNT_AGRO 0
	callevent 0.5 flee_from_holy
}

{ flee_from_holy
	dbg bslele:flee_from_holy PLAYING_DEAD STAY_DEAD_DAMNIT
	if !PLAYING_DEAD
	//if !STAY_DEAD_DAMNIT
	setvard CAN_FLEE 1
	setvard PURE_FLEE 1
	callevent npcatk_flee THE_EXCORCIST FLEE_DISTANCE TURN_DURATION
}

{ setup_body

	//setmodelbody based on name (easier than going through each one, then screwing it up and having to repeat)
	//bodies
	//0 - old default
	//1 - bloody
	//2 - green M
	//3 - black armor
	//4 - white armor
	//5 - white brest, black hat
	//6 - one-eye
	//7 - frosted
	//8 - Wassup-G

	//weapons
	//0 - Unarmed
	//1 - bone blade
	//2 - double axes
	//3 - double shields
	//4 - sword

	local MY_NAME $get(ent_me,name)
	local SKEL_NAME "Skeleton Warrior"	 	//0 0	monsters/skeleton
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 0
		setmodelbody 1 0
	}
	local SKEL_NAME "Walking Ashes"	 		//6 0	monsters/skeleton2
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 6
		setmodelbody 1 0
	}
	local SKEL_NAME "Fragile Knight"	 	//1 4	monsters/tcadaver
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 5
		setmodelbody 1 4
	}
	local SKEL_NAME "Ghastly Knight"	 	//3 4	monsters/cadaver
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 3
		setmodelbody 1 4
	}
	local SKEL_NAME "Hungry Skeleton"	 	//0 1	monsters/hunger
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 0
		setmodelbody 1 1
	}
	local SKEL_NAME "Enraged Skeleton"  	//1 2	calruin/corpse2
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 6
		setmodelbody 1 2
	}
	local SKEL_NAME "Awakened Guardian" 	//3 3	calruin/corpse
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 3
		setmodelbody 1 3
	}
	local SKEL_NAME "Living Dead"			//6 2	calruin/livingdead
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 1
		setmodelbody 1 0
	}
	local SKEL_NAME "Fallen Knight"			//4 4	calruin/undeadwarrior	
	if ( MY_NAME equals SKEL_NAME )
	{
		setmodelbody 0 4
		setmodelbody 1 4
	}
}

{ really_dead

	if ( IS_FLEEING ) callevent npcatk_stopflee
	playanim critical ANIM_RESPAWN_DEADIDLE
	setvard STAY_DEAD_DAMNIT 1
	setvard NO_STUCK_CHECKS 1
	setvard CANT_FLEE 1
	setvard PLAYING_DEAD 0
	setvard STAY_DEAD_DAMNIT 1
	setsolid none
	invincible 0
	race hated
	setmovedest none
	setidleanim ANIM_RESPAWN_DEADIDLE
	setmoveanim ANIM_RESPAWN_DEADIDLE
	setvard ANIM_DEATH ANIM_RESPAWN_DEADIDLE
	setvard NPC_ALT_SOUND_DEATH SOUND_NULL
	callevent 0.1 npc_suicide
}

{ cycle_up

	setvard RESTORE_TARGET ENTITY_ENEMY
}

{ skeleton_wakeup_call

	if SLEEPER
	name MY_NAME
   	race undead

	local AWAKE_DELAY 1.5 //give time to play getup anim
	if ( STONE_SKELETON )
	{
		setmoveanim ANIM_WALK
		setidleanim ANIM_IDLE
		callevent animate_stone
		local AWAKE_DELAY 0.1 //stone skeles pose as statues and do not play getup anim
	}
	if ( BASE_FRAMERATE equals 'BASE_FRAMERATE' ) setanim.framerate 1.0
	if ( BASE_FRAMERATE isnot 'BASE_FRAMERATE' ) setanim.framerate BASE_FRAMERATE


	if ( !STONE_SKELETON )
	{
		setmoveanim ANIM_WALK
		setidleanim ANIM_IDLE
		playanim once getup
	}
	callevent AWAKE_DELAY skeleton_awake
}

{ skeleton_awake

	invincible 0
	roam 1
	callevent 0.1 skeleton_attribs
	setvard SLEEPER 0
	setvard DEEP_SLEEPER 0
	setvard PLAYING_DEAD 0
	hearingsensitivity 10
	setmoveanim ANIM_WALK
	setidleanim ANIM_IDLE
	setvard NO_STUCK_CHECKS 0
	callevent npcatk_resume_ai
}

{ game_heardsound

	if SLEEPER
	if !DEEP_SLEEPER
	if $get(ent_lastheard,isplayer)
	if $get(ent_lastheard,range) < 128
	callevent skeleton_wakeup_call
}

{ gib_me
	callexternal GAME_MASTER gm_fade $get(ent_me,id)
	//			<modelname/spritename> 	<position> 		<size> 	<velocity> 	<randomness> 	<amount> 	<duration>
	//effect tempent gibs 	bonegibs.mdl 			$relpos(0,0,10) 1.0 	(0,0,100) 	10 				4 			2.0
}

{ make_sleeper

		setvard SLEEPER 1 //this is redundant unless called via params

		setanim.framerate 0.0
		callevent 0.2 hide_name
		roam 0
		setvard PLAYING_DEAD 1
		if ( !STONE_SKELETON )
		{
			setmoveanim ANIM_RESPAWN_DEADIDLE
		 	setidleanim ANIM_RESPAWN_DEADIDLE
		}
		if ( STONE_SKELETON )
		{
			setmoveanim ANIM_IDLE
		 	setidleanim ANIM_IDLE
		}
		invincible 1
		hearingsensitivity 0
		callevent npcatk_suspend_ai
		setvard NO_STUCK_CHECKS 1
		if ( !DEEP_SLEEPER ) hearingsensitivity 10
}

{ make_deep_sleeper

		setvard SLEEPER 1
		setvard DEEP_SLEEPER 1

		setanim.framerate 0.0
		callevent 0.2 hide_name
		roam 0
		setvard PLAYING_DEAD 1
		if ( !STONE_SKELETON )
		{
			setmoveanim ANIM_RESPAWN_DEADIDLE
		 	setidleanim ANIM_RESPAWN_DEADIDLE
		}
		if ( STONE_SKELETON )
		{
			setmoveanim ANIM_IDLE
		 	setidleanim ANIM_IDLE
		}
		invincible 1
		hearingsensitivity 0
		callevent npcatk_suspend_ai
		setvard NO_STUCK_CHECKS 1
		if ( !DEEP_SLEEPER ) hearingsensitivity 10
}

{ go_greek
	setmodelbody 0 10
	setvard SET_GREEK 1
}

{ set_summon_circle2
	if ( ANIM_RUN equals run )
	{
		playanim critical sitstand
	}
	else
	{
		playanim critical getup
	}
}