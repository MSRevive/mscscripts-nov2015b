

{
	
	const MODEL_HANDS misc/p_misc.mdl
	const MODEL_WORLD misc/p_misc.mdl
	
	const MODEL_VIEW none

	const ARMOR_HUMAN_FEMALE_OFS_ADJ 20 

	
	const NEW_ARMOR_MODEL armor/p_armorvest_new.mdl
}

#include items/base_miscitem
#include items/base_effect_armor

{  game_spawn

	setworldmodel MODEL_WORLD
	sethudsprite hand armor
	sethudsprite trade leather

	sethand any

	setvard ARMOR_TYPE BARMOR_TYPE
	setvard ARMOR_PROTECTION 0
	setvard ARMOR_PROTECTION_AREA BARMOR_PROTECTION_AREA
	setvard ARMOR_REPLACE_BODYPARTS BARMOR_PROTECTION_AREA

	registerarmor


	
	callevent hide_body_parts

	if game.serverside
	setvard L_PERC_TO_FLOAT BARMOR_PROTECTION
	multiply L_PERC_TO_FLOAT 0.01
	setvard DMG_REDUCT 1
	subtract DMG_REDUCT L_PERC_TO_FLOAT
}

{ hide_body_parts
	
	
	callexternal ent_owner ext_setbodytype BARMOR_TYPE $get(ent_me,id)
}

{ barmor_effect_activate
	callexternal ent_owner ext_register_armor $get(ent_me,id)
}

{ show_body_parts
	
	if ( $get(ent_owner,scriptvar,'PLR_LAST_WORN_ARMOR') equals $get(ent_me,id) ) callexternal ent_owner ext_setbodytype normal remove
	callexternal ent_owner ext_register_armor none remove
}

{ game_deploy

	setviewmodel  MODEL_VIEW
	setmodel MODEL_HANDS

	local L_SUBMODEL 16
	add L_SUBMODEL game.item.hand_index
	setmodelbody 0 L_SUBMODEL
}

{ game_wear

	local L_RACE PARAM1
	local L_GENDER PARAM2
	local L_DEBUG PARAM3

	callevent barmor_update_vest L_RACE L_GENDER L_DEBUG


	local L_ARMOR_TEXT ARMOR_TEXT
	stradd L_ARMOR_TEXT " ("
	stradd L_ARMOR_TEXT BARMOR_PROTECTION
	stradd L_ARMOR_TEXT "%)"
	playermessagecl L_ARMOR_TEXT

	if game.serverside
	
	callevent hide_body_parts

	if $get(ent_owner,stat.strength) < ARMOR_STR_REQ
	callevent 0.1 failed_str_req_loop
}

{ game_show
	local L_RACE PARAM1
	local L_GENDER PARAM2
	local L_DEBUG PARAM3

	callevent barmor_update_vest L_RACE L_GENDER L_DEBUG
}

{ barmor_update_vest 
	setmodel NEW_ARMOR_MODEL
	setmodelbody 0 NEW_ARMOR_OFS

	if ( game.clientside )
	{
		local OWNER_RACE $get_local_prop(race)
		local OWNER_GENDER $get_local_prop(gender)
	}
	else
	{
		if game.serverside
		local OWNER_RACE $get(ent_owner,race)
		local OWNER_GENDER $get(ent_owner,gender)
	}

	local OWNER_RACE $lcase(OWNER_RACE)
	local OWNER_GENDER $lcase(OWNER_GENDER)
	if ( OWNER_RACE equals 0 ) local OWNER_RACE PARAM1 
	if ( OWNER_GENDER equals 0 ) local OWNER_GENDER PARAM2 



	if ( OWNER_GENDER equals female )
	{
		if ( OWNER_RACE equals human )
		{
		
			local ARMOR_GROUP 0

			if ( ARMOR_BODY_HUMAN_FEMALE isnot 'ARMOR_BODY_HUMAN_FEMALE' )
			{
				setmodelbody ARMOR_GROUP ARMOR_BODY_HUMAN_FEMALE
			}
			else
			{
				local FEM_BODY NEW_ARMOR_OFS
				add FEM_BODY ARMOR_HUMAN_FEMALE_OFS_ADJ
				setmodelbody ARMOR_GROUP FEM_BODY
			}
		}
	}

	if ( $get(ent_owner,scriptvar,'PLR_HIDE_ARMOR') ) setmodelbody ARMOR_GROUP 0

}

{ ext_set_armor 
	setmodelbody PARAM1 PARAM2
}

{ barmor_effect_remove
	if $get(ent_owner,scriptvar,'PLR_BODY_TYPE') isnot normal
	
	callevent show_body_parts
}

{ barmor_effect_activate
	callexternal ent_owner ext_setbodytype BARMOR_TYPE $get(ent_me,id)
}

{ failed_str_req_loop
	if $get(ent_me,is_worn)
	if $get(ent_owner,stat.strength) < ARMOR_STR_REQ
	callevent 10.0 failed_str_req_loop
	local ALRT_STR "You are too weak to move freely in this armor. (Min Strength "
	stradd ALRT_STR ARMOR_STR_REQ
	stradd ALRT_STR ")"
	infomsg ent_owner "Insufficient Strength for Armor" ALRT_STR
	applyeffectstack ent_owner effects/effect_slow 10.0 50% $get(ent_me,id) 
}

{ ext_activate_items
	if PARAM1 equals $get(ent_owner,id)
	if $get(ent_owner,stat.strength) < ARMOR_STR_REQ
	callevent 0.1 failed_str_req_loop
}

{ game_takedamage 

	if $get(ent_me,is_worn)

	local DMG_AMT PARAM3

	
	if ( $get(ent_owner,scriptvar,'PLR_SPIDER_PROT') )
	{
		if ( $get(PARAM1,race) equals spider ) local IS_SPIDER 1
		if ( $get(PARAM2,race) equals spider ) local IS_SPIDER 1
		local L_NAME $get(PARAM1,name)
		local L_NAME $lcase(L_NAME)
		if ( L_NAME contains spider ) local IS_SPIDER 1
		if ( $get(PARAM1,itemname) contains spid ) local IS_SPIDER 1
		if IS_SPIDER
		local DMG_RED_AMT $get(ent_owner,scriptvar,'PLR_SPIDER_AMT')

		multiply DMG_AMT DMG_RED_AMT

		setdmg hit 1
		setdmg dmg DMG_AMT

	
	}


	if ( PARAM4 contains poison )
	{
		
		
		local OUT_DMG PARAM3
		multiply DMG_AMT 0.5
		setdmg hit 1
		setdmg dmg DMG_AMT
		returndata 0.5
	}
	else
	{
		
		if !$get(PARAM1,scriptvar,'NPC_IGNORES_ARMOR')
		multiply DMG_AMT DMG_REDUCT
		setdmg hit 1
		setdmg dmg DMG_AMT
		returndata DMG_REDUCT
	}
}

{ ext_hide_armor
	setmodelbody 0 0
}

{ ext_hide_armor_cl
	setmodelbody 0 0
}
