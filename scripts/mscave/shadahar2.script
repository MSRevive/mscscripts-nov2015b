{
	setvar NPC_IS_BOSS 1

	
	setvar ANIM_IDLE idle1
	setvard ANIM_WALK walk
	setvard ANIM_RUN walk 
	setvard ANIM_ATTACK attack1
	setvard ANIM_DEATH dieheadshot 

	
	setvard MOVE_RANGE 32
	setvard ATTACK_RANGE 64
	setvard ATTACK_HITRANGE 127
	setvar IS_UNHOLY 1
	setvar NO_STUCK_CHECKS 1 

	
	const ANIM_SWIPE attack1
	const ANIM_SMASH attack3
	const ANIM_CAST castspell
	setvard ANIM_MOVE walk
	setvard ATTACK_HITCHANCE 70

	
	const DMG_SWIPE $rand(100,150)
	const DMG_SMASH $rand(75,100)
	const DMG_FIRE_BOLT $rand(50,100)
	const ATTACH_IDX_WAND 0
	const ATTACH_IDX_EYE1 1
	const ATTACH_IDX_EYE2 2
	const DMG_BEAM 300
	const WAND_UNLIT 8
	const WAND_LIT 9

	
	const SOUND_STRUCK1 	weapons/cbar_hitbod1.wav
	const SOUND_STRUCK2 	weapons/cbar_hitbod2.wav
	const SOUND_STRUCK3 	weapons/cbar_hitbod3.wav
	const SOUND_STRUCK4 	zombie/zo_pain2.wav
	const SOUND_STRUCK5 	zombie/zo_pain2.wav
	const SOUND_ATTACK1 	zombie/claw_miss1.wav
	const SOUND_ATTACK2 	zombie/claw_miss2.wav
	const SOUND_DEATH		zombie/zo_pain1.wav 
	const SOUND_STUN		debris/glass2.wav
	const SOUND_BEAM_FIRE	debris/beamstart1.wav

	const SOUND_TURNED1		ambience/the_horror1.wav
	const SOUND_TURNED2		ambience/the_horror2.wav
	const SOUND_TURNED3		ambience/the_horror3.wav
	const SOUND_TURNED4		ambience/the_horror4.wav
	const SOUND_HOLY_STRIKE	doors/aliendoor1.wav

	const SOUND_LAUGH monsters/skeleton/cal_laugh.wav
	const SOUND_WARCRY monsters/skeleton/calrain3.wav

	const FREQ_SPECIAL $randf(10,15)

	precache bonegibs.mdl
}

{ game_precache
	precachefile monsters/summon/circle_of_death
	precachefile monsters/eye_drainer
}


#include monsters/base_monster

{ npc_spawn

	if ( $lcase(game.map.name) equals mscave )
	{
		setvard NPC_GIVE_EXP 10000
	}
	else
	{
		deleteent ent_me
	}

	if ( G_SHAD_PRESENT ) deleteent ent_me 

	name The|Remains of Shadahar

	setmodel monsters/skeleton_enraged.mdl
	setmodelbody 0 6
	setmodelbody 1 WAND_UNLIT
	width 32
	height 80

	if game.serverside

	race undead
	hp 6000
	roam 1
	hearingsensitivity 4
	nopush 1

	
	takedmg all .7
	takedmg slash .7
	takedmg pierce .5
	takedmg blunt 1.2
	takedmg holy 2.0
	takedmg cold 0.1
	takedmg poison 0.0

	saytextrange 1024

	
	local MY_SKY game.monster.origin
	vectoradd MY_SKY (0,0,4096)
	local MY_CENTER $get(ent_me,origin)
	vectoradd MY_CENTER $vec(0,0,-48)
	clientevent new all_in_sight effects/sfx_lightning MY_CENTER MY_SKY 1 1

	setvarg G_SHAD_PRESENT 1

	setvard MAX_EYES 2
	if ( game.players >= 4 ) setvard MAX_EYES 4

	callevent 0.1 init_beam1
}

{ npc_post_spawn
	
	if ( $filesize(trans2.dat) > -1 ) erasefile trans2.dat
}

{ init_beam1
	effect beam vector laserbeam.spr 30 $get(ent_me,attachpos,1) $get(ent_me,origin) (255,0,255) 0 10 -1
	setvard BEAM_EYE1 $get(ent_lastcreated,id)
	callevent 1.0 init_beam2
}

{ init_beam2
	effect beam vector laserbeam.spr 30 $get(ent_me,attachpos,2) $get(ent_me,origin) (255,0,255) 0 10 -1
	setvard BEAM_EYE2 $get(ent_lastcreated,id)
}

{  attack_1	

	setvard ATTACK_PUSH $relvel(-100,130,120)
	setvard SWIPE_ATTACK 1
   	playrandomsound 1 5 SOUND_ATTACK1 SOUND_ATTACK2 none none
   	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_SWIPE ATTACK_HITCHANCE
	if $rand(1,30) == 1
	setvard ANIM_ATTACK ANIM_SMASH
}

{  attack_3	

	setvard STUN_ATTACK 1
   	playrandomsound 1 5 SOUND_ATTACK1 SOUND_ATTACK2 none none
   	dodamage NPCATK_TARGET ATTACK_HITRANGE DMG_SWIPE ATTACK_HITCHANCE
	setvard ANIM_ATTACK ANIM_SWIPE
}

{ game_dodamage 

	if ( PARAM1 )
	{
		if ( SWIPE_ATTACK ) addvelocity ent_laststruckbyme ATTACK_PUSH
		setvard SWIPE_ATTACK 0

		if STUN_ATTACK
		playsound 0 10 SOUND_LAUGH
		applyeffect PARAM2 effects/effect_stun $randf(5,10) 1 1
	}
	setvard STUN_ATTACK 0
}

{ npc_post_spawn

	setvarg global.map.weather "fog_black;fog_black;flog_black"
	setvarg G_WEATHER_LOCK fog_black
	callexternal players ext_weather_change G_WEATHER_LOCK

	
	
	
	callevent 8.0 get_tele_points

	callexternal all bo_zombie_mode
	clientevent new all mscave/shadahar_cl
	setvard MY_CL_IDX game.script.last_sent_id
}

{ get_tele_points
	
	setvard MY_TELE_POINTS $get(GAME_MASTER,scriptvar,'SHAD_TELEPOINTS')
	setvard MY_TRIGGERS $get(GAME_MASTER,scriptvar,'SHAD_TRIGGERS')
	token.add MY_TELE_POINTS NPC_HOME_LOC
	token.add MY_TRIGGERS 'none'
	setvard N_TELE_POINTS $get_token_amt(MY_TELE_POINTS)
	subtract N_TELE_POINTS 1
	setvard CUR_TELE_POINT -1
	setvard SET_TELE_POINTS 1
}

{ [server] game_death
	effect beam update BEAM_EYE1 brightness 0
	effect beam update BEAM_EYE2 brightness 0
	effect beam update BEAM_EYE1 remove 0.1
	effect beam update BEAM_EYE2 remove 0.1
	usetrigger door_palace
	clientevent remove all MY_CL_IDX
	callevent bm_gold_spew 500 1 32 2 4
	setvarg G_WEATHER_LOCK 0
}

{ npcatk_targetvalidate

	if ( !DID_WARCRY )
	{
		setvard DID_WARCRY 1
		playsound 0 10 SOUND_LAUGH
	}

	if ( !STARTED_CYCLES )
	{
		setvard STARTED_CYCLES 1
		callevent FREQ_SPECIAL do_special
	}
}

{ game_damaged 

	if !TELEPORT_SEQUENCE

	if ( CUR_TELE_POINT < N_TELE_POINTS )
	{
		if $get(ent_me,hp) <= 3000
		callevent teleport_next_point
	}


	if ( TELEPORT_SEQUENCE )
	{
		setdmg dmg 0
		setdmg hit 0
		returndata 0 .0
	}

	if !TELEPORT_SEQUENCE

  	if ( PARAM3 isnot holy )
	{
		playrandomsound 2 10 SOUND_STRUCK1 SOUND_STRUCK2 SOUND_STRUCK3 SOUND_STRUCK4 SOUND_STRUCK5
	}
	else
	{
		playrandomsound 2 10 SOUND_HOLY_STRIKE
	}

}


{ teleport_next_point

	setvard TELEPORT_SEQUENCE 1

	if ( CUR_TELE_POINT == -1 ) saytext Let's see just how well you know these caves...
	if ( CUR_TELE_POINT == 0 ) saytext Time for more hide and seek...
	if ( CUR_TELE_POINT == 1 ) saytext You seem to know these caves pretty well.
	if ( CUR_TELE_POINT == 2 ) saytext I'm sure you can find me again...
	if ( CUR_TELE_POINT == 3 ) saytext My my, such a persistant seeker.
	if ( CUR_TELE_POINT == 4 ) saytext Such fun! But can you find me again?
	if ( CUR_TELE_POINT == 5 ) saytext Come find me on the bridge of fire...
	if ( CUR_TELE_POINT == 6 ) saytext My defeat shall not come about so easily as this...
	
	playsound 0 10 ambience/particle_suck2.wav
	callevent npcatk_clear_targets
	callevent npcatk_suspend_ai
	playanim critical ANIM_CAST
	setidleanim ANIM_CAST
	setmoveanim ANIM_CAST
	effect screenfade all 2 1 (255,255,255) 255 fadeout
	callevent 2.0 teleport_fx_fadein
	add CUR_TELE_POINT 1
	setanim.framerate 0.5
	gravity 0

	

	callevent 0.01 float_up
	callevent 2.01 teleport_next_point2
}

{ teleport_fx_fadein
	effect screenfade all 2 1 (255,255,255) 255 fadein
}


{ float_up
	if TELEPORT_SEQUENCE
	callevent 0.1 float_up
	local CUR_POS $get(ent_me,origin)
	vectoradd CUR_POS z 1
	setorigin ent_me CUR_POS
}

{ teleport_next_point2

	playsound 0 10 SOUND_LAUGH
	setanim.framerate 1
	setvard TELEPORT_SEQUENCE 0
	setorigin ent_me $get_token(MY_TELE_POINTS,CUR_TELE_POINT)
	hp 6000
	blind 0
	callevent npcatk_resume_ai
	callevent npcatk_clear_targets
	gravity 1
	callevent stay_still

	

	setvard CUR_TRIG $get_token(MY_TRIGGERS,CUR_TELE_POINT)
	usetrigger CUR_TRIG

	if ( CUR_TRIG equals 'spawn_firereaver' )
	{
		
		callevent do_fireballs
		setvard ANIM_WALK idle1
		setvard ANIM_RUN idle1
		callevent stay_still
		setvarg global.map.weather "fog_red;fog_red;fog_red"
		setvarg G_WEATHER_LOCK fog_red
		callexternal players ext_weather_change G_WEATHER_LOCK
	}
	if ( CUR_TELE_POINT == N_TELE_POINTS )
	{
		
		setvard STAY_NEAR_HOME 1
		setvard ANIM_MOVE run
		setvard ANIM_RUN ANIM_MOVE
		setvard ANIM_WALK walk
		setvarg global.map.weather "clear;clear;clear"
		setvarg G_WEATHER_LOCK 0
		callexternal players ext_weather_change clear
	}
}

{ do_special

	if $get(ent_me,isalive)
	callevent FREQ_SPECIAL do_special

	if !DOING_SPECIAL
	if !TELEPORT_SEQUENCE

	if $get(NPCATK_TARGET,isalive)

	local PICK_SPECIAL $rand(1,2)
	if ( N_EYES < MAX_EYES ) local PICK_SPECIAL $rand(1,3)

	

	if ( PICK_SPECIAL == 1 ) callevent do_circles
	if ( PICK_SPECIAL == 2 ) callevent do_eyebeams
	if ( PICK_SPECIAL == 3 ) callevent do_summon_eye
}

{ do_circles

	setvard DOING_SPECIAL 1
	playsound 0 10 SOUND_LAUGH

	playanim critical ANIM_CAST

	getplayersnb PLAYER_LIST
	setvard N_CIRC_PLAYERS $get_token_amt(PLAYER_LIST)
	setvard PLAYER_ORGS ''
	calleventloop N_CIRC_PLAYERS ids_to_pos
	subtract N_CIRC_PLAYERS 1

	setvard CIRCLES_ON 0
	callevent wand_sparkles
	callevent 1.0 do_circles2
}

{ wand_sparkles

	if !CIRCLES_ON
	callevent 0.1 wand_sparkles
	clientevent update all MY_CL_IDX wand_prep_cl $get(ent_me,attachpos,ATTACH_IDX_WAND) (255,0,0)
}

{ do_circles2

	setvard CIRCLES_ON 1

	setvard CIRC_PLAYER_IDX 0

	setvard DOING_SPECIAL 0
	callevent seal_players
}

{ ids_to_pos
	local CUR_PLAYER $get_token(PLAYER_LIST,game.script.iteration)
	local CUR_PLAYER_ORG $get(CUR_PLAYER,origin)
	token.add PLAYER_ORGS CUR_PLAYER_ORG
	
}

{ seal_players

	local CUR_PLAYER_ORG $get_token(PLAYER_ORGS,CIRC_PLAYER_IDX)

	if ( $dist(game.monster.origin,PLAYER_ORGS) < 800 )
	{
		local CIRC_POS CUR_PLAYER_ORG
		vectorset CIRC_POS z $get_ground_height(CIRC_POS)
		createnpc monsters/summon/circle_of_death CIRC_POS $get(ent_me,id) 10.0 100
	}

	if CIRC_PLAYER_IDX < N_CIRC_PLAYERS
	add CIRC_PLAYER_IDX 1
	callevent 0.1 seal_players
}

{ do_eyebeams

	setvard DOING_SPECIAL 1
	playanim critical ANIM_CAST
	setmovedest none
	callevent npcatk_suspend_ai
	setidleanim ANIM_CAST
	setmoveanim ANIM_CAST
	setvard NO_STUCK_CHECKS 1
	blind 1
	roam 0
	setvard EYE_BEAM_WARMUP 1
	setvard MY_YAW $get(ent_me,angles.yaw)
	callevent eye_warmup_loop
	callevent 2.0 do_eyebeams2
}

{ eye_warmup_loop
	if EYE_BEAM_WARMUP
	callevent 0.1 eye_warmup_loop
	subtract MY_YAW 1
	if ( MY_YAW < 0 ) local MY_YAW 359.99
	setangle face $vec(0,MY_YAW,0)
	clientevent update all MY_CL_IDX eye_beam_prep_cl $get(ent_me,attachpos,ATTACH_IDX_EYE1) $get(ent_me,attachpos,ATTACH_IDX_EYE2)
}

{ do_eyebeams2

	setvard EYE_BEAM_WARMUP 0 

	playsound 0 10 SOUND_BEAM_FIRE
	svplaysound 1 10 ambience/zapmachine.wav

	effect beam update BEAM_EYE1 brightness 128
	effect beam update BEAM_EYE2 brightness 128

	
	
	
	
	

	
	setanim.framerate 0

	setvard BEAM_COUNT 0
	setvard MY_YAW $get(ent_me,angles.yaw)
	callevent 0.1 do_eyebeam_cycle
}

{ do_eyebeam_cycle

	if $get(ent_me,isalive)

	add MY_YAW 1
	if ( MY_YAW > 359.99 ) subtract MY_YAW 359.99
	setangle face $vec(0,MY_YAW,0)
	


	local BEAM1_START $get(ent_me,attachpos,ATTACH_IDX_EYE1)
	local BEAM2_START $get(ent_me,attachpos,ATTACH_IDX_EYE2)
	local BEAM1_END BEAM1_START
	local BEAM2_END BEAM2_START
	vectoradd BEAM1_END $relpos($vec(0,MY_YAW,0),$vec(0,1000,-200))
	vectoradd BEAM2_END $relpos($vec(0,MY_YAW,0),$vec(0,1000,-200))
	local BEAM1_END $get_traceline(BEAM1_START,BEAM1_END,worldonly)
	local BEAM2_END $get_traceline(BEAM2_START,BEAM2_END,worldonly)
	effect beam update BEAM_EYE1 points BEAM1_START BEAM1_END
	effect beam update BEAM_EYE2 points BEAM2_START BEAM2_END
	xdodamage BEAM1_START BEAM1_END DMG_BEAM 100% ent_me ent_me none dark
	xdodamage BEAM2_START BEAM2_END DMG_BEAM 100% ent_me ent_me none dark

	add BEAM_COUNT 1
	if ( TELEPORT_SEQUENCE ) setvard BEAM_COUNT 120 
	if ( BEAM_COUNT == 120 )
	{
		callevent do_eyebeam_end
	}
	else
	{
		callevent 0.1 do_eyebeam_cycle
	}
}

{ do_eyebeam_end
	if ( !TELEPORT_SEQUENCE )
	{
		setidleanim ANIM_IDLE
		setmoveanim ANIM_MOVE
	}

	if ( AM_MOBILE ) roam 1

	setanim.framerate 1.0
	svplaysound 1 0 ambience/zapmachine.wav
	callevent npcatk_resume_ai
	effect beam update BEAM_EYE1 brightness 0
	effect beam update BEAM_EYE2 brightness 0
	setvard DOING_SPECIAL 0
	if ( TELEPORT_SEQUENCE )
	{
		
		playanim critical ANIM_CAST
		setidleanim ANIM_CAST
		setmoveanim ANIM_CAST
	}
}

{ do_summon_eye
	callevent npcatk_suspend_ai
	playanim critical ANIM_CAST
	playsound 0 10 SOUND_LAUGH
	setmodelbody 1 WAND_LIT

	local RND_ANG $randf(0,359)
	local SUMMON_ADJ $relpos($vec(0,RND_ANG,0),$vec(0,64,32))
	setvard SUMMON_POS game.monster.origin
	vectoradd SUMMON_POS SUMMON_ADJ
	
	local BEAM_TOP SUMMON_POS
	local BEAM_BOTTOM SUMMON_POS
	vectorset BEAM_BOTTOM z $get_ground_height(SUMMON_POS)
	vectoradd BEAM_TOP z 1024
	effect beam vector lgtning.spr 200 BEAM_BOTTOM BEAM_TOP (64,64,255) 200 50 3.0

	callevent 1.5 do_summon_eye2
	setvard SUMMON_WARMUP 1
	callevent 0.01 summon_warmup_loop
}

{ summon_warmup_loop
	if SUMMON_WARMUP
	callevent 0.1 summon_warmup_loop
	clientevent update all MY_CL_IDX wand_prep_cl $get(ent_me,attachpos,ATTACH_IDX_WAND) (0,0,255)
}

{ do_summon_eye2
	setvard SUMMON_WARMUP 0
	setmodelbody 1 WAND_UNLIT
	callevent npcatk_resume_ai

	createnpc monsters/eye_drainer SUMMON_POS $get(ent_me,id)
	add N_EYES 1
}

{ eye_died
	subtract N_EYES 1
}

{ do_fireballs
	if CUR_TRIG equals 'spawn_firereaver'
	callevent 1.0 do_fireballs
	
	movespeed 0.0
	setmoveanim idle1
	setidleanim idle1
	if $get(NPCATK_TARGET,isalive)
	if $get(NPCATK_TARGET,range) < 800

	tossprojectile NPCATK_TARGET 400 DMG_FIRE_BOLT 2 proj_fire_xolt (0,8,72)
}

{ npcatk_hunt

	if ( STAY_NEAR_HOME )
	{
		
		if !$get(NPCATK_TARGET,isalive)
		if game.time > LAST_RETURN_TIME
		setmovedest NPC_HOME_LOC 10
		setvard LAST_RETURN_TIME game.time
		add LAST_RETURN_TIME 2.0
	}
	if !STAY_NEAR_HOME
	if !$get(NPCATK_TARGET,isalive)
	if AM_MOBILE
	callevent stay_still
}

{ npcatk_clear_targets
	callevent stay_still
}

{ npc_targetsighted

	if ( !DID_ORC_COMMENT )
	{
		if SET_TELE_POINTS
		if CUR_TELE_POINT == 0
		if $get(NPCATK_TARGET,range) < 768
		setvard DID_ORC_COMMENT 1
		if ( game.players > 1 ) local PRO_NOUN yourselves.
		if ( game.players == 1 ) local PRO_NOUN yourself.
		saytext Filthy Orcs! More useful dead than alive. Much like PRO_NOUN
	}

	if !AM_MOBILE
	callevent resume_movement
}

{ stay_still
	roam 0
	setvard AM_MOBILE 0
	setmoveanim idle1
	setidleanim idle1
	movespeed 0.0
}

{ resume_movement
	roam 1
	setvard AM_MOBILE 1
	setmoveanim ANIM_MOVE
	setidleanim idle1
	movespeed 1.0
}
