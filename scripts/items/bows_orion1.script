{
  
	const MODEL_VIEW_IDX	3
	const MODEL_VIEW       	viewmodels/v_bows.mdl
  const MODEL_HANDS      	weapons/p_weapons2.mdl
  const MODEL_WORLD      	weapons/p_weapons2.mdl
  const MODEL_WEAR		weapons/p_weapons2.mdl
  const SOUND_SHOOT      	weapons/bow/bow.wav
  const ITEM_NAME		longbow
  const ANIM_PREFIX		longbow
	const BASE_LEVEL_REQ 15
	const MODEL_BODY_OFS 48
	

	const BOLT_SPRITE nhth1.spr

	const CUSTOM_ATTACK 1 

  
  
  const RANGED_AIMANGLE		(0,3,0)

	const MP_DRAIN 4
	const CHARGE_RATE 0.3 
	const DMG_MULTI 10 

	const NOT_WEARABLE 1
}

#include items/bows_base

{ bow_spawn

   name 	an|Orion Bow
   desc 	This enchanted bow fires spheres of pure mana
   weight 	30
   size 	1
   value	1000
   sethudsprite trade 108

	setvard BOLT_SIZE 0

	callevent custom_register
}

{ custom_register

   local reg.attack.type		charge-throw-projectile
   local reg.attack.keys		+attack1
   local reg.attack.hold_min&max	0.1;0.1
   local reg.attack.dmg.type		magic
   local reg.attack.range		200
   local reg.attack.energydrain	1
   local reg.attack.stat		archery
   local reg.attack.COF			0
   local reg.attack.projectile		proj_mana2
   local reg.attack.priority		0
   local reg.attack.delay.strike	0
   local reg.attack.delay.end		0
   local reg.attack.ofs.startpos	RANGED_STARTPOS
   local reg.attack.ofs.aimang		RANGED_AIMANGLE
	local reg.attack.ammodrain	0
   local reg.attack.callback		ranged
   local reg.attack.noise		1000
	local reg.attack.reqskill BASE_LEVEL_REQ

	
}


{ game_deploy
	setvard NEXT_ATTACK game.time
	add NEXT_ATTACK 1.0
}

{ game_attack1_down
	if game.time > NEXT_ATTACK
	setvard NEXT_ATTACK game.time
	add NEXT_ATTACK 0.2
	if !AM_CHARGING
	setvard AM_CHARGING 1
  	playviewanim  ANIM_STRETCH
  	
	if game.serverside
	callexternal MY_OWNER set_mana_dart 0 0
  	playowneranim critical bow_pull global.anim.combatwalk
	if ( !TALLY_ACTIVE )
	{
		setvard TALLY_ACTIVE 1
		callevent tally_stretch
	}
}

{ [server] tally_stretch

	if TALLY_ACTIVE
	callevent 0.1 tally_stretch

	if AM_CHARGING

	if game.time > NEXT_CHARGE
	setvard NEXT_CHARGE game.time
	add NEXT_CHARGE CHARGE_RATE

	local BOLT_DMG BOLT_SIZE
	multiply BOLT_DMG DMG_MULTI

	if !MAX_LEVEL
	add BOLT_SIZE 1

	if ( !BALOON_ON )
	{
		if BOLT_SIZE > 0
		setvard BALOON_ON 1
		
		
		
		clientevent new all items/bows_orion1_cl $get(ent_owner,index)
		setvard BOW_CL_IDX game.script.last_sent_id
	}
	else
	{
		clientevent update all BOW_CL_IDX add_charge
	}
	


	if ( $get(ent_owner,mp) <= MP_DRAIN )
	{
		setvard MAX_LEVEL 1
		dplayermessage ent_owner Orion Bow: Insufficient Mana
		
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if ( BOLT_SIZE >= 10 )
	{
		setvard BOLT_SIZE 10
		setvard MAX_LEVEL 1
	}
	if ( MAX_LEVEL ) playermessage Manaball has reached maximum charge

	givemp $neg(MP_DRAIN)
	callexternal ent_owner mana_drain 

	svplaysound 1 3 ambience/alien_humongo.wav

	local BOLT_DMG BOLT_SIZE
	multiply BOLT_DMG DMG_MULTI

	callexternal ent_owner set_mana_dart BOLT_SIZE BOLT_DMG
}

{ game_-attack1

	

	if AM_CHARGING

	setvard NEXT_ATTACK game.time
	add NEXT_ATTACK 0.2

	playviewanim ANIM_FIRE

	if ( game.serverside )
	{
		clientevent update all BOW_CL_IDX charge_release 
		callexternal ent_owner ext_tossprojectile view 200 0 0 proj_mana2 (0,20,28)

	}

	callevent reset_atk
}

{ game_fall
	callevent reset_atk
}

{ reset_atk

	
	setvard BALOON_ON 0
	setvard AM_CHARGING 0
	setvard BOLT_SIZE 0
	setvard MAX_LEVEL 0
	setvard TALLY_ACTIVE 0
	if game.serverside
	callexternal $get(ent_owner,id) set_mana_dart 0 0
}

{ game_putinpack
	callevent cancel_attack
}

{ cancel_attack 
	if AM_CHARGING
	clientevent update all BOW_CL_IDX charge_release 
	callevent reset_atk
}
