{
	const BASE_LEVEL_REQ 35

	const STUN_BURST_MP 40

	const MELEE_RANGE 90
	const MELEE_DMG_DELAY  0.5
	const MELEE_ATK_DURATION 1.1
	const DEMON_DMG_DELAY  0.25
	const DEMON_ATK_DURATION 0.7
	const MELEE_ENERGY 2
	const MELEE_DMG 600
	const MELEE_DMG_RANGE 40
	const MELEE_ACCURACY 80%
	const MELEE_PARRY_AUGMENT 20%
	const MELEE_DMG_TYPE fire

	const MODEL_VIEW viewmodels/v_2hblunts.mdl
	const MODEL_VIEW_IDX 12

	const MODEL_WORLD weapons/p_weapons4.mdl
	const MODEL_BODY_OFS 34
	const ANIM_PREFIX standard
}

#include items/blunt_base_twohanded

{  weapon_spawn

   name  The|Fire Breaker
   desc  An Earth Breaker imbued with elemental fire
   weight  80
   size  10
   value 7000
   sethudsprite hand hammer
   sethudsprite trade 194
}

{ game_deploy
	setvard GAME_PVP game.pvp
}

{ melee_damaged_other
	if ( $get(PARAM1,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	if $get(ent_owner,relationship,PARAM1) equals enemy

	local L_DOT $get(ent_owner,skill.spellcasting.fire)
	multiply L_DOT 0.5
	applyeffect PARAM1 effects/effect_burn 5.0 $get(ent_owner,id) L_DOT
}

{ special_02_strike
	local ATTACK_END_POS PARAM2
	
	if ( $get(ent_owner,mp) < STUN_BURST_MP )
	{
		dplayermessage ent_owner "Fire Breaker: Insufficient Mana for Fire Burst"
		local EXIT_SUB 1
	}
	if !EXIT_SUB

	vectorset ATTACK_END_POS z $get_ground_height(ATTACK_END_POS)

	givemp ent_owner $neg(STUN_BURST_MP)
	clientevent new all effects/sfx_fire_burst ATTACK_END_POS 250 1 (255,128,0)
	setvard BURST_POS ATTACK_END_POS
	vectoradd BURST_POS 32

	if ( $get(ent_owner,skill.spellcasting.fire) > 20 )
	{
		callexternal ent_owner ext_fissure
	}
	

	
	setvard BURST_STUN_DURATION $get(ent_owner,skill.bluntarms)
	setvard BURST_DMG BURST_STUN_DURATION
	multiply BURST_STUN_DURATION 0.5
	multiply BURST_DMG 4.0

	setvard DOT_FIRE $get(ent_owner,skill.spellcasting.fire)
	
	
	
	xdodamage BURST_POS 128 BURST_DMG 0 ent_owner ent_me spellcasting.fire fire_effect dmgevent:*burst
}

{ burst_dodamage
	local CUR_TARG PARAM2 
	if $get(CUR_TARG,relationship,ent_owner) equals enemy
	if ( $get(CUR_TARG,isplayer) )
	{
		if !GAME_PVP
		local EXIT_SUB 1
	}
	if !EXIT_SUB
	applyeffect CUR_TARG effects/effect_stun BURST_STUN_DURATION 0 1 $get(ent_owner,id)
	if ( DOT_FIRE >= 10 ) applyeffect CUR_TARG effects/effect_burn 5.0 $get(ent_owner,id) DOT_FIRE
	local TARG_ORG $get(CUR_TARG,origin)
	local MY_ORG BURST_POS
	local NEW_YAW $angles(MY_ORG,TARG_ORG)
	addvelocity CUR_TARG $relvel($vec(0,NEW_YAW,0),$vec(0,800,110))
	
}
