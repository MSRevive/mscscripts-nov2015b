#scope server

{
	setvar HAS_INCLUDE_VENDOR 1

	const STORE_RESTOCK 1
	const STORE_RESTOCK_TIME_LO 300
	const STORE_RESTOCK_TIME_HI 600
	const BUSY_COMMENT "One at a time, please."
}

#include help/first_vendor

{
   
   if( STORE_TRADEEXT equals 'STORE_TRADEEXT' )		setvard STORE_TRADEEXT 	 trade
   if( STORE_BUYMENU equals 'STORE_BUYMENU' )		setvard STORE_BUYMENU 	 1
}
{  game_spawn

	
	catchspeech npc_say_store store trade buy sell purchase sale offer

	callevent 0.1 vendor_setup_store

	if game.monster.race equals human
	setmodelbody 0 3
}

{ vendor_setup_store
	if !VEND_INDIVIDUAL
	npcstore.create STORE_NAME

	callevent 1.0 vendor_addstoreitems

}

{ npc_spawn
	if game.monster.race equals human
	setmodelbody 0 3
}

{ game_menu_getoptions

	callevent vendor_addstoremenu PARAM1
}

{ vendor_addstoremenu

	if !VENDOR_MENU_OFF

	local reg.mitem.id	  genericstore
	local reg.mitem.priority  -100
	local reg.mitem.access	  all

	if( !STORE_CLOSED )
	{
		if VEND_CHAT_MODE isnot weapons
		local reg.mitem.title	"Shop"
		local reg.mitem.type	callback
		local reg.mitem.callback vendor_offerstore
	}
	else
	{
		local reg.mitem.title 	Closed
		local reg.mitem.type 	disabled
	}
	
	menuitem.register
}

  
{  game_playerused
   
	callevent vendor_used
	if !VENDOR_NOT_ON_USE
	callevent vendor_offerstore $get(ent_lastused,id)
}

  
{  npc_say_store
	callevent vendor_offerstore $get(ent_lastspoke,id)
}

   
   
{  vendor_offerstore		


	if !VENDOR_STORE_ACTIVE

	setvard VENDOR_STORE_ACTIVE 1

	if( !STORE_CLOSED )
	{
		setvard VENDOR_TARGET PARAM1
		if ( VENDOR_DELAY > 0 )
		{
			
			callevent VENDOR_DELAY basevendor_offerstore
		}
		else
		{
			callevent basevendor_offerstore
		}
	}
	else
	{
		callevent vendor_say_closed
		setvard VENDOR_STORE_ACTIVE 0
	}
}

{ vendor_fail
	setvard VENDOR_STORE_ACTIVE 0
}

{ basevendor_offerstore	

	if ( PARAM1 isnot 'PARAM1' ) setvard VENDOR_TARGET PARAM1

   local L_SERVICE 0
   if( STORE_BUYMENU ) setvard L_SERVICE buy
   if( STORE_SELLMENU ) stradd L_SERVICE ;sell

   
   if ( SELL_WEAPON_LEVEL > 0 ) gplayermessage VENDOR_TARGET This vendor's weapons require proficiency level SELL_WEAPON_LEVEL

	if ( !VEND_INDIVIDUAL )
	{
		npcstore.offer STORE_NAME VENDOR_TARGET L_SERVICE trade
	}
	else
	{
		
		if ( VEND_PREFIX equals 'VEND_PREFIX' )
		{
			setvard VEND_PREFIX STORE_NAME
			array.create ARRAY_STORES
		
		}
		local L_TARGET_STORE VEND_PREFIX
		stradd L_TARGET_STORE $get(VENDOR_TARGET,steamid)
		if ( $get_arrayfind(ARRAY_STORES,L_TARGET_STORE) > -1 )
		{
		
			npcstore.offer L_TARGET_STORE VENDOR_TARGET L_SERVICE trade
		}
		else
		{
			helptip	VENDOR_TARGET generic "Individualized Store" "This merchant shows a seperate inventory for each player."
			array.add ARRAY_STORES L_TARGET_STORE
			setvard STORE_NAME L_TARGET_STORE
			npcstore.create STORE_NAME
			callevent vendor_addstoreitems
			npcstore.offer STORE_NAME VENDOR_TARGET L_SERVICE trade
		}
	}
}



{ trade_busy
	if $get(PARAM1,isalive)
	if $get(PARAM1,range) < 256
	saytext BUSY_COMMENT
}

{ trade_success
	setvard VENDOR_STORE_ACTIVE 0
}

{ vendor_store_reset
	npcstore.remove STORE_NAME allitems
	callevent 0.1 vendor_addstoreitems
}

{ vendor_clear
	npcstore.remove STORE_NAME allitems
}

{ game_confirm_buy 


	local PLAYER_ID PARAM1
	local ITEM_NAME PARAM2
	local ITEM_SCRIPT PARAM3
	local ITEM_TYPE PARAM4
	local ITEM_STAT skill.
	stradd ITEM_STAT PARAM4

	

	saytext I do not think you yet have the skill to use a ITEM_NAME properly.
	saytext You might consider buying one of my other ITEM_TYPE items instead.
	setvard VEND_NO_GOODBYE 1 

	if ( SELL_WEAPON_LEVEL > 0 )
	{
		if $lcase(game.map.name) isnot edana
		local MSG_TEXT "This vendor offers weapons requiring levels "
		stradd MSG_TEXT SELL_WEAPON_LEVEL
		infomsg PARAM1 "VENDOR LEVEL"
	}

	if ( HAS_BASE_CHAT_INCLUDE ) callevent bchat_mouth_move
	if ( HAS_BASE_CHAT_ARRAY_INCLUDE ) callevent chat_mouth_move
}
