//statotionary, temporary sprite (why is there no function for this?)
//- remember to precache sprite
#scope client
{ client_activate //<origin> <sprite_name> <render_props> <duration|once>
	//render props format: fadeout:0|1;scale;renderamt;rendermode;rendercolor;framerate;frames

	local SPRITE_POS PARAM1
	local SPRITE_NAME PARAM2
	setvard RENDER_PROPS PARAM3
	setvard SPRITE_DURATION PARAM4

	dbg *** $currentscript params: SPRITE_POS SPRITE_NAME RENDER_PROPS SPRITE_DURATION
	
	if ( SPRITE_DURATION isnot once )
	{
		cleffect tempent sprite SPRITE_NAME SPRITE_POS setup_temp_sprite
		local F_DURATION SPRITE_DURATION
		add F_DURATION 0.1
		callevent F_DURATION remove_me
	}
	else
	{
		cleffect tempent sprite SPRITE_NAME SPRITE_POS setup_temp_sprite update_temp_sprite
		add F_DURATION 20.0 //max duration just in case
		callevent F_DURATION remove_me
		dbg *** $currentscript playonce started @ game.time
	}
}

{ update_temp_sprite
	if game.tempent.frame >= $get_token(RENDER_PROPS,6)
	callevent remove_me
}

{ remove_me
	dbg *** $currentscript playonce removed @ game.time
	removescript
}

{ setup_temp_sprite

	dbg *** $currentscript renderprops: sc $get_token(RENDER_PROPS,1) ra $get_token(RENDER_PROPS,2) rm $get_token(RENDER_PROPS,3) rc $get_token(RENDER_PROPS,4) fr $get_token(RENDER_PROPS,5) fn $get_token(RENDER_PROPS,6)

	if ( SPRITE_DURATION isnot once ) cleffect tempent set_current_prop death_delay SPRITE_DURATION
	if ( $get_token(RENDER_PROPS,0) == 1 ) cleffect tempent set_current_prop fadeout lifetime
	cleffect tempent set_current_prop scale $get_token(RENDER_PROPS,1)
	cleffect tempent set_current_prop renderamt $get_token(RENDER_PROPS,2)
	cleffect tempent set_current_prop rendermode $get_token(RENDER_PROPS,3)
	cleffect tempent set_current_prop rendercolor $get_token(RENDER_PROPS,4)
	cleffect tempent set_current_prop framerate $get_token(RENDER_PROPS,5)
	cleffect tempent set_current_prop frames $get_token(RENDER_PROPS,6)
	cleffect tempent set_current_prop gravity 0
	cleffect tempent set_current_prop collide none
}